/////                                                  \\\\\
///// Common variables                                 \\\\\
/////                                                  \\\\\

OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~
OUTER_SPRINT tra_path ~EET_Tweaks/lang~

OUTER_SET valid_answer = 0
OUTER_SET button = 0
OUTER_SPRINT value ~dummy~

/////                                                  \\\\\
///// Check --args-list                                \\\\\
/////                                                  \\\\\

OUTER_SET ARGS_check = 0
OUTER_SET number = 0

OUTER_WHILE (ARGS_check = 0) BEGIN
	ACTION_IF VARIABLE_IS_SET EVAL ~%argv[%number%]%~ BEGIN
		OUTER_SPRINT ARGS_var EVAL ~%argv[%number%]%~
		OUTER_INNER_PATCH ARGS_var BEGIN
			INNER_PATCH_SAVE ~ARGS_var~ ~%ARGS_var%~ BEGIN
				REPLACE_EVALUATE ~\([0-9]+\)-\([0-9]+\)[-]*\([0-9]*\)~ BEGIN
					PATCH_IF (IS_AN_INT MATCH1) AND (IS_AN_INT MATCH2) BEGIN
						PATCH_IF (MATCH1 = "%COMPONENT_NUMBER%") BEGIN
							SET button = MATCH2
							PATCH_IF IS_AN_INT MATCH3 BEGIN
								SET value = MATCH3
								PATCH_PRINT ~Component %COMPONENT_NUMBER%, chosen option: %MATCH2%, chosen value: %MATCH3%~
							END ELSE BEGIN
								PATCH_PRINT ~Component %COMPONENT_NUMBER% chosen option: %MATCH2%~
							END
							SET ARGS_check = 1
						END
					END
				END
				~~
			END
		END
		OUTER_SET number = (number + 1)
	END ELSE BEGIN
		OUTER_SET ARGS_check = 1
	END
END

/////                                                  \\\\\
///// HANDLE_CHARSETS                                  \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ASSOCIATIVE_ARRAY charsetsTable BEGIN
	"cs_cz" => "CP1250"
	"de_de" => "CP1252"
	"en_us" => "CP1252"
	"es_es" => "CP1252"
	"fr_fr" => "CP1252"
	"it_it" => "CP1252"
	"ja_jp" => "CP932"
	"ko_kr" => "CP949"
	"pl_pl" => "CP1250"
	//"pt_br" => ""
	"ru_ru" => "CP1251"
	//"tr_tr" => ""
	//"uk_ua" => ""
	//"zh_cn" => ""
END
ACTION_DEFINE_ARRAY charsetsConvertArray BEGIN 0 2 10 areas END
//ACTION_DEFINE_ARRAY charsetsReloadArray BEGIN 0 2 10 END
LAF ~HANDLE_CHARSETS~
	INT_VAR
	infer_charsets = 0
	STR_VAR
	tra_path = "EET_Tweaks/lang"
	default_language = en_US
	charset_table = charsetsTable
	convert_array = charsetsConvertArray
	reload_array = charsetsReloadArray
END
