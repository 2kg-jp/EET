OUTER_SPRINT option ~~
OUTER_WHILE NOT (IS_AN_INT %option%) || (option > 2) || (option < 1) BEGIN
	PRINT ~Choose GAME_IS platform to append eet parameter to:
 1. Baldur's Gate 2 / Throne of Bhaal
 2. Siege of Dragonspear~
	ACTION_READLN option
END

PRINT ~~
PRINT ~Patching chapters...~

OUTER_SET cntcurrent = 0
OUTER_SET ignore_cnt = 0
OUTER_SET patch_cnt = 0
OUTER_SPRINT log ~~
ACTION_IF (option = 1) BEGIN
	OUTER_SPRINT game ~bg2~
	OUTER_SPRINT platformMATCH1 "AUTO_EVAL_STRINGS

ALWAYS
	OUTER_SET bg2_chapter = 0
	ACTION_IF (GAME_IS ~eet~) AND (FILE_CONTAINS ~override/EET.flag~ ~EET_JOURNAL~) BEGIN
		OUTER_SET bg2_chapter = 14
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END
"
END ELSE BEGIN
	OUTER_SPRINT game ~sod~
	OUTER_SPRINT platformMATCH1 "AUTO_EVAL_STRINGS

ALWAYS
	OUTER_SET sod_chapter = 0
	ACTION_IF (GAME_IS ~eet~) AND (FILE_CONTAINS ~override/EET.flag~ ~EET_JOURNAL~) BEGIN
		OUTER_SET sod_chapter = 7
	END
	OUTER_FOR (i=1; i<=7; i=i+1) BEGIN
		OUTER_SET sod_chapter = sod_chapter + 1
		OUTER_SPRINT name_source ~sod_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = sod_chapter
	END
"
END

COPY - ~EET_modConverter/Filelist.txt~ ~.../Filelist.txt~
	REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
	COUNT_REGEXP_INSTANCES ~^Input[/\\]+~ cntrow
	REPLACE_EVALUATE CASE_INSENSITIVE ~^Input[/\\]+\(.+\)$~ BEGIN
		SET cntcurrent = cntcurrent + 1
		INNER_PATCH_SAVE ~patch_name~ ~%MATCH1%~ BEGIN
			REPLACE_TEXTUALLY ~\\~ ~/~
		END
		INNER_PATCH_SAVE ~patch_ext~ ~%patch_name%~ BEGIN
			REPLACE_TEXTUALLY ~^.+\.~ ~~
		END
		INNER_PATCH_SAVE ~patch_dir~ ~%patch_name%~ BEGIN
			REPLACE_TEXTUALLY ~[^/]+$~ ~~
		END
		//PATCH_PRINT ~patch_name = %patch_name%; ;patch_dir = %patch_dir%; patch_ext = %patch_ext%~
		SET patched = 0
		INNER_ACTION BEGIN
			ACTION_IF ~%patch_ext%~ STRING_EQUAL_CASE ~BAF~ OR
				~%patch_ext%~ STRING_EQUAL_CASE ~D~ OR
				~%patch_ext%~ STRING_EQUAL_CASE ~TP2~ OR
				~%patch_ext%~ STRING_EQUAL_CASE ~TPA~ OR
				~%patch_ext%~ STRING_EQUAL_CASE ~TPH~ OR
				~%patch_ext%~ STRING_EQUAL_CASE ~TPP~ BEGIN
				COPY - ~EET_modConverter/Input/%patch_name%~ ~.../Output/%patch_name%~
					REPLACE_TEXTUALLY ~IncrementGlobal~ ~IncrementReplaceMe~ //we don't want to patch this one
					REPLACE_EVALUATE CASE_INSENSITIVE ~\([A-Za-z]*G[A-Za-z]*("CHAPTER","GLOBAL",\)\([0-9]+\))~ BEGIN //this should take care of SetGlobal, Global, GlobalGT, GlobalLT, SG, G, GGT, GLT
						PATCH_IF (patched = 0) BEGIN
							PATCH_PRINT ~%cntcurrent%/%cntrow% Patching: %patch_name%...~
						END
						SPRINT log_temp ~Patching %patch_name%: %MATCH1%%MATCH2%) => %MATCH1%%%game%_chapter_%MATCH2%%)~
						SPRINT log ~%log%%log_temp%%LNL%~
						PATCH_PRINT ~%log_temp%~
						SET patched = 1
					END
					~%MATCH1%%%game%_chapter_%MATCH2%%)~
					REPLACE_TEXTUALLY ~IncrementReplaceMe~ ~IncrementGlobal~
			END
			ACTION_IF (patched = 0) BEGIN
				OUTER_SET ignore_cnt = ignore_cnt + 1
				PRINT ~%cntcurrent%/%cntrow% Ignoring patching: %patch_name%...~
			END ELSE BEGIN
				MKDIR ~EET_modConverter/Output/%patch_dir%~
				COPY ~.../Output/%patch_name%~ ~EET_modConverter/Output/%patch_name%~
				OUTER_SET patch_cnt = patch_cnt + 1
			END
		END
	END
	~~

ACTION_IF (patch_cnt > 0) BEGIN
	COPY - ~EET_modConverter/Filelist.txt~ ~.../Filelist.txt~
		REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^Input[/\\]+\(.+\.tp2\)$~ BEGIN
			INNER_PATCH_SAVE ~patch_name~ ~%MATCH1%~ BEGIN
				REPLACE_TEXTUALLY ~\\~ ~/~
			END
			INNER_PATCH_SAVE ~patch_dir~ ~%patch_name%~ BEGIN
				REPLACE_TEXTUALLY ~[^/]+$~ ~~
			END
			SET patched = 0
			INNER_ACTION BEGIN
				ACTION_IF NOT FILE_EXISTS ~EET_modConverter/Output/%patch_name%~ BEGIN
					OUTER_SPRINT tp2_path ~Input~
				END ELSE BEGIN
					OUTER_SPRINT tp2_path ~Output~
				END
				COPY - ~EET_modConverter/%tp2_path%/%patch_name%~ ~.../Output/%patch_name%~
					COUNT_REGEXP_INSTANCES CASE_SENSITIVE ~AUTHOR~ valid
					PATCH_IF (valid > 0) BEGIN
						REPLACE_TEXTUALLY ~AUTO_EVAL_STRINGS~ ~~
						COUNT_REGEXP_INSTANCES ~^[ 	]*ALWAYS[ 	]*.*[ 	]*$~ num_matches
						PATCH_IF (num_matches > 0) BEGIN
							REPLACE_EVALUATE CASE_SENSITIVE ~\(^[ 	]*ALWAYS\)~ BEGIN
								PATCH_IF (patched = 0) BEGIN
									SPRINT MATCH1 ~%platformMATCH1%~
									SET patched = 1
									SPRINT log_temp ~Patching %patch_name%: ALWAYS command expanded%LNL%%MATCH1%~
									SPRINT log ~%log%%log_temp%%LNL%~
									PATCH_PRINT ~%log_temp%~
								END
							END
							~%MATCH1%~
						END ELSE BEGIN
							REPLACE_EVALUATE CASE_SENSITIVE ~^\([ 	]*LANGUAGE\)~ BEGIN
								PATCH_IF (patched = 0) BEGIN
									SPRINT MATCH1 ~%platformMATCH1%END%LNL%%LNL%%MATCH1%~
									SET patched = 1
									SPRINT log_temp ~Patching %patch_name%: ALWAYS command added%LNL%%MATCH1%~
									SPRINT log ~%log%%log_temp%%LNL%~
									PATCH_PRINT ~%log_temp%~
								END
							END
							~%MATCH1%~
							PATCH_IF (patched = 0) BEGIN
								REPLACE_EVALUATE CASE_SENSITIVE ~^\([ 	]*BEGIN\)~ BEGIN
									PATCH_IF (patched = 0) BEGIN
										SPRINT MATCH1 ~%platformMATCH1%END%LNL%%LNL%%MATCH1%~
										SET patched = 1
										SPRINT log_temp ~Patching %patch_name%: ALWAYS command added%LNL%%MATCH1%~
										SPRINT log ~%log%%log_temp%%LNL%~
										PATCH_PRINT ~%log_temp%~
									END
								END
								~%MATCH1%~
							END
						END
					END
				ACTION_IF (patched = 1) BEGIN
					ACTION_IF (~%tp2_path%~ STRING_EQUAL_CASE ~Input~) BEGIN
						OUTER_SET patch_cnt = patch_cnt + 1
						OUTER_SET ignore_cnt = ignore_cnt - 1
					END
					MKDIR ~EET_modConverter/Output/%patch_dir%~
					COPY ~.../Output/%patch_name%~ ~EET_modConverter/Output/%patch_name%~
					//OUTER_SET tp2_cnt = tp2_cnt + 1
				END
			END
		END
		~~
END

PRINT ~Conversion complete. Statistics:
%patch_cnt% files patched.
%ignore_cnt% files ignored.~
