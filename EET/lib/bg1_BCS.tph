/////                                                  \\\\\
///// Re-open and clean SoD areas                      \\\\\
/////                                                  \\\\\

//WIP

/////                                                  \\\\\
///// BGSUNRIS/BGSUNSET movies                         \\\\\
/////                                                  \\\\\

<<<<<<<< .../BGSUNRIS_BGSUNSET-eb.baf
IF
  Time(DUSK_END)
  !GlobalTimerNotExpired("K#TimerSunset","GLOBAL")
THEN
  RESPONSE #100
    StartMovie("BGSUNSET")
    SetGlobalTimer("K#TimerSunset","GLOBAL",ONE_HOUR)
END

IF
  Time(DAWN_END)
  !GlobalTimerNotExpired("K#TimerSunrise","GLOBAL")
THEN
  RESPONSE #100
    StartMovie("BGSUNRIS")
    SetGlobalTimer("K#TimerSunrise","GLOBAL",ONE_HOUR)
END
>>>>>>>>

ACTION_BASH_FOR ~%patch_dir%/are~ ~^.+\.are$~ BEGIN
	COPY + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_FILESPEC%~
		READ_BYTE 0x48 flags
		PATCH_IF (flags BAND BIT0) AND (flags BAND BIT1) AND (flags BAND BIT3) AND (flags BAND BIT6) BEGIN
			INNER_ACTION BEGIN
				EXTEND_BOTTOM ~%BASH_FOR_RES%.BCS~ ~.../BGSUNRIS_BGSUNSET-eb.baf~
				PRINT ~BGSUNRIS/BGSUNSET movies added to %BASH_FOR_RES%.BCS~
			END
		END
END

/////                                                  \\\\\
///// workaround for engine differences                \\\\\
/////                                                  \\\\\

//BG2:EE can't run script on 'Unselectable' NPCs, so resurrection in BP1 is broken

//BPHUBA.BCS
<<<<<<<< .../BPHUBA-eb.baf
IF
  GlobalLT("T3_B5","GLOBAL",1)
  HitBy([PC],CRUSHING)
  OR(2)
    AreaCheck("OH9310")
    AreaCheck("OH9350")
THEN
  RESPONSE #100
    ClearAllActions()
    StartCutSceneMode()
    SetGlobal("ResistedBaeloth","GLOBAL",2)
    StartCutScene("CUTBP05")
END
>>>>>>>>

EXTEND_BOTTOM ~BPHUBA.BCS~ ~.../BPHUBA-eb.baf~

//BPPLOT.BCS
<<<<<<<< .../BPPLOT-rbClear.baf
IF
  OR(2)
  SpellCastOnMe([ANYONE],CLERIC_RESURRECTION)
  SpellCastOnMe([ANYONE],CLERIC_RAISE_DEAD)
  UnselectableVariableGT(0)
THEN
  RESPONSE #100
    Wait(1)
    ChangeEnemyAlly(Myself,PC)
    MakeUnselectable(0)
    MoveToOffset([0.0])
    SetSequence(SEQ_AWAKE)
    Continue()
END

IF
  OR(2)
  AreaCheck("OH9350")
  AreaCheck("OH9310")
  Global("BATTLE_COMPLETE","GLOBAL",0)
  UnselectableVariableGT(0)
THEN
  RESPONSE #100
    PlayDeadInterruptible(9999)
END

IF
  Global("BATTLE_COMPLETE","GLOBAL",0)
  UnselectableVariableGT(0)
  HPGT(Myself,1)
THEN
  RESPONSE #100
    ApplyDamagePercent(Myself,100,CRUSHING)
END

IF
  AreaCheck("OH9360")
  OR(2)
  !Allegiance(Myself,PC)
  UnselectableVariableGT(0)
THEN
  RESPONSE #100
    ChangeEnemyAlly(Myself,PC)
    MakeUnselectable(0)
    Continue()
END

IF
  GlobalGT("BATTLE_COMPLETE","GLOBAL",0)
  !Allegiance(Myself,PC)
  OR(2)
  AreaCheck("OH9350")
  AreaCheck("OH9310")
THEN
  RESPONSE #100
    ChangeEnemyAlly(Myself,PC)
    Continue()
END

IF
  GlobalGT("BATTLE_COMPLETE","GLOBAL",0)
  UnselectableVariableGT(0)
THEN
  RESPONSE #100
    Wait(1)
    MakeUnselectable(0)
    MoveToOffset([0.0])
    SetSequence(SEQ_AWAKE)
    Continue()
END

IF
  Global("BATTLE_COMPLETE","GLOBAL",0)
  GlobalLT("T3_B5","GLOBAL",1)
  HPLT(Myself,2)
  OR(10)
  StateCheck(Myself,STATE_CHARMED)
  StateCheck(Myself,STATE_BERSERK)
  StateCheck(Myself,STATE_PANIC)
  StateCheck(Myself,STATE_STUNNED)
  StateCheck(Myself,STATE_HELPLESS)
  StateCheck(Myself,STATE_SILENCED)
  StateCheck(Myself,STATE_CONFUSED)
  StateCheck(Myself,STATE_FEEBLEMINDED)
  StateCheck(Myself,STATE_DISEASED)
  StateCheck(Myself,STATE_BLIND)
THEN
  RESPONSE #100
    ApplySpellRES("BPDISPEL",Myself)
    MakeUnselectable(5000)
    ChangeEnemyAlly(Myself,EVILBUTBLUE)
    PlayDeadInterruptible(9999)
END

IF
  OR(2)
  AreaCheck("OH9360")
  !AreaCheckAllegiance(ENEMY)
  OR(10)
  StateCheck(Myself,STATE_CHARMED)
  StateCheck(Myself,STATE_BERSERK)
  StateCheck(Myself,STATE_PANIC)
  StateCheck(Myself,STATE_STUNNED)
  StateCheck(Myself,STATE_HELPLESS)
  StateCheck(Myself,STATE_SILENCED)
  StateCheck(Myself,STATE_CONFUSED)
  StateCheck(Myself,STATE_FEEBLEMINDED)
  StateCheck(Myself,STATE_DISEASED)
  StateCheck(Myself,STATE_BLIND)
THEN
  RESPONSE #100
    ApplySpellRES("BPDISPEL",Myself)
END

IF
  Global("BATTLE_COMPLETE","GLOBAL",0)
  GlobalLT("T3_B5","GLOBAL",1)
  HPLT(Myself,2)
  UnselectableVariable(0)
  OR(2)
  AreaCheck("OH9350")
  AreaCheck("OH9310")
THEN
  RESPONSE #100
    MakeUnselectable(5000)
    ChangeEnemyAlly(Myself,EVILBUTBLUE)
    PlayDeadInterruptible(9999)
END
>>>>>>>>

COPY_EXISTING ~BPPLOT.BCS~ ~override~
	REPLACE_BCS_BLOCK ~.../BPPLOT-rbClear.baf~ ~.../blank.baf~ ON_MISMATCH PATCH_WARN ~WARNING: could not find REPLACE_BCS_BLOCK matching .../BPPLOT-rbClear.baf in %SOURCE_FILE%.~ END
BUT_ONLY

//OH9310.BCS, OH9350.BCS
<<<<<<<< .../OH9310-rbOld.baf
IF
  Delay(5)
  Global("BPISDEAD","OH9310",0)
  GlobalLT("T3_B5","GLOBAL",1)
  OR(2)
  !HPGT(Player1,1)
  Allegiance(Player1,EVILBUTBLUE)
  OR(2)
  !HPGT(Player2,1)
  Allegiance(Player2,EVILBUTBLUE)
  OR(2)
  !HPGT(Player3,1)
  Allegiance(Player3,EVILBUTBLUE)
  OR(2)
  !HPGT(Player4,1)
  Allegiance(Player4,EVILBUTBLUE)
  OR(2)
  !HPGT(Player5,1)
  Allegiance(Player5,EVILBUTBLUE)
  OR(2)
  !HPGT(Player6,1)
  Allegiance(Player6,EVILBUTBLUE)
THEN
  RESPONSE #100
    SetGlobal("BPISDEAD","OH9310",1)
    StartMovie("DEATHAND")
    GoToStartScreen()
END

IF
  Delay(5)
  Global("BPISDEAD","OH9310",0)
  GlobalGT("T3_B5","GLOBAL",0)
  !HPGT(Player1,0)
  !HPGT(Player2,0)
  !HPGT(Player3,0)
  !HPGT(Player4,0)
  !HPGT(Player5,0)
  !HPGT(Player6,0)
THEN
  RESPONSE #100
    SetGlobal("BPISDEAD","OH9310",1)
    StartMovie("DEATHAND")
    GoToStartScreen()
END

IF
  HPLT(Player1,2)
  OR(3)
  StateCheck(Player1,STATE_CHARMED)
  StateCheck(Player1,STATE_PANIC)
  StateCheck(Player1,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player1,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player1,MakeUnselectable(5000))
    ActionOverride(Player1,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player1,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player2,2)
  OR(3)
  StateCheck(Player2,STATE_CHARMED)
  StateCheck(Player2,STATE_PANIC)
  StateCheck(Player2,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player2,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player2,MakeUnselectable(5000))
    ActionOverride(Player2,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player2,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player3,2)
  OR(3)
  StateCheck(Player3,STATE_CHARMED)
  StateCheck(Player3,STATE_PANIC)
  StateCheck(Player3,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player3,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player3,MakeUnselectable(5000))
    ActionOverride(Player3,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player3,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player4,2)
  OR(3)
  StateCheck(Player4,STATE_CHARMED)
  StateCheck(Player4,STATE_PANIC)
  StateCheck(Player4,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player4,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player4,MakeUnselectable(5000))
    ActionOverride(Player4,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player4,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player5,2)
  OR(3)
  StateCheck(Player5,STATE_CHARMED)
  StateCheck(Player5,STATE_PANIC)
  StateCheck(Player5,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player5,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player5,MakeUnselectable(5000))
    ActionOverride(Player5,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player5,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player6,2)
  OR(3)
  StateCheck(Player6,STATE_CHARMED)
  StateCheck(Player6,STATE_PANIC)
  StateCheck(Player6,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player6,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player6,MakeUnselectable(5000))
    ActionOverride(Player6,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player6,PlayDeadInterruptible(9999))
    Continue()
END
>>>>>>>>

<<<<<<<< .../OH93x0-rbNew.baf
IF
  NumInPartyAlive(0)
THEN
  RESPONSE #100
    StartMovie("DEATHAND")
    GoToStartScreen()
END
>>>>>>>>

COPY_EXISTING ~OH9310.BCS~ ~override~
	REPLACE_BCS_BLOCK ~.../OH9310-rbOld.baf~ ~.../OH93x0-rbNew.baf~ ON_MISMATCH PATCH_WARN ~WARNING: could not find REPLACE_BCS_BLOCK matching .../OH9310-rbOld.baf in %SOURCE_FILE%.~ END
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(StateCheck(Player6,STATE_REALLY_DEAD)[%newline%]*THEN[%newline%]*RESPONSE #100[%newline%]*SetGlobal("BATTLE_COMPLETE","GLOBAL",2)[%newline%]*\)SetGlobal("BP_AUTOSAVE","GLOBAL",0)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1
    SetGlobal("BP_AUTOSAVE","GLOBAL",1)
    SetGlobal("BAELOTH_FACING","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

<<<<<<<< .../OH9350-rbOld.baf
IF
  Delay(5)
  Global("BPISDEAD","OH9350",0)
  OR(2)
  !HPGT(Player1,1)
  Allegiance(Player1,EVILBUTBLUE)
  OR(2)
  !HPGT(Player2,1)
  Allegiance(Player2,EVILBUTBLUE)
  OR(2)
  !HPGT(Player3,1)
  Allegiance(Player3,EVILBUTBLUE)
  OR(2)
  !HPGT(Player4,1)
  Allegiance(Player4,EVILBUTBLUE)
  OR(2)
  !HPGT(Player5,1)
  Allegiance(Player5,EVILBUTBLUE)
  OR(2)
  !HPGT(Player6,1)
  Allegiance(Player6,EVILBUTBLUE)
THEN
  RESPONSE #100
    SetGlobal("BPISDEAD","OH9350",1)
    StartMovie("DEATHAND")
    GoToStartScreen()
END

IF
  HPLT(Player1,2)
  OR(3)
  StateCheck(Player1,STATE_CHARMED)
  StateCheck(Player1,STATE_PANIC)
  StateCheck(Player1,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player1,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player1,MakeUnselectable(5000))
    ActionOverride(Player1,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player1,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player2,2)
  OR(3)
  StateCheck(Player2,STATE_CHARMED)
  StateCheck(Player2,STATE_PANIC)
  StateCheck(Player2,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player2,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player2,MakeUnselectable(5000))
    ActionOverride(Player2,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player2,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player3,2)
  OR(3)
  StateCheck(Player3,STATE_CHARMED)
  StateCheck(Player3,STATE_PANIC)
  StateCheck(Player3,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player3,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player3,MakeUnselectable(5000))
    ActionOverride(Player3,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player3,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player4,2)
  OR(3)
  StateCheck(Player4,STATE_CHARMED)
  StateCheck(Player4,STATE_PANIC)
  StateCheck(Player4,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player4,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player4,MakeUnselectable(5000))
    ActionOverride(Player4,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player4,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player5,2)
  OR(3)
  StateCheck(Player5,STATE_CHARMED)
  StateCheck(Player5,STATE_PANIC)
  StateCheck(Player5,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player5,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player5,MakeUnselectable(5000))
    ActionOverride(Player5,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player5,PlayDeadInterruptible(9999))
    Continue()
END

IF
  HPLT(Player6,2)
  OR(3)
  StateCheck(Player6,STATE_CHARMED)
  StateCheck(Player6,STATE_PANIC)
  StateCheck(Player6,STATE_BERSERK)
THEN
  RESPONSE #100
    ActionOverride(Player6,ApplySpell(Myself,FORCE_DISPEL_MAGIC))
    ActionOverride(Player6,MakeUnselectable(5000))
    ActionOverride(Player6,ChangeEnemyAlly(Myself,NOTGOOD))
    ActionOverride(Player6,PlayDeadInterruptible(9999))
    Continue()
END
>>>>>>>>

COPY_EXISTING ~OH9350.BCS~ ~override~
	REPLACE_BCS_BLOCK ~.../OH9350-rbOld.baf~ ~.../OH93x0-rbNew.baf~ ON_MISMATCH PATCH_WARN ~WARNING: could not find REPLACE_BCS_BLOCK matching .../OH9350-rbOld.baf in %SOURCE_FILE%.~ END
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(StateCheck(Player6,STATE_REALLY_DEAD)[%newline%]*THEN[%newline%]*RESPONSE #100[%newline%]*SetGlobal("BATTLE_COMPLETE","GLOBAL",2)[%newline%]*SetGlobal("BP_AUTOSAVE","GLOBAL",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1
    SetGlobal("BAELOTH_FACING","GLOBAL",1)
    TriggerActivation("GREASETRAP",FALSE)
    TriggerActivation("ARROWTRAP1",FALSE)
    TriggerActivation("ARROWTRAP2",FALSE)
    TriggerActivation("ARROWTRAP3",FALSE)
    TriggerActivation("ARROWTRAP4",FALSE)
    TriggerActivation("ARROWTRAP5",FALSE)
    TriggerActivation("ARROWTRAP6",FALSE)
    TriggerActivation("STINKYTRAP",FALSE)
    TriggerActivation("JERKTRAP",FALSE)
    SetGlobal("BPTRAP01_TRIGGERED","OH9350",0)
    SetGlobal("BPTRAP03_TRIGGERED","OH9350",0)
    SetGlobal("BPTRAP04_TRIGGERED","OH9350",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/////                                                  \\\\\
///// BG:EE movies restoration                         \\\\\
/////                                                  \\\\\

//BG0705.BCS
COPY_EXISTING ~BG0705.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		COUNT_REGEXP_INSTANCES ~StartMovie("TAVERN")~ num_matches
	END
BUT_ONLY
ACTION_IF (num_matches = 0) BEGIN
<<<<<<<< .../BG0705-et.baf
IF
  Global("EnteredElfsong","GLOBAL",0)
THEN
  RESPONSE #100
    StartMovie("TAVERN")
    SetGlobal("EnteredElfsong","GLOBAL",1)
END
>>>>>>>>
	EXTEND_TOP ~BG0705.BCS~ ~.../BG0705-et.baf~
END

//BG3300.BCS
COPY_EXISTING ~BG3300.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~SetGlobal("EnteredBeregost","GLOBAL",1)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		COUNT_REGEXP_INSTANCES ~StartMovie("BEREGOST")~ num_matches2
		PATCH_IF (num_matches2 > 0) BEGIN
		END ELSE PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~StartMovie("BEREGOST")
    SetGlobal("EnteredBeregost","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END	ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/////                                                  \\\\\
///// Chapter strings                                  \\\\\
/////                                                  \\\\\

COPY_EXISTING ~CUTCKEEP.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("CHPTXT6")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000029)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("CHPTXT6")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~BDCUT10.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("chptxt8")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000032)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("chptxt8")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~BD7100.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("chptxt9")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000033)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("chptxt9")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~BD3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("chptxt10")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000034)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("chptxt10")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~BD4000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("chptxt11")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000035)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("chptxt11")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~BD4400.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("chptxt12")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000036)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("chptxt12")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~BDCUT61.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementChapter("chptxt13")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			SET strref = RESOLVE_STR_REF (@2000037)
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementChapter("chptxt13")
    AddJournalEntry(%strref%,INFO)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/////                                                  \\\\\
///// added by BG:EE                                   \\\\\
/////                                                  \\\\\

//BDBALDUR.BCS

COPY_EXISTING ~BDBALDUR.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~FamiliarSummoner~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Player1~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

<<<<<<<< .../BDBALDUR-eb.baf
//code from BG:EE
IF
  Global("TanariXP","GLOBAL",1)
  Dead("DeathK")  // ~Demon Knight~
  Dead("loupgar")  // ~Loup Garou~
  Global("FinishedExpansion","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("FinishedExpansion","GLOBAL",1)
    AddJournalEntry(@24117,INFO)
END

IF
  Global("HasTranzigScroll","GLOBAL",0)
  PartyHasItem("SCRL2X")  // ~Letter~
THEN
  RESPONSE #100
    SetGlobal("HasTranzigScroll","GLOBAL",1)
    AddJournalEntry(@31449,QUEST)
END
>>>>>>>>

EXTEND_BOTTOM ~BDBALDUR.BCS~ ~.../BDBALDUR-eb.baf~
EXTEND_TOP ~BDBALDUR.BCS~ ~.../timers-et.baf~ //via bg2_BCS.tph

//BDCUT08.BCS, BDDEBUG.BCS
COPY_EXISTING ~BDCUT08.BCS~ ~override~
	~BDDEBUG.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~SetWorldmap("SODMAP")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetWorldmap("WORLDMAP")
    //Can be visited + Has been visited
    RemoveWorldmapAreaFlag("BG0100",12)
    RemoveWorldmapAreaFlag("BG0200",12)
    RemoveWorldmapAreaFlag("BG0300",12)
    RemoveWorldmapAreaFlag("BG0400",12)
    RemoveWorldmapAreaFlag("BG0600",12)
    RemoveWorldmapAreaFlag("BG0700",12)
    RemoveWorldmapAreaFlag("BG0800",12)
    RemoveWorldmapAreaFlag("BG1100",12)
    RemoveWorldmapAreaFlag("BG1200",12)
    RemoveWorldmapAreaFlag("BG1300",12)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//BD0108.BCS
COPY_EXISTING ~BD0108.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~CreateCreature("MINSC7_",\[403\.571\],SE)[%newline%]*ApplySpellRES("bdbump1","MINSC")~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~CreateCreature("MINSC7_",[403.571],SE)
    ActionOverride("Minsc",MakeGlobalOverride())
    ApplySpellRES("bdbump1","MINSC")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~CreateCreature("dynahe7",\[244\.463\],SE)[%newline%]*SetGlobal("BD_DYNAHEIR","BD0108",1)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~CreateCreature("dynahe7",[244.463],SE)
    ActionOverride("Dynaheir",MakeGlobalOverride())
    SetGlobal("BD_DYNAHEIR","BD0108",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//BD0110.BCS
COPY_EXISTING ~BD0110.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~CreateCreature("safana7",\[889\.620\],N)[%newline%]*SetGlobal("BD_SPAWN_SAFANA","BD0110",1)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~CreateCreature("safana7",[889.620],N)
    ActionOverride("Safana",MakeGlobalOverride())
    SetGlobal("BD_SPAWN_SAFANA","BD0110",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//BD0120.BCS
<<<<<<<< .../BD0120-et.baf
IF
  Global("ENDOFBG1","GLOBAL",0)
  Global("K#NewGame","BD0120",0)
THEN
  RESPONSE #100
    SetGlobal("K#NewGame","BD0120",1)
    //SetGlobal("DREAM","GLOBAL",7)
    SetGlobal("ENDOFBG1","GLOBAL",1)
    SetGlobal("NEWGAME_SOD","GLOBAL",1)
    //SetGlobal("K#temp","GLOBAL",364)
    //AddGlobals("K#DaysPassed","K#temp")
    //AdvanceTime(ONE_YEAR)
    RemoveWorldmapAreaFlag("BG0900",ENABLED) //Reveal from linked area
    ReallyForceSpellRES("K#REMBHA",Player1)
    TakeItemListPartyNum("K#PLOT",99)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~BD0120.BCS~ ~.../BD0120-et.baf~

//BD6100.BCS
COPY_EXISTING ~BD6100.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		/*SPRINT textToReplace ~IncrementGlobal("bd_finale","bd6100",10)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementGlobal("bd_finale","bd6100",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END*/
		SPRINT textToReplace ~ContinueGame(FALSE)[%newline%]*EndCredits()~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~CreateCreatureObject("K#TELBGT",Player1,0,0,0)~//~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		/*SPRINT textToReplace ~PlaySound("dpjon156")[%newline%]*Wait(4)[%newline%]*EndCutSceneMode()~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Wait(4)
    EndCutSceneMode()
    CreateCreatureObject("K#TELBGT",Player1,0,0,0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END*/
	END
BUT_ONLY

//BD6200.BCS
COPY_EXISTING ~BD6200.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~CreateCreature("bdimoen",\[775\.705\],E)[%newline%]*ActionOverride("bdimoen",MakeGlobalOverride())~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~MoveGlobal("BD6200","Imoen2",[775.705])
    ApplySpellRES("bdresurr","Imoen2") // No such index
    SmallWait(1)
    ActionOverride("Imoen2",Face(E))
    ApplySpellRES("bdrejuve","Imoen2")
    ChangeEnemyAlly("Imoen2",NEUTRAL)
    ChangeSpecifics("Imoen2",ALLIES)
    ActionOverride("Imoen2",ChangeAIScript("BDIMOEN",OVERRIDE))
    ActionOverride("Imoen2",ChangeAIScript("",CLASS))
    ActionOverride("Imoen2",ChangeAIScript("",RACE))
    ActionOverride("Imoen2",ChangeAIScript("",GENERAL))
    ActionOverride("Imoen2",ChangeAIScript("",DEFAULT))
    ActionOverride("Imoen2",SetDialog("BDIMOEN"))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//BDSODTRN.BCS
COPY_EXISTING ~BDSODTRN.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~SetGlobal("EndOfBG1","GLOBAL",2)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("EndOfBG1","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~Wait(1)[%newline%]*StartMovie("ENDCRDI_")[%newline%]*FadeToColor([1.0],0)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("EndOfBG1","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//BG0125.BCS
COPY_EXISTING ~BG0125.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~GlobalLT("EndOfBG1","GLOBAL",2)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Global("EndOfBG1","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//BG0900.BCS
<<<<<<<< .../BG0900-et.baf
IF
  Global("K#Drawbridge","BG0900",0)
  GlobalGT("Chapter","GLOBAL",7)
THEN
  RESPONSE #100
    SetGlobal("K#Drawbridge","BG0900",1)
    OpenDoor("DoorDraw")
END
>>>>>>>>

EXTEND_TOP ~BG0900.BCS~ ~.../BG0900-et.baf~

//BG2600.BCS
<<<<<<<< .../BG2600-et.baf
//STWEAPON.2DA is not reliable for some reason
IF
  Global("K#NewGame","BG2600",0)
  !HasItem("staf01",Player1)
  !HasItemSlot(Player1,SLOT_WEAPON)
  !Class(Player1,MONK)
  CheckStatGT(Player1,5,STR)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player1,1,1,0)
    ActionOverride(Player1,FillSlot(SLOT_WEAPON))
    ActionOverride(Player1,EquipMostDamagingMelee())
    Continue()
END

IF
  Global("K#NewGame","BG2600",0)
  InPartyAllowDead(Player2)
  !HasItem("staf01",Player2)
  !HasItemSlot(Player2,SLOT_WEAPON)
  !Class(Player2,MONK)
  CheckStatGT(Player2,5,STR)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player2,1,1,0)
    ActionOverride(Player2,FillSlot(SLOT_WEAPON))
    ActionOverride(Player2,EquipMostDamagingMelee())
    Continue()
END

IF
  Global("K#NewGame","BG2600",0)
  InPartyAllowDead(Player3)
  !HasItem("staf01",Player3)
  !HasItemSlot(Player3,SLOT_WEAPON)
  !Class(Player3,MONK)
  CheckStatGT(Player3,5,STR)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player3,1,1,0)
    ActionOverride(Player3,FillSlot(SLOT_WEAPON))
    ActionOverride(Player3,EquipMostDamagingMelee())
    Continue()
END

IF
  Global("K#NewGame","BG2600",0)
  InPartyAllowDead(Player4)
  !HasItem("staf01",Player4)
  !HasItemSlot(Player4,SLOT_WEAPON)
  !Class(Player4,MONK)
  CheckStatGT(Player4,5,STR)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player4,1,1,0)
    ActionOverride(Player4,FillSlot(SLOT_WEAPON))
    ActionOverride(Player4,EquipMostDamagingMelee())
    Continue()
END

IF
  Global("K#NewGame","BG2600",0)
  InPartyAllowDead(Player5)
  !HasItem("staf01",Player5)
  !HasItemSlot(Player5,SLOT_WEAPON)
  !Class(Player5,MONK)
  CheckStatGT(Player5,5,STR)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player5,1,1,0)
    ActionOverride(Player5,FillSlot(SLOT_WEAPON))
    ActionOverride(Player5,EquipMostDamagingMelee())
    Continue()
END

IF
  Global("K#NewGame","BG2600",0)
  InPartyAllowDead(Player6)
  !HasItem("staf01",Player6)
  !HasItemSlot(Player6,SLOT_WEAPON)
  !Class(Player6,MONK)
  CheckStatGT(Player6,5,STR)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player6,1,1,0)
    ActionOverride(Player6,FillSlot(SLOT_WEAPON))
    ActionOverride(Player6,EquipMostDamagingMelee())
    Continue()
END

IF
  Global("K#NewGame","BG2600",0)
THEN
  RESPONSE #100
    SetGlobal("K#NewGame","BG2600",1)
    LeaveAreaLUAPanic("BG2600","",[1110.600],S)
    ActionOverride(Player1,LeaveAreaLUA("BG2600","",[1110.600],S))
    ActionOverride(Player2,LeaveAreaLUA("BG2600","",[1059.667],S))
    ActionOverride(Player3,LeaveAreaLUA("BG2600","",[1156.677],S))
    ActionOverride(Player4,LeaveAreaLUA("BG2600","",[1013.742],S))
    ActionOverride(Player5,LeaveAreaLUA("BG2600","",[1238.585],S))
    ActionOverride(Player6,LeaveAreaLUA("BG2600","",[1277.490],S))
    MultiPlayerSync()
    SmallWait(2)
    RemoveWorldmapAreaFlag("AR0020",ENABLED) //Reveal from linked area
    ReallyForceSpellRES("K#REMBHA",Player1)
    TakeItemListPartyNum("K#PLOT",99)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~BG2600.BCS~ ~.../BG2600-et.baf~

//TU0015.BCS
<<<<<<<< .../TU0015-et.baf
//STWEAPON.2DA is not reliable for some reason
IF
  Global("K#NewGame","TU0015",0)
  !HasItem("staf01",Player1)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player1,1,1,0)
    Continue()
END

IF
  Global("K#NewGame","TU0015",0)
  !HasItem("staf01",Player2)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player2,1,1,0)
    Continue()
END

IF
  Global("K#NewGame","TU0015",0)
  !HasItem("staf01",Player3)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player3,1,1,0)
    Continue()
END

IF
  Global("K#NewGame","TU0015",0)
  !HasItem("staf01",Player4)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player4,1,1,0)
    Continue()
END

IF
  Global("K#NewGame","TU0015",0)
  !HasItem("staf01",Player5)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player5,1,1,0)
    Continue()
END

IF
  Global("K#NewGame","TU0015",0)
  !HasItem("staf01",Player6)
THEN
  RESPONSE #100
    GiveItemCreate("staf01",Player6,1,1,0)
    Continue()
END

IF
  Global("K#NewGame","TU0015",0)
THEN
  RESPONSE #100
    SetGlobal("K#NewGame","TU0015",1)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~TU0015.BCS~ ~.../TU0015-et.baf~

/////                                                  \\\\\
/////existing in BG2:EE, replace them if not used      \\\\\
/////                                                  \\\\\

//FTOWBAZ.BCS, MTOWBAZ.BCS, NOBLBAZ.BCS, NOBWBAZ.BCS
COPY_EXISTING ~FTOWBAZ.BCS~ ~override~
	~MTOWBAZ.BCS~ ~override~//blank script in BG2:EE
	~NOBLBAZ.BCS~ ~override~
	~NOBWBAZ.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Global("DukeThanks","GLOBAL",1)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Global("Chapter","GLOBAL",7)
  Global("DukeThanks","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//FTOWBEZ.BCS, FTOWNAZ.BCS, MTOWBEZ.BCS, MTOWNAZ.BCS
COPY_EXISTING ~FTOWBEZ.BCS~ ~override~
	~FTOWNAZ.BCS~ ~override~
	~MTOWBEZ.BCS~ ~override~
	~MTOWNAZ.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~GlobalGT("Chapter","GLOBAL",2)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("Chapter","GLOBAL",2)
  GlobalLT("Chapter","GLOBAL",2)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//AJANTIS.BCS
//ALBERT.BCS
//ALORA.BCS
//AMNISH4.BCS
//AMNISNA.BCS
//ANDRIS.BCS
//ANGELO.BCS
//ANKHEGG.BCS
//ARREST.BCS
//ATTKALLY.BCS
//BANDCAP.BCS
//BANDIT.BCS
//BANDIT2.BCS
//BARESH.BCS
//BHOSTILE.BCS
//BLBEAR.BCS
//BOUNCER.BCS
//BRAGE.BCS
//BRBEAR.BCS
//CALDO.BCS
//CANTO.BCS
//CAPCUT02.BCS
//CH1CUT03.BCS
//CH1CUT04.BCS
//CH6CUT01.BCS
//CLKGONG.BCS
//CLKPRESS.BCS
//CORAN.BCS
//DABRON.BCS
//DAITEL.BCS
//DAVAEORN.BCS//assigned to DAVAEO.CRE, but not used
//DAVCUT01.BCS
//DEATHINI.BCS
//DEATHKNI.BCS
//DGANGER.BCS
//DGUARD.BCS
//DIGCUT01.BCS
//DIGCUT02.BCS
//DIGGER.BCS
//DOOMDUR.BCS
//DOPPLE.BCS
//DOPPSS.BCS
//DOPSPELL.BCS//assigned to DOPGRE01.CRE, but not used
//DOPZORL.BCS
//DRASUS.BCS
//DRIZZT.BCS//assigned to DRIZZT.CRE, but not used
//DRUID3.BCS
//DRYAD.BCS
//DYNAHEIR.BCS
//ELDOTH.BCS
//EMERSON.BCS
//ENDCH4.BCS
//ERIK.BCS
//FFHUNT.BCS
//FGOLEM.BCS
//FIGHTER2.BCS//?
//FLAM10.BCS
//FLAM10A.BCS
//FLAM2.BCS
//FLAM7.BCS
//FLAMSPEL.BCS
//FTOWBAZ.BCS//assigned to CENANNY.CRE, but contains only junk BG1 data
//FTOWBEZ.BCS
//FTOWNAZ.BCS
//GERVISSE.BCS
//GHASTD.BCS
//GNARL.BCS
//GNOLL5.BCS
//GNOLL5A.BCS
//GNOLLDR.BCS
//GNOLLP.BCS
//GOODDEAT.BCS
//GREED.BCS
//GREYWOLF.BCS
//GUARDDLG.BCS
//HAIRTOTH.BCS
//HALFOGRE.BCS
//HOBGOBA.BCS
//INFO01.BCS
//INFO08.BCS
//INFO14.BCS
//INITDLG3.BCS
//INITNEB.BCS
//INITREV.BCS
//INNKEEP.BCS
//ISLANDSI.BCS
//JELLSPA.BCS
//JONDALAR.BCS//assigned to JONDAL.CRE, but not used
//KAGAIN.BCS
//KAISHA.BCS
//KHALID.BCS
//KINGDEAD.BCS
//KIVAN.BCS//assigned to KIVAN/2/4.CRE, but not used
//KOBOLDA.BCS
//KOBOLDAL.BCS
//KOBOLDFL.BCS
//KOLVAR.BCS
//KRUMM.BCS
//LAUREL.BCS
//LIIA.BCS//assigned to LIIA.CRE, but not used
//MARL.BCS
//MELICAMP.BCS
//MONKEN.BCS
//MONTARON.BCS
//MTOWBAZ.BCS//assigned to MTOWN2, SERVANT, STAGE01, TOUR01, TOWNCR, TRAIN01, TRAINER1 but blank in BG2:EE
//MTOWBEZ.BCS
//MTOWNAZ.BCS
//MULAHEY.BCS
//NARCUT01.BCS
//NARLEN.BCS
//NEREID.BCS
//NOBWBAZ.BCS
//OGREMAGE.BCS
//OGRES.BCS
//OGRILLON.BCS
//PBEAR.BCS
//PHEOARCH.BCS
//PHEOGURD.BCS
//PHOENIX.BCS
//PRIDE.BCS
//PRISMCUT.BCS
//PUMBER.BCS
//QUAYLE.BCS
//RAIKEN.BCS
//RAMAZITH.BCS
//READER6.BCS
//REDBAKE.BCS
//RUFIE.BCS
//SAFANA.BCS
//SARRUN.BCS
//SCAR.BCS
//SEMAJ.BCS
//SENIYAD.BCS
//SHADOW.BCS
//SHANDAL.BCS
//SHARTEEL.BCS
//SHARTEL2.BCS
//SHIP.BCS
//SHOAL.BCS
//SIL.BCS
//SILKE.BCS
//SKELETB.BCS
//SMAREARN.BCS
//SPIDER.BCS
//TASLOIA.BCS
//TASLOIF.BCS
//TAZOK.BCS
//TENYA.BCS
//TEVEN.BCS
//THIEF.BCS
//THIEF4.BCS
//THIEVES.BCS
//TIAX.BCS
//TMOORLCH.BCS
//TRANZIG.BCS
//TRAPDIS.BCS
//TRPBLACK.BCS
//TRPGHAST.BCS
//TRPPAWN.BCS
//TRPWHITE.BCS
//TTARNOR.BCS
//TTBELT.BCS//disabled in BG2:EE, use BG1:EE version
//TTBRAN.BCS//disabled in BG2:EE, use BG1:EE version
//TTCUT01.BCS//disabled in BG2:EE, use BG1:EE version
//TTIMOEN.BCS//disabled in BG2:EE, use BG1:EE version
//TTJAHEIR.BCS//disabled in BG2:EE, use BG1:EE version
//TTMINSC.BCS//disabled in BG2:EE, use BG1:EE version
//TTWEAPON.BCS//disabled in BG2:EE, use BG1:EE version
//TTXAN.BCS//disabled in BG2:EE, use BG1:EE version
//WAITATTK.BCS
//WAITRUN.BCS
//WANDRUN.BCS
//WATCH3.BCS//assigned to WATCH3.CRE, but not used
//WATCHER.BCS//assigned to JONDAL/WATCH/WATCH2.CRE, but not used
//WDATTKBE.BCS
//WDATTWAT.BCS
//WDHOLDBE.BCS
//WDRUNBE.BCS
//WDRUNWAT.BCS
//WNTRWOLF.BCS
//WORG.BCS
//WTATTKBE.BCS
//WTHOLDBE.BCS
//WTRUNBE.BCS
//XVARTH.BCS
//XZAR.BCS
//YESLICK.BCS
//ZOMBIEB.BCS
//ZORL.BCS
