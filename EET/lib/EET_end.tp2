BACKUP ~EET/backup/end~
AUTHOR ~K4thos (swit)~

ALWAYS
	//WeiDU currently doesn't support custom USER_DIRECTORY constant variable
	OUTER_PATCH_SAVE USER_DIRECTORY ~%USER_DIRECTORY%~ BEGIN
		REPLACE_TEXTUALLY ~Baldur's Gate II - Enhanced Edition~ ~Baldur's Gate - Enhanced Edition Trilogy~
	END
END

LANGUAGE ~English~
	~en_US~
	~EET/lang/en_US/prompts.tra~
	~EET/lang/en_US/setup.tra~
LANGUAGE ~Polski (Polish)~
	~pl_PL~
	~EET/lang/pl_PL/prompts.tra~
	~EET/lang/pl_PL/setup.tra~
LANGUAGE ~Deutsch (German)~
	~de_DE~
	~EET/lang/de_DE/prompts.tra~
	~EET/lang/de_DE/setup.tra~
LANGUAGE ~French~
	~fr_FR~
	~EET/lang/fr_FR/prompts.tra~
	~EET/lang/fr_FR/setup.tra~
LANGUAGE ~Spanish~
	~es_ES~
	~EET/lang/es_ES/prompts.tra~
	~EET/lang/es_ES/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Update old saves to work with new installation   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @910000 //Update old EET saves to work with new installation
NO_LOG_RECORD
REQUIRE_COMPONENT ~EET/EET.tp2~ 0 ~You must have EET core component installed~
REQUIRE_PREDICATE (!FILE_CONTAINS ~override/EET.flag~ ~SAVES_UPDATED~) @910004 //Your saves have been already updated in this installation
REQUIRE_PREDICATE (!FILE_CONTAINS ~override/EET.flag~ ~TRA_EXPORTED~) @910005 //Saves created on old installation can't be updated because you have already overwritten exported TLK files with new ones via last component of this mod.
//REQUIRE_PREDICATE FILE_EXISTS ~%USER_DIRECTORY%/save/saves.tra~ @910006 //%USER_DIRECTORY%/save/saves.tra doesn't exist
//REQUIRE_PREDICATE FILE_EXISTS ~%USER_DIRECTORY%/save/saves.txt~ @910007 //%USER_DIRECTORY%/save/saves.txt doesn't exist

//workaround until WeiDU is updated: http://forums.pocketplane.net/index.php/topic,29530.0.html?PHPSESSID=7635c39a228aeaa622a46b76dfa5e287
ACTION_FOR_EACH ext IN tra txt BEGIN
	ACTION_IF NOT FILE_EXISTS ~%USER_DIRECTORY%/save/saves.%ext%~ BEGIN
		FAIL @910006 //%USER_DIRECTORY%/save/saves.tra doesn't exist
	END
END

OUTER_SET valid_answer = 0
OUTER_SET button = 0
OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //Yes
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //No
		FAIL @910008 //Skipping installation. Saves won't be updated.
	END ELSE BEGIN
		PRINT @910009 //This is entirely OPTIONAL component! Are you sure you want to update all saves located in %USER_DIRECTORY%/save directory? You should not do it if you're planning to finish your ongoing game on older EET installation. This component can't be uninstalled (requires manual steps to do so). 1] Yes 2] No (skips component installation)
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

INCLUDE ~EET/lib/EET_end_saves.tph~

ACTION_IF (!FILE_CONTAINS ~override/EET.flag~ ~SAVES_UPDATED~) BEGIN
	APPEND + ~EET.flag~ ~SAVES_UPDATED~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Alternative GUI (SoD)                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @910001 //Alternative EET GUI (SoD)
REQUIRE_COMPONENT ~EET/EET.tp2~ ~0~ @910003 //You must have EET core component installed

COPY ~EET/SoD_GUI~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// End installation merger                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @910002 //Finalise EET (last mod in install order)
REQUIRE_COMPONENT ~EET/EET.tp2~ ~0~ @910003 //You must have EET core component installed

CLEAR_ARRAYS

/////                                                  \\\\\
///// Prepare arrays, update 25DIALOG.2DA              \\\\\
/////                                                  \\\\\

INCLUDE ~EET/lib/EET_end.tph~

COPY_EXISTING - ~CAMPAIGN.2DA~ ~override~
	READ_2DA_ENTRIES_NOW campaign_tbl 32
	FOR (i=0; i < campaign_tbl; ++i) BEGIN
		READ_2DA_ENTRY_FORMER campaign_tbl i 0 campaign
		PATCH_PRINT ~Analyzing %campaign% campaign~
		READ_2DA_ENTRY_FORMER campaign_tbl i 4 file
		PATCH_IF (!VARIABLE_IS_SET $interdia_2da(~%file%~)) AND (FILE_EXISTS_IN_GAME ~%file%.2da~) BEGIN
			DEFINE_ASSOCIATIVE_ARRAY interdia_2da BEGIN ~%file%~ => ~~ END
		END
		READ_2DA_ENTRY_FORMER campaign_tbl i 30 file
		PATCH_IF (!VARIABLE_IS_SET $clastext_2da(~%file%~)) AND (FILE_EXISTS_IN_GAME ~%file%.2da~) BEGIN
			DEFINE_ASSOCIATIVE_ARRAY clastext_2da BEGIN ~%file%~ => ~~ END
		END
		READ_2DA_ENTRY_FORMER campaign_tbl i 31 file
		PATCH_IF (!VARIABLE_IS_SET $racetext_2da(~%file%~)) AND (FILE_EXISTS_IN_GAME ~%file%.2da~) BEGIN
			DEFINE_ASSOCIATIVE_ARRAY racetext_2da BEGIN ~%file%~ => ~~ END
		END
		READ_2DA_ENTRY_FORMER campaign_tbl i 11 file
		PATCH_IF (!VARIABLE_IS_SET $pdialog_2da(~%file%~)) AND (FILE_EXISTS_IN_GAME ~%file%.2da~) BEGIN
			DEFINE_ASSOCIATIVE_ARRAY pdialog_2da BEGIN ~%file%~ => ~~ END
			INNER_ACTION BEGIN
				COPY_EXISTING ~%file%.2da~ ~override~
					PRETTY_PRINT_2DA
					COUNT_2DA_COLS "cntcol"
					READ_2DA_ENTRIES_NOW pdialog cntcol
					FOR (j=0; j < pdialog; ++j) BEGIN
						READ_2DA_ENTRY_FORMER pdialog j 0 dv
						TO_UPPER dv
						READ_2DA_ENTRY_FORMER pdialog j 2 join
						TO_UPPER join
						SET join_exists = 0
						PATCH_IF (FILE_EXISTS_IN_GAME ~%join%.dlg~) BEGIN
							SET join_exists = 1
							PATCH_IF (!VARIABLE_IS_SET $table_dv(~%dv%~)) BEGIN
								DEFINE_ASSOCIATIVE_ARRAY table_dv BEGIN ~%dv%~ => ~%join%~ END
								DEFINE_ASSOCIATIVE_ARRAY table_append_dlg BEGIN ~%join%~ , ~~ => ~~ END
							END ELSE /*PATCH_IF (!VARIABLE_IS_SET $table_append_dlg(~%join%~))*/ BEGIN
								TEXT_SPRINT join_main $table_dv(~%dv%~)
								PATCH_IF (NOT ~%join%~ STRING_EQUAL_CASE ~%join_main%~) BEGIN
									DEFINE_ASSOCIATIVE_ARRAY table_append_dlg BEGIN ~%join%~ , ~%join_main%~ => ~~ END
								END
								SET_2DA_ENTRY j 2 cntcol ~%join_main%~
							END
						END
						PATCH_IF (~%file%~ STRING_EQUAL_CASE ~PDIALOG~) BEGIN
							READ_2DA_ENTRY_FORMER pdialog j 1 post
							READ_2DA_ENTRY_FORMER pdialog j 3 dream
							READ_2DA_ENTRY_FORMER pdialog j 4 25post
							READ_2DA_ENTRY_FORMER pdialog j 5 25join
							TO_UPPER 25join
							READ_2DA_ENTRY_FORMER pdialog j 6 25dream
							READ_2DA_ENTRY_FORMER pdialog j 7 25override
							PATCH_IF (join_exists = 1) AND (FILE_EXISTS_IN_GAME ~%25join%.dlg~) AND (NOT ~%25join%~ STRING_EQUAL_CASE ~%join%~) BEGIN
								DEFINE_ASSOCIATIVE_ARRAY table_append_dlg BEGIN ~%25join%~ , ~%join%~ => ~~ END
							END
							PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~25DIALOG.2DA~ ~^%dv% ~)) BEGIN
								INNER_ACTION BEGIN
									//PRINT ~Patching 25DIALOG.2DA: %dv% %25post% %join% %25dream%~
									APPEND ~25DIALOG.2DA~ ~%dv% %25post% %join% %25dream%~
								END
							END
							SET_2DA_ENTRY j 4 cntcol ~***~
							SET_2DA_ENTRY j 5 cntcol ~***~
							SET_2DA_ENTRY j 6 cntcol ~***~
							SET_2DA_ENTRY j 7 cntcol ~***~
						END
					END
					PRETTY_PRINT_2DA
				BUT_ONLY
			END
		END
	END

COPY_EXISTING ~BDDIALOG.2DA~ ~override~
	REPLACE_TEXTUALLY ~^IMOEN_ ~ ~IMOEN2 ~
BUT_ONLY

ACTION_PHP_EACH table_append_dlg AS file => var BEGIN
	PRINT "~%file%~ , ~%file_1%~ => ~%var%~"
END

/////                                                  \\\\\
///// Prepare dialogue arrays                          \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~newline~ ~%WNL%%LNL%%MNL%%TAB% ~

MKDIR ~EET/temp/append/dlg~

<<<<<<<< .../end.txt

END
>>>>>>>>

<<<<<<<< .../blank.d
BEGIN ~%file%~
>>>>>>>>

ACTION_PHP_EACH table_append_dlg AS file => var BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%file%.dlg~ BEGIN
		ACTION_IF ~%file_1%~ STRING_EQUAL_CASE ~~ BEGIN //source file
			COPY_EXISTING ~%file%.dlg~ ~override~
				READ_LONG 0x8 num_states
				SPRINT EVAL ~num_states_%file%~ ~%num_states%~
				DECOMPILE_AND_PATCH BEGIN
					PATCH_PRINT ~Preparing %file%.dlg source file (%num_states% states starting from 0)~
					REPLACE_EVALUATE CASE_SENSITIVE ~^\(IF .*  \)\(.*\)$~ BEGIN
						SPRINT trigger ~~
						PATCH_IF (~%MATCH2%~ STRING_CONTAINS_REGEXP ~"CHAPTER"~) AND (~%MATCH2%~ STRING_CONTAINS_REGEXP ~"ENDOFBG[1-2]"~) BEGIN
							SPRINT trigger ~%var%%LNL%~
						END
					END ~%MATCH1%%trigger%%MATCH2%~
				END
			BUT_ONLY
		END ELSE BEGIN //appended file
			COPY_EXISTING ~%file%.dlg~ ~EET/temp/append/dlg/%file%.d~
				READ_LONG 0x8 num_states
				SET num_states_src = EVAL ~num_states_%file_1%~
				PATCH_PRINT ~Appending %file%.dlg to %file_1%.dlg (%num_states% appended states starting from %num_states_src%)~
				SET EVAL ~num_states_%file_1%~ = num_states_src + num_states
				DEFINE_ASSOCIATIVE_ARRAY remapped_state BEGIN
					~%file%~ , ~%file_1%~ => ~%num_states_src%~
				END
				DEFINE_ASSOCIATIVE_ARRAY remapped_dlg BEGIN
					~%file%~ => ~%file_1%~
				END
				DECOMPILE_DLG_TO_D
				REPLACE_EVALUATE CASE_SENSITIVE ~^\(IF .*  \)\(.*\)$~ BEGIN
					SPRINT trigger ~~
					PATCH_IF (~%MATCH2%~ STRING_CONTAINS_REGEXP ~"CHAPTER"~) AND (~%MATCH2%~ STRING_CONTAINS_REGEXP ~"ENDOFBG[1-2]"~) BEGIN
						SPRINT trigger ~%var%%LNL%~
					END
				END ~%MATCH1%%trigger%%MATCH2%~
				REPLACE_TEXTUALLY CASE_SENSITIVE ~THEN BEGIN \([0-9]+\)~ ~THEN BEGIN %file%\1~
				REPLACE_TEXTUALLY CASE_SENSITIVE ~GOTO \([0-9]+\)~ ~GOTO %file%\1~
				REPLACE_TEXTUALLY ~~~~~^BEGIN \(~\)%SOURCE_RES%\(~\)$~~~~~ ~APPEND \1%file_1%\2~
				APPEND_FILE ~.../end.txt~
			COMPILE ~EET/temp/append/dlg/%file%.d~
			COMPILE EVAL ~.../blank.d~
		END
	END
END

/////                                                  \\\\\
///// Patch files                                      \\\\\
/////                                                  \\\\\

INCLUDE ~EET/lib/macros.tph~

COPY_EXISTING_REGEXP GLOB ~.+\.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		PHP_EACH remapped_state AS file => state BEGIN
			REPLACE_EVALUATE CASE_INSENSITIVE ~\( EXTERN .\)%file%\(. \)\([0-9]+\)~ BEGIN
				SET match = state + MATCH3
				PATCH_PRINT ~Patching %SOURCE_FILESPEC%: %file% => %file_1% - dlg; %MATCH3% => %match% - state~
			END ~%MATCH1%%file_1%%MATCH2%%match%~
		END
		REPLACE_EVALUATE CASE_INSENSITIVE ~\([Tt]Dialog[A-Za-z]*("\)\([^"]+\)"~ BEGIN SPRINT match ~%MATCH2%~ SPRINT res ~dlg~ LPM EET_PCU_outer_res END ~\1%match%"~
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.+\.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\([Tt]Dialog[A-Za-z]*("\)\([^"]+\)"~ BEGIN SPRINT match ~%MATCH2%~ SPRINT res ~dlg~ LPM EET_PCU_outer_res END ~\1%match%"~
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.+\.CRE~ ~override~
	LPF ~FJ_CRE_VALIDITY~ RET valid END
	PATCH_IF valid = 1 BEGIN
		SET off = 0x2cc SPRINT res ~dlg~ LPM EET_PCU_off_res
	END
BUT_ONLY

/////                                                  \\\\\
///// Append missing entries                           \\\\\
/////                                                  \\\\\

//CAMPAIGN.2DA
COPY_EXISTING ~CAMPAIGN.2DA~ ~override~
	SET_2DA_ENTRY 0 9 32 ~*~
	SET_2DA_ENTRY 1 9 32 ~*~
	SET_2DA_ENTRY 2 9 32 ~*~
	SET_2DA_ENTRY 3 9 32 ~*~
BUT_ONLY
//even after disabling it from 2DA default files are still used
<<<<<<<< .../NPCLEVEL.2DA
2DA V1.0
****
 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40
>>>>>>>>
COPY ~.../NPCLEVEL.2DA~ ~override~
	~.../NPCLEVEL.2DA~ ~override/NPCLVL25.2DA~

//PDIALOG
//25DIALOG.2DA already patched with PDIALOG.2DA entries
ACTION_PHP_EACH pdialog_2da AS file => dummy BEGIN
	COPY_EXISTING ~%file%.2da~ ~override~
		PRETTY_PRINT_2DA
		READ_2DA_ENTRIES_NOW pdialog_tbl 2
		FOR (i=0; i < pdialog_tbl; ++i) BEGIN
			READ_2DA_ENTRY_FORMER pdialog_tbl i 0 dv
			PATCH_IF (NOT ~%dv%~ STRING_EQUAL_CASE ~POST_DIALOG_FILE~) BEGIN
				PHP_EACH pdialog_2da AS cmp_file => dummy BEGIN
					PATCH_IF (NOT ~%file%~ STRING_EQUAL_CASE ~%cmp_file%~) AND (NOT FILE_CONTAINS_EVALUATED (~%cmp_file%.2DA~ ~^[ %TAB%]*%dv%[ %TAB%]~)) BEGIN
						READ_2DA_ENTRY_FORMER pdialog_tbl i 1 post
						READ_2DA_ENTRY_FORMER pdialog_tbl i 2 join
						READ_2DA_ENTRY_FORMER pdialog_tbl i 3 dream
						INNER_ACTION BEGIN
							//PRINT ~Patching %cmp_file%.2DA: %dv% %post% %join% %dream%~
							APPEND ~%cmp_file%.2DA~ ~%dv% %post% %join% %dream%~
							COPY_EXISTING ~%cmp_file%.2DA~ ~override~
								PRETTY_PRINT_2DA
							BUT_ONLY
						END
					END
				END
			END
		END
		PRETTY_PRINT_2DA
	BUT_ONLY
END

//INTERDIA
COPY_EXISTING ~INTERDIA.2DA~ ~override~
	PRETTY_PRINT_2DA
	READ_2DA_ENTRIES_NOW interdia 3
	FOR (i=0; i < interdia; ++i) BEGIN
		READ_2DA_ENTRY_FORMER interdia i 0 dv
		READ_2DA_ENTRY_FORMER interdia i 1 banter
		READ_2DA_ENTRY_FORMER interdia i 2 25banter
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~25BANTER.2DA~ ~^[ %TAB%]*%dv%[ %TAB%]~)) BEGIN
			INNER_ACTION BEGIN
				//PRINT ~Patching 25BANTER.2DA: %dv% %25banter%~
				APPEND ~25BANTER.2DA~ ~%dv% %25banter%~
				COPY_EXISTING ~25BANTER.2DA~ ~override~
					PRETTY_PRINT_2DA
				BUT_ONLY
			END
		END
		SET_2DA_ENTRY i 2 3 ~***~
	END
	PRETTY_PRINT_2DA
BUT_ONLY

ACTION_PHP_EACH interdia_2da AS file => dummy BEGIN
	COPY_EXISTING ~%file%.2da~ ~override~
		PRETTY_PRINT_2DA
		READ_2DA_ENTRIES_NOW interdia_tbl 2
		FOR (i=1; i < interdia_tbl; ++i) BEGIN
			READ_2DA_ENTRY_FORMER interdia_tbl i 0 dv
			PATCH_IF (NOT ~%dv%~ STRING_EQUAL_CASE ~FILE~) BEGIN
				PHP_EACH interdia_2da AS cmp_file => dummy BEGIN
					PATCH_IF (NOT ~%file%~ STRING_EQUAL_CASE ~%cmp_file%~) AND (NOT FILE_CONTAINS_EVALUATED (~%cmp_file%.2DA~ ~^[ %TAB%]*%dv%[ %TAB%]~)) BEGIN
						READ_2DA_ENTRY_FORMER interdia_tbl i 1 banter
						INNER_ACTION BEGIN
							//PRINT ~Patching %cmp_file%.2DA: %dv% %banter%~
							APPEND ~%cmp_file%.2DA~ ~%dv% %banter%~
							COPY_EXISTING ~%cmp_file%.2DA~ ~override~
								PRETTY_PRINT_2DA
							BUT_ONLY
						END
					END
				END
			END
		END
		PRETTY_PRINT_2DA
	BUT_ONLY
END

//CLASSTEXT
ACTION_PHP_EACH clastext_2da AS file => dummy BEGIN
	COPY_EXISTING ~%file%.2da~ ~override~
		PRETTY_PRINT_2DA
		REPLACE_TEXTUALLY ~^[ ]+CLASSID[ ]+KITID .+$~ ~ CLASSID KITID LOWER DESCSTR MIXED BIOGRAPHY FALLEN BRIEFDESC FALLEN_NOTICE~
		REPLACE_EVALUATE CASE_INSENSITIVE ~^\([^ ]+[ ]+[^ ]+[ ]+.+\)$~ BEGIN
			INNER_PATCH_SAVE MATCH1 ~%MATCH1%~ BEGIN
				COUNT_REGEXP_INSTANCES ~[ ]+~ num_matches
				WHILE (num_matches < 9) BEGIN
					REPLACE_TEXTUALLY ~$~ ~ -1~
					SET num_matches = num_matches + 1
				END
			END
		END ~%MATCH1%~
		READ_2DA_ENTRIES_NOW clastext_tbl 10
		FOR (i=0; i < clastext_tbl; ++i) BEGIN
			READ_2DA_ENTRY_FORMER clastext_tbl i 0 class
			PHP_EACH clastext_2da AS cmp_file => dummy BEGIN
				PATCH_IF (NOT ~%file%~ STRING_EQUAL_CASE ~%cmp_file%~) AND (NOT FILE_CONTAINS_EVALUATED (~%cmp_file%.2DA~ ~^[ %TAB%]*%class%[ %TAB%]~)) BEGIN
					READ_2DA_ENTRY_FORMER clastext_tbl i 1 classid
					READ_2DA_ENTRY_FORMER clastext_tbl i 2 kitid
					READ_2DA_ENTRY_FORMER clastext_tbl i 3 lower
					READ_2DA_ENTRY_FORMER clastext_tbl i 4 descstr
					READ_2DA_ENTRY_FORMER clastext_tbl i 5 mixed
					READ_2DA_ENTRY_FORMER clastext_tbl i 6 biography
					READ_2DA_ENTRY_FORMER clastext_tbl i 7 fallen
					READ_2DA_ENTRY_FORMER clastext_tbl i 8 briefdesc
					READ_2DA_ENTRY_FORMER clastext_tbl i 9 fallen_notice
					INNER_ACTION BEGIN
						//PRINT ~Patching %cmp_file%.2DA: %class% %classid% %kitid% %lower% %descstr% %mixed% %biography% %fallen% %briefdesc% %fallen_notice%~
						APPEND ~%cmp_file%.2DA~ ~%class% %classid% %kitid% %lower% %descstr% %mixed% %biography% %fallen% %briefdesc% %fallen_notice%~
						COPY_EXISTING ~%cmp_file%.2da~ ~override~
							PRETTY_PRINT_2DA
						BUT_ONLY
					END
				END
			END
		END
		PRETTY_PRINT_2DA
	BUT_ONLY
END

//RACETEXT
ACTION_PHP_EACH racetext_2da AS file => dummy BEGIN
	COPY_EXISTING ~%file%.2da~ ~override~
		PRETTY_PRINT_2DA
		READ_2DA_ENTRIES_NOW racetext_tbl 6
		FOR (i=0; i < racetext_tbl; ++i) BEGIN
			READ_2DA_ENTRY_FORMER racetext_tbl i 0 race
			PHP_EACH racetext_2da AS cmp_file => dummy BEGIN
				PATCH_IF (NOT ~%file%~ STRING_EQUAL_CASE ~%cmp_file%~) AND (NOT FILE_CONTAINS_EVALUATED (~%cmp_file%.2DA~ ~^[ %TAB%]*%race%[ %TAB%]~)) BEGIN
					READ_2DA_ENTRY_FORMER racetext_tbl i 1 id
					READ_2DA_ENTRY_FORMER racetext_tbl i 2 name
					READ_2DA_ENTRY_FORMER racetext_tbl i 3 descstr
					READ_2DA_ENTRY_FORMER racetext_tbl i 4 uppercase
					READ_2DA_ENTRY_FORMER racetext_tbl i 5 biography
					INNER_ACTION BEGIN
						//PRINT ~Patching %cmp_file%.2DA: %race% %id% %name% %descstr% %uppercase% %biography%~
						APPEND ~%cmp_file%.2DA~ ~%race% %id% %name% %descstr% %uppercase% %biography%~
						COPY_EXISTING ~%cmp_file%.2da~ ~override~
							PRETTY_PRINT_2DA
						BUT_ONLY
					END
				END
			END
		END
		PRETTY_PRINT_2DA
	BUT_ONLY
END

//MASTAREA
COPY_EXISTING ~SODMAREA.2DA~ ~override~
	PRETTY_PRINT_2DA
	COUNT_2DA_ROWS 2 "cntrow"
	FOR (cnt = 1; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		SET noupdate = 0
		READ_2DA_ENTRY cnt 0 2 "col1"
		READ_2DA_ENTRY cnt 1 2 "col2"
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~MASTAREA.2DA~ ~[ %TAB%]*%col1%[ %TAB%]~)) BEGIN
			INNER_ACTION BEGIN
				APPEND ~MASTAREA.2DA~ ~%col1% %col2%~
				//PRINT ~%col1% %col2% appended to MASTAREA.2DA~
			END
		END
	END
COPY_EXISTING ~MASTAREA.2DA~ ~override~
	PRETTY_PRINT_2DA
BUT_ONLY

//REPUTATI
COPY_EXISTING ~REPUTATI.2DA~ ~override~
	PRETTY_PRINT_2DA
	REPLACE_TEXTUALLY ~$~ ~ ~
	REPLACE_TEXTUALLY ~^ $~ ~~
	COUNT_2DA_ROWS 3 "cntrow"
BUT_ONLY
COPY_EXISTING - ~SODREPUT.2DA~ ~.../SODREPUT.2DA~
	PRETTY_PRINT_2DA
	REPLACE_TEXTUALLY ~$~ ~ ~
	COUNT_2DA_COLS "cntcol"
	FOR (cnt = 1; cnt < (cntcol - 1); cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY 0 cnt 3 "name"
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~REPUTATI.2DA~ ~[ ]%name%[ ]~)) BEGIN
			PATCH_PRINT ~Patching REPUTATI.2DA: %name%~
			SET updated = 0
			SPRINT col ~dummy_entry dummy_entry %name%~
			FOR (cnt2 = 1; cnt2 < cntrow; cnt2 = cnt2 + 1) BEGIN
				READ_2DA_ENTRY cnt2 (cnt + 1) 3 "add"
				SPRINT col ~%col% %add%~
			END
			INNER_ACTION BEGIN
				APPEND_COL ~REPUTATI.2DA~ ~%col%~
				OUTER_PATCH_SAVE ~col~ ~%col%~ BEGIN //for easier DEBUG reading
					REPLACE_TEXTUALLY ~dummy_entry ~ ~~
					REPLACE_TEXTUALLY ~[ ]+~ ~ ~
				END
				//PRINT ~%col% column appended to %SOURCE_FILE%~
			END
		END
	END
COPY_EXISTING ~REPUTATI.2DA~ ~override~
	REPLACE_TEXTUALLY ~dummy_entry~ ~~
	PRETTY_PRINT_2DA
BUT_ONLY

/////                                                  \\\\\
///// Prepare files needed for save updating           \\\\\
/////                                                  \\\\\

ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN
	OUTER_SPRINT WEIDU_EXECUTABLE ~setup-EET.exe~
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
	OUTER_SPRINT WEIDU_EXECUTABLE ~./EET/weidu~
END

AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --traify-tlk --out "%USER_DIRECTORY%/save/saves.tra"~

ACTION_IF FILE_EXISTS_IN_GAME ~add_spell.ids~ BEGIN
	COPY_EXISTING - ~add_spell.ids~ ~.../add_spell.ids~
END

OUTER_SET string_count = 500000 //request weidu feature to get accurate number without copying the file (32MB limit)
<<<<<<<< .../saves.txt
%string_count%
>>>>>>>>
COPY + ~.../saves.txt~ ~%USER_DIRECTORY%/save/saves.txt~ EVALUATE_BUFFER
	PATCH_IF FILE_EXISTS ~.../add_spell.ids~ BEGIN
		APPEND_FILE ~.../add_spell.ids~
	END

ACTION_IF (FILE_EXISTS ~%USER_DIRECTORY%/Baldur.lua~) BEGIN
	COPY ~%USER_DIRECTORY%/Baldur.lua~ ~%USER_DIRECTORY%~
		REPLACE_TEXTUALLY ~^.+'EET Installation State'.+$~ ~SetPrivateProfileString('Program Options','EET Installation State','2')~
	BUT_ONLY
END

ACTION_IF (!FILE_CONTAINS ~override/EET.flag~ ~TRA_EXPORTED~) BEGIN
	APPEND + ~EET.flag~ ~TRA_EXPORTED~
END
