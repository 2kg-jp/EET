//Compatibility with ~BG1UB/SETUP-BG1UB.TP2~ #0 #22 // Item Corrections and Restorations
COPY - ~%bgee_dir%/WeiDU.log~ ~%bgee_dir%~
	COUNT_REGEXP_INSTANCES ~BG1UB\.TP2. #0 #22 ~ num_matches

ACTION_IF (num_matches > 0) BEGIN
	COPY_EXISTING ~misc76.itm~ ~override~ // Dream Potion
		WRITE_EVALUATED_ASCII 0x3a "imisc76"
		WRITE_EVALUATED_ASCII 0x58 "cpotn13"
	BUT_ONLY

	COPY_EXISTING ~misc77.itm~ ~override~ // Skull Of Kereph
		WRITE_EVALUATED_ASCII 0x3a "imisc77"
	BUT_ONLY

	COPY_EXISTING ~SQUIRP.ITM~ ~override~ // Restores the Squirrel Paw icon (devSin)
		PATCH_IF (SOURCE_SIZE > 0x72) BEGIN
			WRITE_EVALUATED_ASCII 0x3a ~ISQUIRL~ #8
			READ_SHORT 0x68 "num_abl"
			PATCH_IF ("%num_abl%" > 0x0) BEGIN
				READ_LONG 0x64 "abl_offset"
				WRITE_EVALUATED_ASCII "%abl_offset%" + 0x4 ~ISQUIRL~ #8
			END
		END
	BUT_ONLY

	COPY_EXISTING ~SPIN109.SPL~ ~override~ // Restores the unused Lay on Hands spell icon (devSin)
		PATCH_IF (SOURCE_SIZE > 0x72) BEGIN
			READ_LONG 0x64 ablOff
			FOR (READ_SHORT 0x68 numAbl; numAbl > 0x0; numAbl -= 0x1) BEGIN
				WRITE_ASCII ablOff + 0x4 ~SPIN109~ #8
				ablOff += 0x28
			END
		END
	BUT_ONLY
END

//Compatibility with ~BG1UB/SETUP-BG1UB.TP2~ #0 #16 // Creature Corrections
COPY - ~%bgee_dir%/WeiDU.log~ ~%bgee_dir%~
	COUNT_REGEXP_INSTANCES ~BG1UB\.TP2. #0 #16 ~ num_matches

ACTION_IF (num_matches > 0) BEGIN
	COPY_EXISTING ~PLYOGRE.CRE~ ~override~ // Ogre (OGRE animation)
		PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
			WRITE_BYTE 0x237 1                  // Sex: Male
			WRITE_BYTE 0x275 4                  // Gender: Neither
		END
	BUT_ONLY
END
