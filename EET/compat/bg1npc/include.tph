//Compatibility with BG1NPC Required Modifications

ACTION_IF (FILE_EXISTS ~override/WeiDU.log~) AND (FILE_CONTAINS ~override/WeiDU.log~ ~BG1NPC\.TP2. #[0-9]+ #0 ~) BEGIN
	EXTEND_TOP ~REDDEATH.BCS~ ~EET/compat/bg1npc/baf/X#BRDWI.BAF~ EVALUATE_BUFFER //Interjection scripting changes, BG1 NPCs
END

//Compatibility with BG1NPC Banters, Quests, and Interjections

ACTION_IF (FILE_EXISTS ~override/WeiDU.log~) AND (FILE_CONTAINS ~override/WeiDU.log~ ~BG1NPC\.TP2. #[0-9]+ #1 ~) BEGIN
	EXTEND_TOP ~NIEMAIN.BCS~ ~EET/compat/bg1npc/baf/X#NIEMAIN.BAF~ EVALUATE_BUFFER

	COPY_EXISTING ~BEARBL.CRE~ ~override~
		WRITE_ASCII 0x280 ~bearbl~ #32 //death variable
	BUT_ONLY

	COPY_EXISTING ~BEARBR.CRE~ ~override~
		WRITE_ASCII 0x280 ~bearbr~ #32 //death variable
	BUT_ONLY
END

//Compatibility with BG1NPC "Dynaheir's Romance Core (teen content)" component

//Disable Dynaheir's End Cutscene (EET implements different transition, so just like in BGT case this cutscene should be disabled)
ACTION_IF (FILE_EXISTS ~override/WeiDU.log~) AND (FILE_CONTAINS ~override/WeiDU.log~ ~BG1NPC\.TP2. #[0-9]+ #12 ~) BEGIN
<<<<<<<< EET-inlined/EET/compat/bg1npc/baf/ar0125-rbOld.baf
IF
  InParty("dynaheir")
  !Dead("dynaheir")
  !Global("X#DynaheirRomanceInactive","GLOBAL",1)
  OR(2)
    Global("X#DynaheirRomanceActive","GLOBAL",1)
    Global("X#DynaheirRomanceActive","GLOBAL",2)
  Global("DeathOfSarevok","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("DeathOfSarevok","GLOBAL",-1)
    Continue()
END

IF
  InParty("dynaheir")
  !Dead("dynaheir")
  !Global("X#DynaheirRomanceInactive","GLOBAL",1)
  OR(2)
    Global("X#DynaheirRomanceActive","GLOBAL",1)
    Global("X#DynaheirRomanceActive","GLOBAL",2)
  Dead("sarevok")
  GlobalTimerExpired("DeathOfSarevok","GLOBAL")
  Global("endofbg1","GLOBAL",0)
  Global("X#DynaDeath","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("X#DynaDeath","GLOBAL",1)
    AddexperienceParty(15000)
    StartCutSceneMode()
    ClearAllActions()
    MultiPlayerSync()
    SaveGame(2)
    SmallWait(2)
    StartMovie("ENDMOVIE")
    SmallWait(2)
    DisplayStringHead(Player1,0)
    Wait(4)
    FadeToColor([20.0],0)
    DayNight(MIDNIGHT)
    StartCutScene("X#DYDEAD")
END
>>>>>>>>
	COPY + ~%patch_dir%/bcs/FH0125.bcs~ ~%patch_dir%/bcs~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~OR(4)[%newline%]*Global("X#DynaheirRomanceInactive","GLOBAL",1)[%newline%]*Global("X#DynaheirRomanceActive","GLOBAL",0)[%newline%]*Dead("dynaheir")[%newline%]*!InParty("dynaheir")~ ~~
			//before replacing bcs block to change unknown string to dummy 0
			REPLACE_TEXTUALLY ~DisplayStringHead(Player1,.*)[%newline%]*Wait(4)[%newline%]*FadeToColor(\[20\.0\],0)[%newline%]*DayNight(MIDNIGHT)[%newline%]*StartCutScene("X#DYDEAD")~ ~DisplayStringHead(Player1,0)
		Wait(4)
		FadeToColor([20.0],0)
		DayNight(MIDNIGHT)
		StartCutScene("X#DYDEAD")~
		END
		REPLACE_BCS_BLOCK ~EET-inlined/EET/compat/bg1npc/baf/ar0125-rbOld.baf~ ~EET-inlined/blank.baf~
	BUT_ONLY
END
