BACKUP ~EET/backup~
AUTHOR ~K4thos (swit)~
VERSION ~1.0 RC5~
README ~EET/lang/%LANGUAGE%/readme-EET.html~ ~EET/readme-EET.html~

LANGUAGE ~English~
	~en_US~
	~EET/lang/en_US/prompts.tra~
	~EET/lang/en_US/setup.tra~
	~EET/lang/en_US/chapters.tra~
	~EET/lang/en_US/dialog.tra~
	~EET/lang/en_US/scripts.tra~
	~EET/lang/en_US/2da.tra~
	~EET/lang/en_US/compatibility.tra~
LANGUAGE ~Polski (Polish)~
	~pl_PL~
	~EET/lang/pl_PL/prompts.tra~
	~EET/lang/pl_PL/setup.tra~
	~EET/lang/pl_PL/chapters.tra~
	~EET/lang/pl_PL/dialog.tra~
	~EET/lang/pl_PL/scripts.tra~
	~EET/lang/pl_PL/2da.tra~
	~EET/lang/pl_PL/compatibility.tra~
LANGUAGE ~Deutsch (German)~
	~de_DE~
	~EET/lang/de_DE/prompts.tra~
	~EET/lang/de_DE/setup.tra~
	~EET/lang/de_DE/chapters.tra~
	~EET/lang/de_DE/dialog.tra~
	~EET/lang/de_DE/scripts.tra~
	~EET/lang/de_DE/2da.tra~
	~EET/lang/de_DE/compatibility.tra~
LANGUAGE ~French~
	~fr_FR~
	~EET/lang/fr_FR/prompts.tra~
	~EET/lang/fr_FR/setup.tra~
	~EET/lang/fr_FR/chapters.tra~
	~EET/lang/fr_FR/dialog.tra~
	~EET/lang/fr_FR/scripts.tra~
	~EET/lang/fr_FR/2da.tra~
	~EET/lang/fr_FR/compatibility.tra~
LANGUAGE ~Spanish~
	~es_ES~
	~EET/lang/es_ES/prompts.tra~
	~EET/lang/es_ES/setup.tra~
	~EET/lang/es_ES/chapters.tra~
	~EET/lang/es_ES/dialog.tra~
	~EET/lang/es_ES/scripts.tra~
	~EET/lang/es_ES/2da.tra~
	~EET/lang/es_ES/compatibility.tra~

BEGIN @900000 //Initialise EET (first mod in install order)
REQUIRE_PREDICATE ENGINE_IS ~bg2ee~ @900001 //Please install it on BG2:EE engine
FORBID_COMPONENT ~setup-bp-bgt-worldmap.tp2~ ~0~ @900002 //EET should be installed BEFORE BP-BGT Worldmap

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Initialise installation                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/*OUTER_SET files_count = 0
ACTION_BASH_FOR ~override~ ~^.+$~ BEGIN
	COPY + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_FILESPEC%~
	OUTER_SET files_count = files_count + 1
END
PRINT ~Total number of files in override directory = %files_count%~
ACTION_IF ("%files_count%" > 3) BEGIN
	FAIL ~Unexpected files detected within override directory. EET should be installed on clean BG2:EE.~
END*/

//create dummy file with UTF-8 without BOM encoding - used many times during installation
<<<<<<<< .../blank.txt
>>>>>>>>

COPY - ~.../blank.txt~ ~.../blank.baf~

//Nothing done within EET/temp folder is registered for uninstallation (saves hundreds of MB disk space).
//The whole folder is removed after the installation is complete (saves 1GB+ disk space) unless the debug mode argument is parsed.
MKDIR ~EET/temp~
COPY + ~.../blank.txt~ ~EET/temp/uninstall.flag~

OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~
OUTER_SPRINT slash ~/~
OUTER_SPRINT quote ~"~
OUTER_SPRINT tilde "~"
OUTER_SPRINT bg2ee_dir ~.~
OUTER_SPRINT tra_path ~EET/lang~
OUTER_SPRINT temp_dir ~EET/temp~
OUTER_SPRINT patch_dir ~EET/temp/patch~
OUTER_SPRINT biff_dir ~EET/temp/biff~
OUTER_SPRINT backup_dir ~EET/backup~
OUTER_SPRINT arch_var ~~

OUTER_SPRINT log_remapped_sum ~~
OUTER_SPRINT log_remapped_kit ~~
OUTER_SPRINT log_remapped_race ~~
OUTER_SPRINT log_remapped_class ~~
OUTER_SPRINT log_remapped_spec ~~
OUTER_SPRINT log_remapped_ipro ~~
OUTER_SPRINT log_remapped_mus ~~
OUTER_SPRINT log_remapped_bam ~~
OUTER_SPRINT log_remapped_eff ~~
OUTER_SPRINT log_remapped_itm ~~
OUTER_SPRINT log_remapped_spl ~~
OUTER_SPRINT log_remapped_splName ~~

OUTER_SET auto_install = 0
OUTER_SET valid_dir = 0
OUTER_SET first_prompt = 0
OUTER_SET allow_mod = 0

ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN
	OUTER_SPRINT os_slash ~\~
	OUTER_SPRINT exe ~.exe~
	OUTER_SPRINT WEIDU_EXECUTABLE ~setup-EET.exe~
	OUTER_SPRINT bash_debug ~ 2>&1|EET\bin\win32\mtee.exe EET\temp\bash.debug /A~
	ACTION_IF (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_UNINSTALL ~rmdir /s /q EET\temp~ EXACT
	END
	ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[dD]~ = 1) AND (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_EXIT ~rmdir /s /q EET\temp~ EXACT
	END
<<<<<<<< .../arch_var.bat
::32/64Bit Switch
ECHO %PROCESSOR_ARCHITEW6432%|FINDSTR AMD64>NUL && SET arch_var=AMD64 || SET arch_var=x86
ECHO %arch_var%>EET\temp\arch_var.txt
>>>>>>>>
	COPY ~.../arch_var.bat~ ~EET/temp/arch_var.bat~
	AT_NOW ~CALL EET\temp\arch_var.bat~
	COPY - ~EET/temp/arch_var.txt~ ~EET/temp~
		REPLACE_EVALUATE CASE_INSENSITIVE ~AMD64~ BEGIN
			SPRINT arch_var ~x86_64\~
			PATCH_PRINT ~arch_var = %arch_var%~
		END ~~
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
	OUTER_SPRINT os_slash ~/~
	OUTER_SPRINT exe ~~
	OUTER_SPRINT WEIDU_EXECUTABLE ~./EET/weidu~
	OUTER_SPRINT bash_debug ~ 2>>EET/temp/bash.debug | tee -a EET/temp/bash.debug~
	ACTION_IF (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_UNINSTALL ~rm -rf EET/temp~
	END
	ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[dD]~ = 1) AND (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_EXIT ~rm -rf EET/temp~
	END
END

OUTER_SPRINT tileconv ~EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%%arch_var%tileconv%exe%~
OUTER_SPRINT tile2ee ~EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%%arch_var%tile2ee%exe%~
OUTER_SPRINT ffmpeg ~EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%ffmpeg%exe%~
OUTER_SET EET_JOURNAL = 1

/////                                                  \\\\\
///// Include                                          \\\\\
/////                                                  \\\\\

COPY - ~EET/other/cpmvars/eet_cpmvars.tpa~ ~.../eet_cpmvars.tpa~
	REPLACE_TEXTUALLY ~\(GAME_IS "\)eet~ ~\1bg2ee~//at this point WeiDU thinks it's still BG2:EE installation
	REPLACE_TEXTUALLY "FILE_CONTAINS ~override/EET.flag~ ~EET_JOURNAL~" "EET_JOURNAL = 1" //at this point the EET.flag does no exist
	REPLACE_TEXTUALLY ~FAIL .*~ ~~
INCLUDE ~.../eet_cpmvars.tpa~

COPY - ~EET/other/EET_functions/EET_functions.tph~ ~.../EET_functions.tph~
	REPLACE_TEXTUALLY "\(GAME_IS ~\)eet" "\1bg2ee"//at this point WeiDU thinks it's still BG2:EE installation
INCLUDE ~.../EET_functions.tph~

INCLUDE ~EET/lib/macros.tph~
INCLUDE ~EET/lib/pcu_dict.tph~
INCLUDE ~EET/lib/strings.tph~

/////                                                  \\\\\
///// Get location of BG:EE installation               \\\\\
/////                                                  \\\\\

ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Pp]~ = 0) BEGIN
	OUTER_SPRINT bgee_dir ~%argv[1]%~
	OUTER_SET auto_install = 1
	PRINT ~%bgee_dir% directory assigned via auto install batch command~
END ELSE ACTION_IF (FILE_EXISTS ~EET/bgee_dir.txt~) BEGIN
	COPY - ~EET/bgee_dir.txt~ ~EET~
		READ_ASCII 0x0 bgee_dir (%SOURCE_SIZE%)
	OUTER_SET first_prompt = 1
	PRINT ~%bgee_dir% directory assigned via EET/bgee_dir.txt~
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN
<<<<<<<< .../programfiles.bat
echo %%var%%>EET/temp/%var%.txt
>>>>>>>>
	ACTION_FOR_EACH var IN ~programfiles~ ~programfiles(x86)~ ~ProgramW6432~ BEGIN
		COPY + ~.../programfiles.bat~ ~EET/temp~ EVALUATE_BUFFER
		AT_NOW ~EET/temp/programfiles.bat~
		COPY - ~EET/temp/%var%.txt~ ~EET/temp~//for some reason the same file can't be used for all 3 vars
			READ_ASCII 0x0 dir (%SOURCE_SIZE%)
		OUTER_PATCH_SAVE dir ~%dir%~ BEGIN
			REPLACE_TEXTUALLY ~%WNL%~ ~~//needed due to newline added by windows > command
		END
		PRINT ~%var% = %dir%~
		ACTION_FOR_EACH dir2 IN ~/BeamDog/Games/00806~ ~/Baldur's Gate Enhanced Edition/Data/00806~ BEGIN
			ACTION_IF (FILE_EXISTS ~%dir%%dir2%/movies/sodcin05.wbm~) AND (first_prompt = 0) BEGIN
				OUTER_SPRINT bgee_dir ~%dir%%dir2%~
				OUTER_SET first_prompt = 1
				PRINT ~%bgee_dir% directory assigned to default dir~
			END
		END
	END
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
	ACTION_FOR_EACH dir IN ~%quote%/Applications/Baldur's Gate - Enhanced Edition/Game Data/00777/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ ~%quote%/Applications/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ ~%quote%%tilde%/Library/Application Support/Steam/SteamApps/common/Baldur's Gate Enhanced Edition/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ ~%quote%/Applications/Baldur's Gate Enhanced Edition.app/Contents/Resources/game/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ BEGIN
		ACTION_IF (FILE_EXISTS ~%dir%/movies/sodcin05.wbm~) AND (first_prompt = 0) BEGIN
			OUTER_SPRINT bgee_dir ~%dir%~
			OUTER_SET first_prompt = 1
			PRINT ~%bgee_dir% directory assigned to default dir~
		END
	END
END

ACTION_IF (auto_install = 0) BEGIN
	ACTION_IF (first_prompt = 1) BEGIN
		OUTER_SPRINT bgee_prompt ~a~
		OUTER_WHILE (~%bgee_prompt%~ STRING_MATCHES_REGEXP ~[YyNn]~) BEGIN
			PRINT @900003 //Detected BG:EE directory as %bgee_dir%. Is this correct? [Y]es or [N]o
			ACTION_READLN ~bgee_prompt~
		END
		ACTION_IF ((~%bgee_prompt%~ STRING_EQUAL_CASE ~y~)=0) BEGIN
			OUTER_SET first_prompt = 0
		END
	END
	ACTION_IF (first_prompt = 0) BEGIN
		PRINT ~~
		PRINT @900004 //Enter the full path to your BG:EE installation then press Enter. Example: C:\Program Files (x86)\BeamDog\Games\00806
		ACTION_READLN ~bgee_dir~
	END
END

OUTER_WHILE (valid_dir = 0) BEGIN
	OUTER_PATCH_SAVE bgee_dir ~%bgee_dir%~ BEGIN
		PATCH_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN //Win32
			REPLACE_TEXTUALLY ~[*?"<>|]~ ~~ //invalid characters in a path
			REPLACE_TEXTUALLY ~\~ ~/~ //change slashes
			REPLACE_TEXTUALLY ~[\\/]*$~ ~~ //remove terminal slashes
		END ELSE PATCH_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
			REPLACE_TEXTUALLY ~\\~ ~~ //remove quotes
			REPLACE_TEXTUALLY ~^"\(.*\)"$~ ~\1~ //if the whole text is quoted, remove quotes
			REPLACE_TEXTUALLY ~/*$~ ~~ //remove terminal slashes//*/
		END
	END
	ACTION_IF NOT (FILE_EXISTS ~%bgee_dir%/movies/sodcin05.wbm~) BEGIN
		PRINT ~~
		PRINT @900005 //Invalid BG:EE directory, or incorrect BG:EE installation. It is safe to abort this installation by closing this window or pressing Ctrl+C.
		PRINT @900004 //Enter the full path to your BG:EE installation then press Enter. Example: C:\Program Files (x86)\BeamDog\Games\00806
		ACTION_READLN ~bgee_dir~
	END ELSE BEGIN
		OUTER_SET valid_dir = 1
		PRINT ~BG:EE directory used for this installation:
%bgee_dir%~
	END
END

<<<<<<<< .../bgee_dir.txt
%bgee_dir%>>>>>>>>
COPY ~.../bgee_dir.txt~ ~EET/bgee_dir.txt~ EVALUATE_BUFFER

/////                                                  \\\\\
///// Detect mods installed on BG:EE                   \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~%bgee_dir%/WeiDU.log~ BEGIN
	COPY ~%bgee_dir%/WeiDU.log~ ~WeiDU-BGEE.log~
		REPLACE_TEXTUALLY ~^//.*~ ~~ //removes "Recently uninstalled" lines
	ACTION_IF (FILE_CONTAINS ~WeiDU-BGEE.log~ "^~SOA/SETUP-SOA\.TP2~ #[0-9]+ #1 ") BEGIN
		FAIL @900006 //You've installed wrong The Stone of Askavar component. EET is compatible with Default version (option 1 during installation).
	END
	ACTION_IF (FILE_CONTAINS ~WeiDU-BGEE.log~ "^~DRIZZTSAGA/DRIZZTSAGA\.TP2~ #[0-9]+ #1 ") BEGIN
		FAIL @900007 //You've installed wrong Drizzt Saga component. EET is compatible with Default version (option 1 during installation).
	END
	COPY + ~%bgee_dir%/WeiDU.log~ ~EET/temp~
		REPLACE_TEXTUALLY ~^//.*~ ~~
		REPLACE_TEXTUALLY ~^.~ ~~
		REPLACE_TEXTUALLY ~^.*[/\\]\(.*.TP2\)~ ~\1~
		REPLACE_TEXTUALLY ~\.TP2.~ ~~
		REPLACE_TEXTUALLY ~SETUP-~ ~~
		COUNT_2DA_ROWS 3 "cntrow"
		PATCH_PRINT ~%cntrow% BG:EE mods detected. Checking compatibility with EET...~
		FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
			READ_2DA_ENTRY cnt 0 1 "col1"
			INNER_ACTION BEGIN
				COPY - ~EET/tbl/compatibility.tbl~ ~EET/tbl~
					COUNT_2DA_ROWS 1 "cntrow2"
					FOR (cnt2 = 0; cnt2 < "%cntrow2%"; cnt2 = cnt2 + 1) BEGIN
						READ_2DA_ENTRY cnt2 0 1 "compareWith"
						PATCH_IF (("%col1%" STRING_EQUAL_CASE "%compareWith%")=1) BEGIN
							SET allow_mod = 1
						END
					END
					PATCH_IF ("%allow_mod%" = 1) BEGIN
						PATCH_PRINT ~%col1% BG:EE component detected~
						SET allow_mod = 0
					END ELSE PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Mm]~ = 0) BEGIN
						PATCH_WARN ~WARNING: %col1% BG:EE mod is not recognized by EET, but M/m flag is assigned, so installation will continue~
					END ELSE BEGIN
						PATCH_FAIL @900008 //%col1% BG:EE mod is not recognized by EET, so it can't be imported to BG2:EE with EET
					END
			END
		END
END

/////                                                  \\\\\
///// Confirm / create weidu.conf                      \\\\\
/////                                                  \\\\\

OUTER_SPRINT LANGUAGEcase ~%LANGUAGE%~
ACTION_TO_LOWER LANGUAGEcase

<<<<<<<< .../weidu.conf
lang_dir = %LANGUAGEcase%>>>>>>>>

ACTION_IF NOT FILE_EXISTS ~%bgee_dir%/weidu.conf~ BEGIN
	COPY ~.../weidu.conf~ ~%bgee_dir%/weidu.conf~ EVALUATE_BUFFER
		PATCH_PRINT ~weidu.conf created within BG:EE directory and set to %LANGUAGEcase%~
END ELSE BEGIN
	COPY ~%bgee_dir%/weidu.conf~ ~%bgee_dir%/weidu.conf~
		READ_2DA_ENTRY 0 2 3 "compareWith"
		PATCH_IF (("%LANGUAGEcase%" STRING_EQUAL_CASE "%compareWith%")=1) BEGIN
			PATCH_PRINT ~%bgee_dir%/weidu.conf detected with the same language setting as the installation: %LANGUAGEcase%~
		END ELSE PATCH_IF FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGE%/dialog.tlk~ BEGIN
			PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~1~ = 0) BEGIN
				SPRINT conf_prompt ~y~
			END ELSE PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~2~ = 0) BEGIN
				SPRINT conf_prompt ~n~
			END ELSE BEGIN
				SPRINT conf_prompt ~z~
			END
			WHILE (~%conf_prompt%~ STRING_MATCHES_REGEXP ~[YyNn]~) BEGIN
				PATCH_PRINT @900010 //%bgee_dir%/weidu.conf file has "%compareWith%" language set instead of "%LANGUAGEcase%". Is this correct? [Y]es (keep it as it is) or [N]o (change it to "%LANGUAGEcase%")
					PATCH_READLN ~conf_prompt~
			END
			PATCH_IF ((~%conf_prompt%~ STRING_EQUAL_CASE ~n~)=1) BEGIN
				PATCH_PRINT ~%bgee_dir%/weidu.conf %compareWith% changed to %LANGUAGEcase%~
				INNER_ACTION BEGIN
					COPY ~.../weidu.conf~ ~%bgee_dir%/weidu.conf~ EVALUATE_BUFFER
				END
			END ELSE BEGIN
				PATCH_PRINT ~%bgee_dir%/weidu.conf %compareWith% kept as it is~
			END
		END ELSE BEGIN
			PATCH_PRINT ~%bgee_dir%/weidu.conf will keep %compareWith% language - %bgee_dir%/lang/%LANGUAGE%/dialog.tlk doesn't exist.~
		END
	BUT_ONLY
END

COPY ~weidu.conf~ ~weidu.conf~
	READ_2DA_ENTRY 0 2 3 "LANGUAGE_BG2"
	INNER_PATCH_SAVE LANGUAGE_BG2 ~%LANGUAGE_BG2%~ BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(..\)$~ BEGIN
			TO_UPPER MATCH1
		END ~%MATCH1%~
	END
	PATCH_PRINT ~LANGUAGE_BG2 = %LANGUAGE_BG2%~
BUT_ONLY

/////                                                  \\\\\
///// Extraction of TLK from BG:EE                     \\\\\
/////                                                  \\\\\

PRINT ~~
PRINT ~Extracting texts for translation from BG:EE...~

ACTION_IF FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGE%/dialog.tlk~ BEGIN
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --traify-tlk --min 0 --max 33999 --out "EET/temp/bgee01.tra"~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --traify-tlk --min 34000 --max 71119 --out "EET/temp/bgee02.tra"~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --traify-tlk --min 71120 --out "EET/temp/bgee03.tra"~
	LAM bash_log
END ELSE BEGIN
	FAIL @900009 //TLK for language: %LANGUAGEcase% not found in %bgee_dir%
END

ACTION_FOR_EACH file IN bgee01 bgee02 bgee03 BEGIN
	COPY + ~EET/temp/%file%.tra~ ~EET/temp~
		REPLACE_TEXTUALLY ~~~~~\([~"%]\)\[[^~"%]*\][ ]*~~~~~ ~~~~~\1~~~~~ //clear []s at the start of StrRefs//"
		PHP_EACH remapped_wav AS source => dest BEGIN
			REPLACE_TEXTUALLY ~\(\[\)%source%\(\]\)~ ~\1%dest%\2~
		END
		PATCH_IF ~%file%~ STRING_EQUAL_CASE ~bgee03~ BEGIN
			COUNT_2DA_ROWS 3 "cnt_row"
			SPRINT StrRef_cutoff ~dummy~
			WHILE (~%StrRef_cutoff%~ STRING_CONTAINS_REGEXP ~^@[0-9]+~) BEGIN //not StrRef
				READ_2DA_ENTRY cnt_row 0 3 "StrRef_cutoff"
				SET cnt_row = cnt_row - 1
			END
			INNER_PATCH_SAVE StrRef_cutoff ~%StrRef_cutoff%~ BEGIN
				REPLACE_TEXTUALLY ~^@~ ~~
			END
			SET StrRef_cutoff = StrRef_cutoff //just to be sure that its integer
			PATCH_PRINT ~StrRef_cutoff = %StrRef_cutoff%~
		END
END

//update BG:EE TRA file
ACTION_DEFINE_ASSOCIATIVE_ARRAY table_EET_bgeeTra BEGIN
	//change BG1 textscreens text to unique strings used only for journal
	16190 => 2000024 //2000000
	15836 => 2000025 //2000001
	15837 => 2000026 //2000002
	15838 => 2000027 //2000003
	15839 => 2000028 //2000004
	15840 => 2000029 //2000005
	15841 => 2000030 //2000006
END

COPY - ~EET/lang/%LANGUAGE%/chapters.tra~ ~EET/lang/%LANGUAGE%~
	PHP_EACH table_EET_bgeeTra AS from => to BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE "^@%to%[ %TAB%]*=[ %TAB%]*~\([^~]+\)~" BEGIN
			SPRINT text ~%MATCH1%~
			INNER_PATCH_SAVE text ~%text%~ BEGIN
				REPLACE_TEXTUALLY ~%LNL%~ ~-NEWLINE-~
			END
			INNER_ACTION BEGIN
				COPY + ~EET/temp/bgee01.tra~ ~EET/temp~
					REPLACE_EVALUATE CASE_INSENSITIVE "^\(@%from%[ %TAB%]*=[ %TAB%]*\)~\([^~]+\)~.*$" BEGIN
						//PATCH_PRINT ~Patching %SOURCE_FILESPEC%: REPLACED @%from%:%LNL%%MATCH2%%LNL%TO:%LNL%%text%~
					END
					"%MATCH1%~%text%~"
					REPLACE_TEXTUALLY ~-NEWLINE-~ ~%LNL%~
			END
		END ~~
	END

LOAD_TRA ~EET/temp/bgee01.tra~
LOAD_TRA ~EET/temp/bgee02.tra~
LOAD_TRA ~EET/temp/bgee03.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Extract and convert BG:EE resources              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~Extract BG:EE resources...~

/////                                                  \\\\\
///// prepare BG:EE resources for PCU conversion       \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH var IN are bcs chr cre dlg eff ini itm pro spl sto vef vvc wed BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	MKDIR ~%patch_dir%/%var%~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --out "%patch_dir%/%var%" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_FOR_EACH dir IN ~%bgee_dir%/override~ ~%bgee_dir%/lang/%LANGUAGE%/override~ BEGIN
		ACTION_BASH_FOR ~%dir%~ ~^.+\.%var%$~ BEGIN
			COPY_LARGE + ~%BASH_FOR_FILESPEC%~ ~%patch_dir%/%var%~
		END
	END
	ACTION_IF FILE_EXISTS ~EET/lib/prep_%var%.tph~ BEGIN
		INCLUDE ~EET/lib/prep_%var%.tph~
	END
END

ACTION_FOR_EACH file IN baldur.gam worldmap.wmp bgmap.wmp sodmap.wmp BEGIN
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/override/%file%~) BEGIN
		COPY_LARGE + ~%bgee_dir%/override/%file%~ ~%patch_dir%~
	END ELSE BEGIN
		AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --out "%patch_dir%" --biff-get-rest "%file%"~
		LAM bash_log
	END
END

/////                                                  \\\\\
///// prepare other BG:EE resources                    \\\\\
/////                                                  \\\\\

MKDIR ~EET/temp/array~
	~%bgee_dir%/lang/%LANGUAGE%/override~
	~lang/%LANGUAGE_BG2%/sounds~
	~%biff_dir%/pvrz~
	~%biff_dir%/eetBGGUI~
	~EET/SoD_GUI~

ACTION_FOR_EACH dir IN eetTU00 eetOH2 eetOH3 eetOH9 eetBG00 eetBG01 eetBG02 eetBG03 eetBG04 eetBG05 eetBG06 eetBG07 eetBG08 eetBG09 eetBG10 eetBG11 eetBG12 eetBG13 eetBG14 eetBG15 eetBG16 eetBG17 eetBG18 eetBG19 eetBG20 eetBG21 eetBG22 eetBG23 eetBG24 eetBG26 eetBG27 eetBG28 eetBG29 eetBG30 eetBG31 eetBG32 eetBG33 eetBG34 eetBG35 eetBG36 eetBG37 eetBG38 eetBG39 eetBG40 eetBG41 eetBG42 eetBG43 eetBG44 eetBG45 eetBG46 eetBG47 eetBG48 eetBG49 eetBG50 eetBG51 eetBG52 eetBG53 eetBG54 eetBG55 eetBG56 eetBG57 eetBG58 eetBG59 eetBG60 eetBG61 eetBD0 eetBD1 eetBD2 eetBD3 eetBD4 eetBD5 eetBD6 eetBD7 BEGIN
	MKDIR ~%biff_dir%/%dir%~
END

ACTION_FOR_EACH var IN bam bmp mos tis pvrz BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --out "EET%os_slash%temp%os_slash%biff" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_FOR_EACH dir IN ~%bgee_dir%/override~ ~%bgee_dir%/lang/%LANGUAGE%/override~ BEGIN
		ACTION_BASH_FOR ~%dir%~ ~^.+\.%var%$~ BEGIN
			COPY_LARGE + ~%BASH_FOR_FILESPEC%~ ~%biff_dir%~
		END
	END
	ACTION_IF FILE_EXISTS ~EET/lib/prep_%var%.tph~ BEGIN
		INCLUDE ~EET/lib/prep_%var%.tph~
	END
END

ACTION_FOR_EACH var IN 2da /*plt*/ wav wbm wfx BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	MKDIR ~EET/temp/%var%~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --out "EET/temp/%var%" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_FOR_EACH dir IN ~%bgee_dir%/override~ ~%bgee_dir%/lang/%LANGUAGE%/override~ BEGIN
		ACTION_BASH_FOR ~%dir%~ ~^.+\.%var%$~ BEGIN
			COPY_LARGE + ~%BASH_FOR_FILESPEC%~ ~EET/temp/%var%~
		END
	END
	ACTION_IF FILE_EXISTS ~EET/lib/prep_%var%.tph~ BEGIN
		INCLUDE ~EET/lib/prep_%var%.tph~
	END
END

ACTION_FOR_EACH file IN animate.ids anisnd.ids class.ids gtimes.ids kit.ids missile.ids projectl.ids race.ids specific.ids spell.ids bgee.lua bgee.sql BEGIN
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/override/%file%~) BEGIN
		COPY_LARGE + ~%bgee_dir%/override/%file%~ ~EET/temp/array~
	END ELSE BEGIN
		AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --out "EET/temp/array" --biff-get-rest "%file%"~
		LAM bash_log
	END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BG2:EE files patching                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~BG2:EE files patching...~

/////                                                  \\\\\
///// ADD_PROJECTILE                                   \\\\\
/////                                                  \\\\\

PRINT ~Adding projectiles...~

COPY_EXISTING ~projectl.ids~ ~override~
	~missile.ids~ ~override~
	REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
	REPLACE_TEXTUALLY ~[%TAB% ]+~ ~ ~
	REPLACE_TEXTUALLY ~^[%TAB% ]+~ ~~
	REPLACE_TEXTUALLY ~[%TAB% ]+$~ ~~
BUT_ONLY

COPY + ~EET/temp/array/missile.ids~ ~EET/temp/array~
	REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
	REPLACE_TEXTUALLY ~[%TAB% ]+~ ~ ~
	REPLACE_TEXTUALLY ~^[%TAB% ]+~ ~~
	REPLACE_TEXTUALLY ~[%TAB% ]+$~ ~~

COPY + ~EET/temp/array/projectl.ids~ ~EET/temp/array~
	REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
	REPLACE_TEXTUALLY ~[%TAB% ]+~ ~ ~
	REPLACE_TEXTUALLY ~^[%TAB% ]+~ ~~
	REPLACE_TEXTUALLY ~[%TAB% ]+$~ ~~
	COUNT_2DA_ROWS 2 cntrow
	FOR (cnt = 0; cnt <= "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 2 "col1"
		PATCH_IF IS_AN_INT col1 BEGIN
			SET pro_num_int = col1
			SET pro_num_int_mis = pro_num_int + 1
			READ_2DA_ENTRY cnt 1 2 "col2"
			SPRINT match ~%col2%~ SPRINT res ~pro~ LPM EET_PCU_outer_res PATCH_IF (NOT ~%col2%~ STRING_EQUAL_CASE ~%match%~) BEGIN SET_2DA_ENTRY cnt 1 2 "%match%" SPRINT col2 ~%match%~ END
			INNER_ACTION BEGIN
				COPY_EXISTING ~projectl.ids~ ~override~
					REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
					COUNT_REGEXP_INSTANCES ~^%col1% %col2%$~ num_matches
					PATCH_IF (num_matches = 0) BEGIN
						COUNT_REGEXP_INSTANCES ~^%pro_num_int% %col2%$~ num_matches
					END
					COUNT_REGEXP_INSTANCES ~ %col2%$~ num_matches2
				BUT_ONLY
				ACTION_IF (num_matches > 0) BEGIN
				END ELSE ACTION_IF (num_matches2 > 0) BEGIN
					COPY_EXISTING - ~projectl.ids~ ~.../projectl.ids~
						SET updated = 0
						REPLACE_EVALUATE CASE_INSENSITIVE ~^\([A-Za-z0-9]+\) %col2%$~ BEGIN
							PATCH_IF (updated = 0) BEGIN
								SPRINT pro_num2 ~%MATCH1%~
								SET pro_num_int2 = pro_num2 + 1
								PATCH_IF (pro_num_int_mis != pro_num_int2) BEGIN
									DEFINE_ASSOCIATIVE_ARRAY remapped_ipro BEGIN ~%pro_num_int_mis%~ => ~%pro_num_int2%~ END
									SPRINT log_remapped_ipro ~%log_remapped_ipro%%TAB%%pro_num_int_mis%%TAB%=>%TAB%%pro_num_int2%%TAB%%slash%%slash%%col2%%LNL%~
								END
								SET updated = 1
							END
						END ~~
				END ELSE ACTION_IF (NOT FILE_EXISTS ~%patch_dir%/pro/%col2%.pro~) BEGIN
					PRINT ~Skipping ADD_PROJECTILE: %patch_dir%/pro/%col2%.pro - doesn't exist~
				END ELSE BEGIN //for some reason ADD_PROJECTILE can't add pro staring with 1
					ADD_PROJECTILE ~%patch_dir%/pro/%col2%.pro~
					OUTER_SPRINT ret EVAL ~%%col2%%~
					/*COPY + ~%backup_dir%/0/UNINSTALL.0~ ~%backup_dir%/0~ //the file is not patched yet
						REPLACE_TEXTUALLY ~override.%ret%.pro~ ~~
					DELETE + ~override/%col2%.pro~*/
					COPY - ~EET/temp/array/missile.ids~ ~.../temp/array/missile.ids~
						SET updated = 0
						REPLACE_EVALUATE CASE_INSENSITIVE ~^%pro_num_int_mis% \(.+\)$~ BEGIN
							PATCH_IF (updated = 0) BEGIN
								SPRINT mis_anme ~%MATCH1%~
								PATCH_IF (pro_num_int_mis != ret) BEGIN
									DEFINE_ASSOCIATIVE_ARRAY remapped_ipro BEGIN ~%pro_num_int_mis%~ => ~%ret%~ END
									SPRINT log_remapped_ipro ~%log_remapped_ipro%%TAB%%pro_num_int_mis%%TAB%=>%TAB%%ret%%TAB%%slash%%slash%%col2%%LNL%~
								END
								SET updated = 1
							END
						END ~~
					ACTION_IF (updated = 1) AND (NOT ~%col2%~ STRING_EQUAL_CASE ~%mis_anme%~) BEGIN
						COPY_EXISTING ~missile.ids~ ~override~
							REPLACE_TEXTUALLY ~^%ret% %col2%$~ ~%ret% %mis_anme%~
						BUT_ONLY
						PRINT ~Changed MISSILE.IDS reference name: %col2% -> %mis_anme%~
					END
				END
			END
		END
	END

/////                                                  \\\\\
///// ADD_SPELL                                        \\\\\
/////                                                  \\\\\

PRINT ~Adding spells...~

COPY_EXISTING ~spell.ids~ ~override~
	REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
	REPLACE_TEXTUALLY ~[%TAB% ]+~ ~ ~
	REPLACE_TEXTUALLY ~^[%TAB% ]+~ ~~
	REPLACE_TEXTUALLY ~[%TAB% ]+$~ ~~
	REPLACE_TEXTUALLY ~2924 BERESH_CHANGE%LNL%~ ~~ //WIZARD_SUMMON_PLANATAR_EVIL dupe
	REPLACE_TEXTUALLY ~2925 KAISHAS_CHANGE%LNL%~ ~~ //WIZARD_COMET dupe
BUT_ONLY

OUTER_SPRINT log ~~
COPY + ~EET/temp/array/spell.ids~ ~EET/temp/array~
	REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~
	REPLACE_TEXTUALLY ~[%TAB% ]+~ ~ ~
	REPLACE_TEXTUALLY ~^[%TAB% ]+~ ~~
	REPLACE_TEXTUALLY ~[%TAB% ]+$~ ~~
	REPLACE_TEXTUALLY ~2924 BERESH_CHANGE%LNL%~ ~~
	REPLACE_TEXTUALLY ~2925 KAISHAS_CHANGE%LNL%~ ~~
	COUNT_2DA_ROWS 2 cntrow
	FOR (cnt = 0; cnt <= "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 2 idscode
		PATCH_IF IS_AN_INT idscode BEGIN
			READ_2DA_ENTRY cnt 1 2 spell_name
			PATCH_IF (idscode >= 4000) BEGIN
				SPRINT type ~SPCL~
				SET spell_type = 4
			END ELSE PATCH_IF (idscode >= 3000) BEGIN
				SPRINT type ~SPIN~
				SET spell_type = 3
			END ELSE PATCH_IF (idscode >= 2000) BEGIN
				SPRINT type ~SPWI~
				SET spell_type = 2
			END ELSE BEGIN
				SPRINT type ~SPPR~
				SET spell_type = 1
			END
			INNER_PATCH_SAVE source_res ~%idscode%~ BEGIN
				REPLACE_TEXTUALLY ~^[1-4]~ ~%type%~
			END
			INNER_PATCH_SAVE spell_level ~%idscode%~ BEGIN
				REPLACE_TEXTUALLY ~^.\(.\).+$~ ~\1~
			END
			INNER_ACTION BEGIN
				ACTION_IF (NOT FILE_EXISTS ~%patch_dir%/spl/%source_res%.spl~) BEGIN
					PRINT ~Skipping SPELL.IDS reference: %source_res%%TAB%%idscode%%TAB%%spell_name%~
				END ELSE BEGIN
					OUTER_SET ref_exists = 0
					ACTION_IF (FILE_CONTAINS_EVALUATED (~spell.ids~ ~ %spell_name%$~)) BEGIN
						LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL ~%spell_name%~ RET spell_res spell_num END
						ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%spell_res%.spl~) BEGIN
							PRINT ~Resource from SPELL.IDS is referenced but doesn't exist in game: %source_res%%TAB%%idscode%%TAB%%spell_name%%TAB%%spell_res%%TAB%%spell_num%%TAB%%spell_name%~
						END ELSE ACTION_IF (~%idscode%~ STRING_EQUAL_CASE ~%spell_num%~) BEGIN
							PRINT ~Resource from SPELL.IDS already exists in game: %source_res%%TAB%%idscode%%TAB%%spell_name%~
							OUTER_SET ref_exists = 1
						END ELSE BEGIN
							PRINT ~Resource from SPELL.IDS already exists in game under different ID: %source_res%%TAB%%idscode%%TAB%%spell_name%%TAB%%spell_res%%TAB%%spell_num%%TAB%%spell_name%~
							OUTER_SPRINT log ~%log%%source_res%%TAB%%idscode%%TAB%%spell_name%%TAB%%spell_res%%TAB%%spell_num%%TAB%%spell_name%%LNL%~
							OUTER_SET ref_exists = 1
						END
						ACTION_IF (ref_exists = 1) BEGIN
							ACTION_FOR_EACH dir IN ~%patch_dir%/spl~ ~%patch_dir%/eff~ BEGIN
								ACTION_BASH_FOR ~%dir%~ ~^%source_res%.*$~ BEGIN
									OUTER_PATCH_SAVE res ~%BASH_FOR_RES%~ BEGIN
										REPLACE_TEXTUALLY ~^%source_res%~ ~%spell_res%~
									END
									DELETE + ~%BASH_FOR_FILESPEC%~
									ACTION_IF (NOT ~%BASH_FOR_RES%~ STRING_EQUAL_CASE ~%res%~) BEGIN
										ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~spl~ BEGIN
											ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_spl BEGIN ~%BASH_FOR_RES%~ => ~%res%~ END
											OUTER_SPRINT log_remapped_spl ~%log_remapped_spl%%TAB%%BASH_FOR_RES%%TAB%=>%TAB%%res%%TAB%%slash%%slash%%spell_name%%LNL%~
										END ELSE ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~eff~ BEGIN
											ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_eff BEGIN ~%BASH_FOR_RES%~ => ~%res%~ END
											OUTER_SPRINT log_remapped_eff ~%log_remapped_eff%%TAB%%BASH_FOR_RES%%TAB%=>%TAB%%res%%TAB%%slash%%slash%%spell_name%%LNL%~
										END
									END
								END
							END
						END
					END
					ACTION_IF (ref_exists = 0) BEGIN
						/*ACTION_IF (spell_type = 3) AND (spell_level = 1) BEGIN
							OUTER_SET spell_level = 2 //not enough level 1 slots
						END
						ACTION_IF ~%source_res%~ STRING_EQUAL_CASE ~name_of_the_spell~ BEGIN
							OUTER_SET spell_level = 0 //not enough level 7 slots
						END*/
						PRINT ~ADD_SPELL: %patch_dir%/spl/%source_res%.spl %spell_type% %spell_level% %spell_name%~
						ADD_SPELL ~%patch_dir%/spl/%source_res%.spl~ spell_type spell_level ~%spell_name%~
						LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL ~%spell_name%~ RET spell_res END
						PRINT ~Renamed spell: %source_res% -> %spell_res%~
						OUTER_SPRINT EVAL ~%spell_name%_RES~ ~%spell_res%~
						ACTION_IF ~%spell_res%~ STRING_CONTAINS_REGEXP ~[A-Z]+[0-9]+~ BEGIN
							PRINT ~ADD_SPELL failed to add %patch_dir%/spl/%source_res%.spl~
						END ELSE BEGIN
							/*COPY + ~%backup_dir%/0/UNINSTALL.0~ ~%backup_dir%/0~ //the file is not patched yet
								REPLACE_TEXTUALLY ~override.%spell_res%.spl~ ~~
							DELETE + ~override/%spell_res%.spl~*/
							ACTION_FOR_EACH dir IN ~%patch_dir%/spl~ ~%patch_dir%/eff~ ~%biff_dir%~ BEGIN
								MKDIR ~%dir%/ADD_SPELL~
								ACTION_BASH_FOR ~%dir%~ ~^%source_res%.*$~ BEGIN
									OUTER_PATCH_SAVE res ~%BASH_FOR_RES%~ BEGIN
										REPLACE_TEXTUALLY ~^%source_res%~ ~%spell_res%~
									END
									ACTION_IF (NOT FILE_EXISTS ~%BASH_FOR_DIRECTORY%/ADD_SPELL/%res%.%BASH_FOR_EXT%~) BEGIN
										MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/ADD_SPELL/%res%.%BASH_FOR_EXT%~
									END ELSE BEGIN
										WARN ~WARNING: %BASH_FOR_DIRECTORY%/ADD_SPELL/%res%.%BASH_FOR_EXT% already exists~
									END
									ACTION_IF (NOT ~%BASH_FOR_RES%~ STRING_EQUAL_CASE ~%res%~) BEGIN
										ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~spl~ BEGIN
											ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_spl BEGIN ~%BASH_FOR_RES%~ => ~%res%~ END
											OUTER_SPRINT log_remapped_spl ~%log_remapped_spl%%TAB%%BASH_FOR_RES%%TAB%=>%TAB%%res%%TAB%%slash%%slash%%spell_name%%LNL%~
										END ELSE ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~eff~ BEGIN
											ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_eff BEGIN ~%BASH_FOR_RES%~ => ~%res%~ END
											OUTER_SPRINT log_remapped_eff ~%log_remapped_eff%%TAB%%BASH_FOR_RES%%TAB%=>%TAB%%res%%TAB%%slash%%slash%%spell_name%%LNL%~
										END ELSE ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~bam~ BEGIN
											ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_bam BEGIN ~%BASH_FOR_RES%~ => ~%res%~ END
											OUTER_SPRINT log_remapped_bam ~%log_remapped_bam%%TAB%%BASH_FOR_RES%%TAB%=>%TAB%%res%%TAB%%slash%%slash%%spell_name%%LNL%~
										END
									END
								END
							END
						END
						COPY_EXISTING ~spell.ids~ ~override~
							REPLACE_TEXTUALLY ~%WNL%~ ~%LNL%~ //just to be sure that ADD_SPELL didn't brake it
						BUT_ONLY
					END
				END
			END
		END
	END

//below code need to be done after all new spells are already present within spell.ids

//expand remaining PCU variables
COPY + ~.../blank.txt~ ~EET/temp/add_spell_temp.txt~
APPEND_OUTER + ~EET/temp/add_spell_temp.txt~ ~%log%~
COPY + ~EET/temp/add_spell_temp.txt~ ~EET/temp~
	COUNT_2DA_ROWS 3 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 3 "col1" //file name
		READ_2DA_ENTRY cnt 1 3 "col2" //IDS num
		READ_2DA_ENTRY cnt 2 3 "col3" //IDS name
		INNER_ACTION BEGIN
			ACTION_IF (FILE_CONTAINS_EVALUATED (~spell.ids~ ~^%col2% ~)) BEGIN
				LAF RES_NAME_OF_SPELL_NUM INT_VAR spell_num = col2 RET spell_name END
				ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_splName BEGIN ~%spell_name%~ => ~%col3%~ END
				OUTER_SPRINT log_remapped_splName ~%log_remapped_splName%%TAB%%spell_name%%TAB%=>%TAB%%col3%%TAB%%slash%%slash%%col3%%LNL%~
			END ELSE BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_splName BEGIN ~%col2%~ => ~%col3%~ END
				OUTER_SPRINT log_remapped_splName ~%log_remapped_splName%%TAB%%col2%%TAB%=>%TAB%%col3%%TAB%%slash%%slash%%col3%%LNL%~
			END
		END
	END

ACTION_BASH_FOR ~%patch_dir%/spl/ADD_SPELL~ ~^[A-Z]+[1-4][0-9][0-9].spl$~ BEGIN //loop only through spells added by ADD_SPELL (ignores those just renamed)
	LAF NAME_NUM_OF_SPELL_RES STR_VAR spell_res = EVAL ~%BASH_FOR_RES%~ RET spell_num spell_name END
	OUTER_SET spell_num_sav = spell_num
	OUTER_SPRINT spell_name_sav ~%spell_name%~
	COPY - ~EET/temp/array/spell.ids~ ~.../temp/array/spell.ids~
		REPLACE_EVALUATE CASE_INSENSITIVE ~\([0-9]+\) %spell_name%$~ BEGIN
			PATCH_IF (FILE_CONTAINS_EVALUATED (~spell.ids~ ~^%MATCH1% ~)) BEGIN
				INNER_ACTION BEGIN
					LAF RES_NAME_OF_SPELL_NUM INT_VAR spell_num = MATCH1 RET spell_name END
					ACTION_IF (NOT ~%spell_name%~ STRING_EQUAL_CASE ~%spell_name_sav%~) BEGIN
						ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_splName BEGIN ~%spell_name%~ => ~%spell_name_sav%~ END
						OUTER_SPRINT log_remapped_splName ~%log_remapped_splName%%TAB%%spell_name%%TAB%=>%TAB%%spell_name_sav%%TAB%%slash%%slash%%spell_name_sav%%LNL%~
					END
				END
			END ELSE PATCH_IF (MATCH1 != spell_num_sav) BEGIN
				DEFINE_ASSOCIATIVE_ARRAY remapped_splName BEGIN ~%MATCH1%~ => ~%spell_name_sav%~ END
				SPRINT log_remapped_splName ~%log_remapped_splName%%TAB%%MATCH1%%TAB%=>%TAB%%spell_name_sav%%TAB%%slash%%slash%%spell_name_sav%%LNL%~
			END
		END ~~
END

ACTION_FOR_EACH dir IN ~%biff_dir%~ ~%patch_dir%/eff~ ~%patch_dir%/spl~ BEGIN
	ACTION_BASH_FOR ~%dir%/ADD_SPELL~ ~^.*$~ BEGIN
		ACTION_IF (FILE_EXISTS ~%dir%/%BASH_FOR_FILE%~) BEGIN
			WARN ~WARNING: %BASH_FOR_FILESPEC% already exists in %dir%~
		END
		COPY + ~%BASH_FOR_FILESPEC%~ ~%dir%~
	END
END

/////                                                  \\\\\
///// Chapter numbers                                  \\\\\
/////                                                  \\\\\

//changes chapter numbers in BG2 to use EET continuous chapter convention
ACTION_FOR_EACH var IN BCS DLG BEGIN
	COPY_EXISTING_REGEXP GLOB ~^.+\.%var%$~ ~override~
		PATCH_IF (NOT FILE_EXISTS ~EET/temp/patch/%var%/%SOURCE_FILE%~) BEGIN //need to be added before converting BCS and DLG to BAF and D
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY ~IncrementGlobal~ ~IncrementReplaceMe~ //we don't want to patch this one
				REPLACE_EVALUATE CASE_INSENSITIVE ~\([A-Za-z]*Global[A-Za-z]*("CHAPTER","GLOBAL",\)\([0-9]+\))~ BEGIN //this should take care of SetGlobal, Global, GlobalGT, GlobalLT
					SET value = MATCH2 + 12
					PATCH_PRINT ~Patching %SOURCE_FILESPEC%: %MATCH2% => %MATCH1%%value%)~
				END
				~%MATCH1%%value%)~
				REPLACE_TEXTUALLY ~IncrementReplaceMe~ ~IncrementGlobal~
			END
		END
	BUT_ONLY
END

/////                                                  \\\\\
///// Patching each group of BG2:EE resources          \\\\\
/////                                                  \\\\\

////////////////////////
// Viader's Extended Animations
////////////////////////
//0xE240 - 0xE2FF - reserved by Extended Animations
//0xE330 - 0xE3FF - free
//0xE440 - 0xE4FF - free
//0xE530 - 0xE5FF - free
//0xE620 - 0xE6FF - free
//0xE730 - 0xE7FF - free
//0xE850 - 0xE8FF - free
//0xE920 - 0xE9FF - free
//0xEA30 - 0xEAFF - free
//0xEB30 - 0xEBFF - free
//0xEC30 - 0xECFF - free
//0xED30 - 0xEDFF - free
//0xEE20 - 0xEEFF - free

ACTION_FOR_EACH var IN IDS TLK 2DA CRE BCS DLG BEGIN
	PRINT ~Patching BG2:EE %var% files~
	INCLUDE ~EET/lib/bg2_%var%.tph~
END

/////                                                  \\\\\
///// LUA / MENU BG2:EE patching                       \\\\\
/////                                                  \\\\\

//no point in patching UI.MENU
COPY ~EET/base/lua/UI_BG2.MENU~ ~override/UI.MENU~

COPY ~EET/base/lua/UI_SoD.MENU~ ~EET/SoD_GUI/UI.MENU~
	/*REPLACE_TEXTUALLY ~text[ %TAB%]+style[ %TAB%"']*\([A-Za-z_]+\)[ %TAB%"']*~ ~text style '\1_sod'~
	DEFINE_ASSOCIATIVE_ARRAY remapped_strref BEGIN
		32129 => 102624
		32208 => 102626
		32729 => 103144
		24315 => 9588
		24364 => 16313
		24359 => 16308
		24358 => 16307
		24356 => 16306
		24360 => 16309
		24357 => 14930
		24362 => 16311
		31813 => 74261
		31827 => 74270
	END
	REPLACE_EVALUATE CASE_INSENSITIVE ~\(Infinity_FetchString(\)\([0-9]+\)\()\)~ BEGIN
		PATCH_IF (VARIABLE_IS_SET $remapped_strref(~%MATCH2%~)) BEGIN
			TEXT_SPRINT MATCH2 $remapped_strref(~%MATCH2%~)
		END
	END ~%MATCH1%%MATCH2%%MATCH3%~
	REPLACE_EVALUATE CASE_INSENSITIVE ~\(getTooltipWithHotkey([0-9]+,\)\([0-9]+\)\()\)~ BEGIN
		PATCH_IF (VARIABLE_IS_SET $remapped_strref(~%MATCH2%~)) BEGIN
			TEXT_SPRINT MATCH2 $remapped_strref(~%MATCH2%~)
		END
	END ~%MATCH1%%MATCH2%%MATCH3%~*/

OUTER_SPRINT L_LUA ~~
ACTION_FOR_EACH entry IN EET_CMP_TEXT_BG1 EET_CMP_TEXT_SOD EET_CMP_TEXT_SOA EET_CMP_TEXT_TOB EET_CMP_TEXT_BP1 EET_CMP_TEXT_BP2 EET_CMP_TEXT_TUT EET_CMP_TITLE_BG1 EET_CMP_TITLE_SOD EET_CMP_TITLE_SOA EET_CMP_TITLE_TOB EET_CMP_TITLE_BP1 EET_CMP_TITLE_BP2 EET_CMP_TITLE_TUT EET_WARNING_TITLE EET_WARNING_TEXT EET_CHAPTER_8 EET_CHAPTER_9 EET_CHAPTER_10 EET_CHAPTER_11 EET_CHAPTER_12 BEGIN
	ACTION_IF (~%entry%~ STRING_EQUAL_CASE ~EET_CMP_TEXT_BG1~) BEGIN
		OUTER_SET cnt = 4000100
	END ELSE ACTION_IF (~%entry%~ STRING_EQUAL_CASE ~EET_CHAPTER_8~) BEGIN
		OUTER_SET cnt = 2000059
	END
	OUTER_SET strref = RESOLVE_STR_REF ((AT cnt))
	ACTION_GET_STRREF strref "string"
	OUTER_PATCH_SAVE string ~%string%~ BEGIN
		REPLACE_TEXTUALLY ~%LNL%~ ~\n~
		REPLACE_TEXTUALLY ~"~ ~\"~
	END
	OUTER_SPRINT L_LUA ~%L_LUA%%LNL%%TAB%%entry% = "%string%",~
	OUTER_SET cnt = cnt + 1
END

COPY ~EET/base/lua/M_EET.lua~ ~override~
	REPLACE_TEXTUALLY ~\(eetStrings = {\)~ ~\1
%L_LUA%~

COPY ~EET/base/lua/util.lua~ ~override~

COPY_EXISTING ~BGEE.LUA~ ~override~
	REPLACE_TEXTUALLY ~chapterBackgrounds\[[0-9]+\][ %TAB%]*=[ %TAB%]*"GUICHP.+"[%newline%]*~ ~~
	REPLACE_TEXTUALLY ~{'intro',.+[%newline%]*~ ~~
	REPLACE_TEXTUALLY ~{'blackpit',.+[%newline%]*~ ~~
	REPLACE_TEXTUALLY ~{'sodcin0[1-3]',.+[%newline%]*~ ~~
	REPLACE_TEXTUALLY ~JOURNAL_MODE_EDIT = 3,~ ~JOURNAL_MODE_EDIT = 3,
	
	START_CAMPAIGN_BG1 = 1,
	START_CAMPAIGN_SOD = 2,
	START_CAMPAIGN_SOA = 3,
	START_CAMPAIGN_TOB = 4,
	START_CAMPAIGN_BP1 = 5,
	START_CAMPAIGN_BP2 = 6,
	START_CAMPAIGN_TUT = 7,~
	REPLACE_TEXTUALLY ~styles[ %TAB%]*=[%newline%]*{~ ~styles =
{
	gamelog_sod =
	{
		color = 'B',
		font = 'POSTANTI',
		point = 12,
		useFontZoom = 1,
		valign = 'top',
		halign = 'left',
	},

	normal_sod =
	{
		color = 'B',
		font = 'MODESTOM',
		point = 14,
		useFontZoom = 1,
		valign = 'top',
		halign = 'left',
	},
	normal_parchment_sod =
	{
		color = '5',
		font = 'POSTANTI',
		point = 12,
		useFontZoom = 1,
		valign = 'top',
		halign = 'left',
	},
	title_sod =
	{
		color = '1',
		font = 'SHERWOOD',
		point = 20,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
	},
	parchment_sod = 
	{
		color = '5',
		font = 'POSTANTI' ,
		point = 12,
		useFontZoom = 1,
		valign = 'top',
		halign = 'left',
	},
	button_sod =
	{
		color = 'B',
		font = 'MODESTOM' ,
		point = 18,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
		upper = 1,
		pad = {8,8,8,8},
	},
	label_sod =
	{
		color = 'B',
		font = 'MODESTOM',
		point = 14,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
	},
	label_parchment_sod =
	{
		color = '5',
		font = 'POSTANTI',
		point = 12,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
	},
	button_parchment_sod =
	{
		color = '5',
		font = 'POSTANTI',
		point = 12,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
		pad = {0,5,0,0},
	},
	edit_sod =
	{
		color = 'B',
		font = 'MODESTOM',
		point = 14,
		useFontZoom = 1,
		valign = 'top',
		halign = 'left',
	},
	edit_parchment_sod =
	{
		color = '5',
		font = 'POSTANTI',
		point = 12,
		useFontZoom = 1,
		valign = 'top',
		halign = 'left',
	},
	list_sod =
	{
		color = 'B',
		font = 'STONESML',
		point = 18,
		useFontZoom = 0,
		valign = 'center',
		halign = 'left',
	},
	list_parchment_sod =
	{
		color = '5',
		font = 'POSTANTI',
		point = 12,
		useFontZoom = 0,
		valign = 'center',
		halign = 'left',
	},
	gold_sod =
	{
		color = '$',
		font = 'MODESTOM',
		point = 12,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
	},
	label_loadname_sod =
	{
		color = 'B',
		font = 'MODESTOM',
		point = 20,
		useFontZoom = 0,
		valign = 'center',
		halign = 'center',
	},~
BUT_ONLY

COPY + ~EET/temp/array/BGEE.lua~ ~EET/temp/array~
	//REPLACE_TEXTUALLY ~--{'sodcin0~ ~{'sodcin0~ //http://redmine.beamdog.com/issues/24733
	REPLACE_TEXTUALLY ~'intro', 719, 779,~ ~'intro', 569, 629,~
	REPLACE_TEXTUALLY ~'intro', 809, 869,~ ~'intro', 618, 668,~
	REPLACE_TEXTUALLY ~'intro', 889, 959,~ ~'intro', 669, 741,~
	REPLACE_TEXTUALLY ~'intro', 949, 989,~ ~'intro', 760, 820,~
	REPLACE_TEXTUALLY ~{'BDSHAM1L', 1}~ ~{'BDSHAM1', 1}~
	REPLACE_TEXTUALLY ~{'BDORCM1L', 1}~ ~{'BDORCM1', 1}~
	REPLACE_TEXTUALLY ~{'BDSHAF1L', 2}~ ~{'BDSHAF1', 2}~
	REPLACE_TEXTUALLY ~{'BDORCF1L', 2}~ ~{'BDORCF1', 2}~
	SPRINT addDLC_var ~~
	SPRINT addDLCContent_var ~~
	SPRINT filenames_stringrefs_var ~~
	SPRINT chapterBackgrounds_var ~~
	SPRINT createQuest_var ~~
	SPRINT createEntry_var ~~
	//SPRINT portraits_var ~~ //check if this will be fixed in future: http://redmine.beamdog.com/issues/21516
	SPRINT portraits_var ~	{'YANNER4', 1},
	{'YANNER5', 1},
	{'YANNER6', 1},
	{'YANNER1', 2},
	{'YANNER2', 2},
	{'YANNER3', 2},
	{'OHHEXX', 2},
~
	SPRINT movies_var ~~
	//SPRINT cheat_var ~~ //check if adding these areas will be needed in future patches
	SPRINT cheat_var ~	--BG Areas
	{"BG0002", "N Baldur's Gate: Temple of Helm"},
	{"BG0004", "N Baldur's Gate: Bheren's House L1"},
	{"BG0005", "N Baldur's Gate: Bheren's House L2"},
	{"BG0006", "N Baldur's Gate: Rinnie's House L1"},
	{"BG0007", "N Baldur's Gate: Rinnie's House L2"},
	{"BG0008", "N Baldur's Gate: House1 L1"},
	{"BG0009", "N Baldur's Gate: House1 L2"},
	{"BG0010", "N Baldur's Gate: Jardak's House L1"},
	{"BG0011", "N Baldur's Gate: Jardak's House L2"},
	{"BG0012", "N Baldur's Gate: House2 L1"},
	{"BG0013", "N Baldur's Gate: House2 L2"},
	{"BG0014", "N Baldur's Gate: House3 L1"},
	{"BG0015", "N Baldur's Gate: House3 L2"},
	{"BG0016", "N Baldur's Gate: House4 L1"},
	{"BG0017", "N Baldur's Gate: House4 L2"},
	{"BG0018", "N Baldur's Gate: House5 L1"},
	{"BG0019", "N Baldur's Gate: House5 L2"},
	{"BG0020", "N Baldur's Gate: House6 L1"},
	{"BG0021", "N Baldur's Gate: House6 L2"},
	{"BG0100", "NW Baldur's Gate"},
	{"BG0101", "NW Baldur's Gate: Silvershield Estate L1"},
	{"BG0102", "NW Baldur's Gate: Silvershield Estate L2"},
	{"BG0103", "NE Baldur's Gate: Splurging Sturgeon L1"},
	{"BG0104", "NE Baldur's Gate: Splurging Sturgeon L2"},
	{"BG0105", "SE Baldur's Gate: Blade & Stars L1"},
	{"BG0106", "SE Baldur's Gate: Blade & Stars L2"},
	{"BG0107", "SE Baldur's Gate: Blade & Stars L3"},
	{"BG0108", "N Baldur's Gate: Ducal Palace L1"},
	{"BG0109", "N Baldur's Gate: Ducal Palace L2"},
	{"BG0110", "N Baldur's Gate: Ducal Palace L3"},
	{"BG0111", "N Baldur's Gate: Ducal Palace Cellar"},
	{"BG0112", "Baldur's Gate Undercellars"},
	{"BG0114", "NE Baldur's Gate: Blushing Mermaid L1"},
	{"BG0115", "NE Baldur's Gate: Blushing Mermaid L2"},
	{"BG0116", "NW Baldur's Gate: Helm & Cloak L1"},
	{"BG0117", "NW Baldur's Gate: Helm & Cloak L2"},
	{"BG0118", "NW Baldur's Gate: Helm & Cloak L3"},
	{"BG0119", "N Baldur's Gate: Three Old Kegs L1"},
	{"BG0120", "N Baldur's Gate: Three Old Kegs L2"},
	{"BG0121", "N Baldur's Gate: Three Old Kegs L3"},
	{"BG0123", "Undercity"},
	{"BG0125", "Undercity: Temple of Bhaal"},
	{"BG0126", "W Baldur's Gate: Ragefast's House"},
	{"BG0127", "SW Baldur's Gate: Merchant League L1"},
	{"BG0128", "SW Baldur's Gate: Merchant League L2"},
	{"BG0129", "SW Baldur's Gate: Merchant League L3"},
	{"BG0130", "W Baldur's Gate: Hall of Wonders"},
	{"BG0131", "W Baldur's Gate: High House of Wonders"},
	{"BG0132", "W Baldur's Gate: Lady's House"},
	{"BG0133", "Baldur's Gate Docks: Low Lantern D1"},
	{"BG0134", "Baldur's Gate Docks: Low Lantern D2"},
	{"BG0135", "Baldur's Gate Docks: Low Lantern D3"},
	{"BG0136", "Baldur's Gate Docks: Low Lantern D4"},
	{"BG0137", "N Baldur's Gate: Ramazith's Tower L1"},
	{"BG0138", "N Baldur's Gate: Ramazith's Tower L6"},
	{"BG0139", "N Baldur's Gate: Ramazith's Tower L2"},
	{"BG0140", "N Baldur's Gate: Ramazith's Tower L3"},
	{"BG0141", "N Baldur's Gate: Ramazith's Tower L4"},
	{"BG0142", "N Baldur's Gate: Ramazith's Tower L5"},
	{"BG0143", "Central Baldur's Gate: Oberon's Estate L1"},
	{"BG0144", "Central Baldur's Gate: Oberon's Estate L2"},
	{"BG0145", "Central Baldur's Gate: Oberon's Estate L3"},
	{"BG0146", "Thieves' Maze"},
	{"BG0148", "E Baldur's Gate: House5"},
	{"BG0149", "NE Baldur's Gate: Ilmater's Shrine"},
	{"BG0150", "E Baldur's Gate: House2"},
	{"BG0151", "E Baldur's Gate: House3"},
	{"BG0152", "E Baldur's Gate: House4"},
	{"BG0153", "E Baldur's Gate: Thieves' Guild"},
	{"BG0154", "E Baldur's Gate: Keexie Tavern L1"},
	{"BG0155", "NW Baldur's Gate: House3 L1"},
	{"BG0156", "NW Baldur's Gate: House3 L2"},
	{"BG0157", "NW Baldur's Gate: Quinn's House L1"},
	{"BG0158", "NW Baldur's Gate: Quinn's House L2"},
	{"BG0159", "NW Baldur's Gate: House1 L1"},
	{"BG0160", "NW Baldur's Gate: House1 L2"},
	{"BG0161", "NW Baldur's Gate: Degrodel's House"},
	{"BG0162", "NW Baldur's Gate: Laerta's House L1"},
	{"BG0163", "NW Baldur's Gate: House2 L1"},
	{"BG0164", "NW Baldur's Gate: House2 L2"},
	{"BG0165", "NW Baldur's Gate: Tavern L1"},
	{"BG0166", "NW Baldur's Gate: Tavern L2"},
	{"BG0167", "NW Baldur's Gate: Pheirkas' House L1"},
	{"BG0168", "NW Baldur's Gate: Pheirkas' House L2"},
	{"BG0169", "NW Baldur's Gate: House4 L1"},
	{"BG0170", "NW Baldur's Gate: House4 L2"},
	{"BG0171", "E Baldur's Gate: Keexie Tavern L2"},
	{"BG0200", "N Baldur's Gate"},
	{"BG0224", "W Sewers"},
	{"BG0225", "Central Sewers"},
	{"BG0226", "E Sewers"},
	{"BG0300", "NE Baldur's Gate"},
	{"BG0302", "NE Baldur's Gate: House2 L1"},
	{"BG0303", "NE Baldur's Gate: House2 L2"},
	{"BG0304", "NE Baldur's Gate: Store"},
	{"BG0305", "NE Baldur's Gate: House1 L1"},
	{"BG0306", "NE Baldur's Gate: House1 L2"},
	{"BG0307", "NE Baldur's Gate: Counting House L1"},
	{"BG0308", "NE Baldur's Gate: Counting House L2"},
	{"BG0400", "Upper Chionthar"},
	{"BG0401", "Upper Chionthar: Farmhouse"},
	{"BG0500", "Durlag's Tower"},
	{"BG0501", "Durlag's Tower: Cellar"},
	{"BG0502", "Durlag's Tower: L1"},
	{"BG0503", "Durlag's Tower: L2"},
	{"BG0504", "Durlag's Tower: L3"},
	{"BG0505", "Durlag's Tower: L4"},
	{"BG0506", "Durlag's Tower: Chessboard"},
	{"BG0507", "Durlag's Tower: Ice Chamber"},
	{"BG0508", "Durlag's Tower: Fire Chamber"},
	{"BG0509", "Durlag's Tower: Air Chamber"},
	{"BG0510", "Durlag's Tower: Earth Chamber"},
	{"BG0511", "Durlag's Tower: D1"},
	{"BG0512", "Durlag's Tower: D2"},
	{"BG0513", "Durlag's Tower: D3"},
	{"BG0514", "Durlag's Tower: D4"},
	{"BG0515", "Durlag's Tower: Compass Room"},
	{"BG0516", "Durlag's Tower: Demonknight's Chamber"},
	{"BG0600", "W Baldur's Gate"},
	{"BG0601", "SW Baldur's Gate: Seven Suns L1"},
	{"BG0602", "SW Baldur's Gate: Seven Suns L2"},
	{"BG0603", "SW Baldur's Gate: Seven Suns Cellar"},
	{"BG0606", "SW Baldur's Gate: Flaming Fist HQ Cellar"},
	{"BG0607", "SW Baldur's Gate: Flaming Fist HQ L1"},
	{"BG0608", "SW Baldur's Gate: Flaming Fist HQ L2"},
	{"BG0609", "Baldur's Gate Docks: Water-Queen's House"},
	{"BG0611", "Baldur's Gate Docks: Iron Throne Cellar"},
	{"BG0612", "Baldur's Gate Docks: Iron Throne L2"},
	{"BG0613", "Baldur's Gate Docks: Iron Throne L3"},
	{"BG0614", "Baldur's Gate Docks: Iron Throne L4"},
	{"BG0615", "Baldur's Gate Docks: Iron Throne L5"},
	{"BG0616", "Baldur's Gate Docks: Iron Throne L1"},
	{"BG0617", "W Baldur's Gate: Tremain's House L1"},
	{"BG0618", "W Baldur's Gate: Tremain's House L2"},
	{"BG0619", "W Baldur's Gate: Fenten's House L1"},
	{"BG0620", "W Baldur's Gate: Fenten's House L2"},
	{"BG0621", "Baldur's Gate Docks: Iron Throne Roof"},
	{"BG0700", "Central Baldur's Gate"},
	{"BG0702", "Baldur's Gate Docks: Harbormaster"},
	{"BG0703", "E Baldur's Gate: Sorcerous Sundries L1"},
	{"BG0704", "E Baldur's Gate: Sorcerous Sundries L2"},
	{"BG0705", "E Baldur's Gate: Elfsong Tavern L1"},
	{"BG0706", "E Baldur's Gate: Elfsong Tavern L2"},
	{"BG0707", "Central Baldur's Gate: House3 L1"},
	{"BG0708", "Central Baldur's Gate: House3 L2"},
	{"BG0709", "Central Baldur's Gate: House2 L1"},
	{"BG0710", "Central Baldur's Gate: House2 L2"},
	{"BG0711", "Central Baldur's Gate: House1 L1"},
	{"BG0712", "Central Baldur's Gate: House1 L2"},
	{"BG0713", "Central Baldur's Gate: House5 L1"},
	{"BG0714", "Central Baldur's Gate: House5 L2"},
	{"BG0715", "Central Baldur's Gate: Nadine's House L1"},
	{"BG0716", "Central Baldur's Gate: Nadine's House L2"},
	{"BG0717", "Central Baldur's Gate: House6 L1"},
	{"BG0718", "Central Baldur's Gate: House6 L2"},
	{"BG0719", "Central Baldur's Gate: Felonius' Manor"},
	{"BG0720", "Central Baldur's Gate: Drakon Tavern"},
	{"BG0721", "Central Baldur's Gate: Poultry Store"},
	{"BG0722", "Central Baldur's Gate: House4 L1"},
	{"BG0723", "Central Baldur's Gate: House4 L2"},
	{"BG0724", "Central Baldur's Gate: Gambling Tent 1"},
	{"BG0725", "Central Baldur's Gate: Gambling Tent 2"},
	{"BG0726", "Central Baldur's Gate: Diviner's Tent"},
	{"BG0800", "E Baldur's Gate"},
	{"BG0801", "E Baldur's Gate: House6 L1"},
	{"BG0802", "E Baldur's Gate: House6 L2"},
	{"BG0803", "E Baldur's Gate: Maltz' Weapon Shop L1"},
	{"BG0804", "E Baldur's Gate: Maltz' Weapon Shop L2"},
	{"BG0805", "E Baldur's Gate: Arkion's House L1"},
	{"BG0806", "E Baldur's Gate: Arkion's House L2"},
	{"BG0807", "E Baldur's Gate: Inn L1"},
	{"BG0808", "E Baldur's Gate: Inn L2"},
	{"BG0809", "E Baldur's Gate: Silence's Shop"},
	{"BG0810", "E Baldur's Gate: Lucky Aello's"},
	{"BG0811", "E Baldur's Gate: House1 L1"},
	{"BG0812", "E Baldur's Gate: House1 L2"},
	{"BG0813", "SE Baldur's Gate: Nemphre's House L1"},
	{"BG0814", "SE Baldur's Gate: Nemphre's House L2"},
	{"BG0900", "Wyrm's Crossing"},
	{"BG0901", "Wyrm's Crossing: Tenya's House"},
	{"BG1000", "Ulgoth's Beard"},
	{"BG1001", "Ulgoth's Beard: Inn"},
	{"BG1002", "Ulgoth's Beard: Demon's Chamber"},
	{"BG1003", "Ulgoth's Beard: Storehouse"},
	{"BG1004", "Ulgoth's Beard: Mendas' House"},
	{"BG1005", "Ulgoth's Beard: Therella's House"},
	{"BG1006", "Ulgoth's Beard: House2"},
	{"BG1007", "Ulgoth's Beard: House1"},
	{"BG1008", "Ice Island"},
	{"BG1009", "Ice Island: Maze L1"},
	{"BG1010", "Ice Island: Maze L2"},
	{"BG1100", "SW Baldur's Gate"},
	{"BG1101", "SW Baldur's Gate: Wiven's House L1"},
	{"BG1102", "SW Baldur's Gate: Wiven's House L2"},
	{"BG1103", "SW Baldur's Gate: House3 L1"},
	{"BG1104", "SW Baldur's Gate: House3 L2"},
	{"BG1105", "SW Baldur's Gate: House1 L1"},
	{"BG1106", "SW Baldur's Gate: House1 L2"},
	{"BG1107", "SW Baldur's Gate: House2 L1"},
	{"BG1108", "SW Baldur's Gate: House2 L2"},
	{"BG1109", "SW Baldur's Gate: Tavern L1"},
	{"BG1110", "SW Baldur's Gate: Tavern L2"},
	{"BG1111", "SW Baldur's Gate: Sunin's House"},
	{"BG1112", "SW Baldur's Gate: Weapons Store 1"},
	{"BG1113", "SW Baldur's Gate: Ye Olde Inn L1"},
	{"BG1114", "SW Baldur's Gate: Ye Olde Inn L2"},
	{"BG1115", "SW Baldur's Gate: Potions Store 2"},
	{"BG1116", "SW Baldur's Gate: Weapons Store 2"},
	{"BG1117", "SW Baldur's Gate: Potions Store 1"},
	{"BG1200", "Baldur's Gate Docks"},
	{"BG1201", "Baldur's Gate Docks: Ogre Magi House L1"},
	{"BG1202", "Baldur's Gate Docks: Ogre Magi House L2"},
	{"BG1203", "Baldur's Gate Docks: House2 L1"},
	{"BG1204", "Baldur's Gate Docks: House2 L2"},
	{"BG1205", "Baldur's Gate Docks: House1 L1"},
	{"BG1206", "Baldur's Gate Docks: House1 L2"},
	{"BG1207", "Baldur's Gate Docks: Basilisk Warehouse"},
	{"BG1208", "Baldur's Gate Docks: Noralee's Warehouse"},
	{"BG1209", "Baldur's Gate Docks: Larriaz' House L1"},
	{"BG1210", "Baldur's Gate Docks: Larriaz' House L2"},
	{"BG1211", "Baldur's Gate Docks: Ghorak's House L1"},
	{"BG1212", "Baldur's Gate Docks: Ghorak's House L2"},
	{"BG1213", "Baldur's Gate Docks: Cordyr's House L1"},
	{"BG1214", "Baldur's Gate Docks: Cordyr's House L2"},
	{"BG1215", "Baldur's Gate Docks: Jopalin's Tavern"},
	{"BG1300", "SE Baldur's Gate"},
	{"BG1301", "SE Baldur's Gate: Warehouse"},
	{"BG1302", "SE Baldur's Gate: General Store 2"},
	{"BG1303", "SE Baldur's Gate: Nadarin's Warehouse"},
	{"BG1304", "SE Baldur's Gate: House3 L1"},
	{"BG1305", "SE Baldur's Gate: House3 L2"},
	{"BG1306", "SE Baldur's Gate: Inn L1"},
	{"BG1307", "SE Baldur's Gate: Inn L2"},
	{"BG1308", "SE Baldur's Gate: House4 L1"},
	{"BG1309", "SE Baldur's Gate: House4 L2"},
	{"BG1310", "SE Baldur's Gate: House2 L1"},
	{"BG1311", "SE Baldur's Gate: House2 L2"},
	{"BG1312", "SE Baldur's Gate: Gantolandan's House L1"},
	{"BG1313", "SE Baldur's Gate: Gantolandan's House L2"},
	{"BG1314", "SE Baldur's Gate: House1 L1"},
	{"BG1315", "SE Baldur's Gate: House1 L2"},
	{"BG1316", "SE Baldur's Gate: Taxek's House L1"},
	{"BG1317", "SE Baldur's Gate: Taxek's House L2"},
	{"BG1320", "SE Baldur's Gate: General Store 1"},
	{"BG1400", "Lower Chionthar"},
	{"BG1401", "Lower Chionthar: Ankheg Pit"},
	{"BG1402", "Lower Chionthar: House1"},
	{"BG1403", "Lower Chionthar: House2"},
	{"BG1404", "Lower Chionthar: Farmer Brun's House"},
	{"BG1500", "Balduran's Isle (North)"},
	{"BG1501", "Balduran's Isle (North): Ship D1"},
	{"BG1502", "Balduran's Isle (North): Ship D2"},
	{"BG1503", "Balduran's Isle (North): Ship D3"},
	{"BG1504", "Balduran's Isle (North): Ship D4"},
	{"BG1505", "Balduran's Isle (North): Dradeel's House"},
	{"BG1600", "Cloakwood Grove"},
	{"BG1601", "Cloakwood Grove: Treehouse L1"},
	{"BG1602", "Cloakwood Grove: Treehouse L2"},
	{"BG1603", "Cloakwood Grove: Peter's Cave"},
	{"BG1700", "Cloakwood Crossings"},
	{"BG1800", "Cloakwood Mines"},
	{"BG1801", "Cloakwood Mines: L1"},
	{"BG1802", "Cloakwood Mines: L3"},
	{"BG1803", "Cloakwood Mines: L4"},
	{"BG1804", "Cloakwood Mines: L2"},
	{"BG1805", "Cloakwood Mines: Barracks L1"},
	{"BG1806", "Cloakwood Mines: Barracks L2"},
	{"BG1807", "Cloakwood Mines: Mine Lift"},
	{"BG1808", "Cloakwood Mines: Storehouse"},
	{"BG1809", "Cloakwood Mines: Mine Shed"},
	{"BG1900", "Bandit Camp"},
	{"BG1901", "Bandit Camp: Raemon's Tent"},
	{"BG1902", "Bandit Camp: Tent2"},
	{"BG1903", "Bandit Camp: Gnoll Cave"},
	{"BG1904", "Bandit Camp: Tersus' Tent"},
	{"BG1905", "Bandit Camp: Tent1"},
	{"BG1906", "Bandit Camp: Tent3"},
	{"BG1907", "Bandit Camp: Tent4"},
	{"BG2000", "Balduran's Isle (South)"},
	{"BG2001", "Balduran's Isle (South): Great Hut"},
	{"BG2002", "Balduran's Isle (South): Kaishas' Hut"},
	{"BG2003", "Balduran's Isle (South): Large Hut 3"},
	{"BG2004", "Balduran's Isle (South): Large Hut 2"},
	{"BG2005", "Balduran's Isle (South): Large Hut 4"},
	{"BG2006", "Balduran's Isle (South): Large Hut 1"},
	{"BG2007", "Balduran's Isle (South): Harbor Hut"},
	{"BG2008", "Balduran's Isle (South): Store Hut 2"},
	{"BG2009", "Balduran's Isle (South): Store Hut 1"},
	{"BG2010", "Balduran's Isle (South): Store Hut 4"},
	{"BG2011", "Balduran's Isle (South): Store Hut 3"},
	{"BG2012", "Werewolf Caverns"},
	{"BG2100", "Cloakwood Falls"},
	{"BG2101", "Cloakwood Falls: Spider Nest"},
	{"BG2200", "Cloakwood Lodge"},
	{"BG2215", "Cloakwood Lodge: Hunting Lodge"},
	{"BG2300", "Friendly Arm Inn"},
	{"BG2301", "Friendly Arm Inn: L1"},
	{"BG2302", "Friendly Arm Inn: L2"},
	{"BG2303", "Friendly Arm Inn: L3"},
	{"BG2304", "Friendly Arm Inn: Temple of Wisdom"},
	{"BG2305", "Friendly Arm Inn: House"},
	{"BG2306", "Friendly Arm Inn: Joia's House"},
	{"BG2400", "Peldvale"},
	{"BG2600", "Candlekeep"},
	{"BG2602", "Candlekeep: Priest's Quarters"},
	{"BG2605", "Candlekeep: Infirmary"},
	{"BG2606", "Candlekeep: Storehouse"},
	{"BG2607", "Candlekeep: Bunkhouse"},
	{"BG2608", "Candlekeep: Library L1"},
	{"BG2609", "Candlekeep: Library L2"},
	{"BG2610", "Candlekeep: Library L3"},
	{"BG2611", "Candlekeep: Library L4"},
	{"BG2612", "Candlekeep: Library L5"},
	{"BG2613", "Candlekeep: Catacombs L1 Antechamber"},
	{"BG2614", "Candlekeep: Library L6"},
	{"BG2615", "Candlekeep: Catacombs L1"},
	{"BG2616", "Candlekeep: Inn L1"},
	{"BG2617", "Candlekeep: Inn L2"},
	{"BG2618", "Candlekeep: Barracks"},
	{"BG2619", "Candlekeep: Catacombs L2"},
	{"BG2626", "Candlekeep: Ch.6"},
	{"BG2627", "Candlekeep: Ch.6 Priest's Quarters"},
	{"BG2628", "Candlekeep: Infirmary"},
	{"BG2629", "Candlekeep: Ch.6 Inn L1"},
	{"BG2630", "Candlekeep: Ch.6 Inn L2"},
	{"BG2631", "Candlekeep: Ch.6 Barracks"},
	{"BG2632", "Candlekeep: Ch.6 Bunkhouse"},
	{"BG2633", "Candlekeep: Ch.6 Infirmary"},
	{"BG2643", "Candlekeep: Obe's Cellar"},
	{"BG2700", "Lion's Way"},
	{"BG2800", "Coast Way"},
	{"BG2900", "Larswood"},
	{"BG3000", "Spider Wood"},
	{"BG3100", "Rocky Coast"},
	{"BG3200", "High Hedge"},
	{"BG3201", "High Hedge: House"},
	{"BG3202", "High Hedge: Thalanthyr's Abode"},
	{"BG3300", "Beregost"},
	{"BG3301", "Beregost: Thunderhammer Smithy"},
	{"BG3302", "Beregost: House19"},
	{"BG3303", "Beregost: Red Sheaf L2"},
	{"BG3304", "Beregost: Jovial Juggler L1"},
	{"BG3305", "Beregost: Jovial Juggler L2F"},
	{"BG3306", "Beregost: Jovial Juggler L2R"},
	{"BG3307", "Beregost: Burning Wizard L1"},
	{"BG3308", "Beregost: Burning Wizard L2"},
	{"BG3309", "Beregost: House13 L1"},
	{"BG3310", "Beregost: House13 L2"},
	{"BG3311", "Beregost: House12 L1"},
	{"BG3312", "Beregost: House12 L2"},
	{"BG3313", "Beregost: Mirianne's House L1"},
	{"BG3314", "Beregost: Mirianne's House L2"},
	{"BG3315", "Beregost: Landrin's House"},
	{"BG3316", "Beregost: House18"},
	{"BG3317", "Beregost: House17"},
	{"BG3318", "Beregost: House15"},
	{"BG3319", "Beregost: House16"},
	{"BG3320", "Beregost: Travenhurst Manor L1"},
	{"BG3321", "Beregost: Travenhurst Manor L2"},
	{"BG3322", "Beregost: Servant's Quarters"},
	{"BG3323", "Beregost: House20 L1"},
	{"BG3324", "Beregost: House20 L2"},
	{"BG3325", "Beregost: House23 L1"},
	{"BG3326", "Beregost: House23 L2"},
	{"BG3327", "Beregost: House14 L1"},
	{"BG3328", "Beregost: House14 L2"},
	{"BG3329", "Beregost: House21 L1"},
	{"BG3330", "Beregost: House21 L2"},
	{"BG3331", "Beregost: House11 L1"},
	{"BG3332", "Beregost: House11 L2"},
	{"BG3333", "Beregost: Firebead's House L1"},
	{"BG3334", "Beregost: Firebead's House L2"},
	{"BG3335", "Beregost: House10 L1"},
	{"BG3336", "Beregost: House10 L2"},
	{"BG3337", "Beregost: House22 L1"},
	{"BG3338", "Beregost: House22 L2"},
	{"BG3339", "Beregost: House01 L1"},
	{"BG3340", "Beregost: House01 L2"},
	{"BG3341", "Beregost: House02 L1"},
	{"BG3342", "Beregost: House02 L2"},
	{"BG3343", "Beregost: House03"},
	{"BG3344", "Beregost: House07"},
	{"BG3345", "Beregost: House04 L1"},
	{"BG3346", "Beregost: House04 L2"},
	{"BG3347", "Beregost: House05 L1"},
	{"BG3348", "Beregost: House05 L2"},
	{"BG3349", "Beregost: House06 L1"},
	{"BG3350", "Beregost: House06 L2"},
	{"BG3351", "Beregost: Feldepost's Inn L1"},
	{"BG3352", "Beregost: Feldepost's Inn L2"},
	{"BG3353", "Beregost: Kagain's Store"},
	{"BG3354", "Beregost: House09"},
	{"BG3355", "Beregost: House08"},
	{"BG3356", "Beregost: House08"},
	{"BG3357", "Beregost: Red Sheaf L1"},
	{"BG3400", "Temple of Lathander"},
	{"BG3401", "Temple of Lathander: Vestibule"},
	{"BG3402", "Temple of Lathander: Song of the Morning"},
	{"BG3500", "Sharp Teeth Plain"},
	{"BG3600", "Lighthouse"},
	{"BG3601", "Lighthouse: Black Alaric's Cave"},
	{"BG3700", "Red Canyons"},
	{"BG3800", "Trade Way North"},
	{"BG3802", "Trade Way North: Cave"},
	{"BG3900", "Ulcaster"},
	{"BG3901", "Ulcaster Ruins"},
	{"BG4000", "Gullykin"},
	{"BG4001", "Gullykin: Jenkal's House L1"},
	{"BG4002", "Gullykin: Jenkal's House Cellar"},
	{"BG4003", "Gullykin: Winery L1"},
	{"BG4004", "Gullykin: Winery Cellar"},
	{"BG4005", "Gullykin: House1 L1"},
	{"BG4006", "Gullykin: House1 Cellar"},
	{"BG4007", "Gullykin: House3 L1"},
	{"BG4008", "Gullykin: House3 Cellar"},
	{"BG4009", "Gullykin: House4 L1"},
	{"BG4010", "Gullykin: House4 Cellar"},
	{"BG4011", "Gullykin: House5 L1"},
	{"BG4012", "Gullykin: House5 Cellar"},
	{"BG4013", "Gullykin: House2 L1"},
	{"BG4014", "Gullykin: House2 Cellar"},
	{"BG4100", "Ancient Ruins"},
	{"BG4101", "Ancient Ruins: Tomb"},
	{"BG4200", "Wilderness Lake"},
	{"BG4300", "Trade Way South"},
	{"BG4400", "Lonely Peaks"},
	{"BG4401", "Lonely Peaks: Cave"},
	{"BG4500", "Firewine Bridge"},
	{"BG4501", "Cloakwood Crossings: Wyvern Cave"},
	{"BG4600", "Bear River"},
	{"BG4700", "Xvart Village"},
	{"BG4701", "Xvart Village: Cave"},
	{"BG4800", "Nashkel"},
	{"BG4801", "Nashkel: Inn"},
	{"BG4802", "Nashkel: Temple of Helm"},
	{"BG4803", "Nashkel: Store"},
	{"BG4804", "Nashkel: Manor House L1"},
	{"BG4805", "Nashkel: Manor House L2"},
	{"BG4806", "Nashkel: Joseph's House"},
	{"BG4807", "Nashkel: House1"},
	{"BG4808", "Nashkel: House2"},
	{"BG4809", "Nashkel: Belching Dragon Tavern"},
	{"BG4810", "Nashkel: Garrison"},
	{"BG4900", "Nashkel Carnival"},
	{"BG4901", "Nashkel Carnival: Armor Tent"},
	{"BG4902", "Nashkel Carnival: Lotus Tent"},
	{"BG4903", "Nashkel Carnival: Vitiare's Tent"},
	{"BG4904", "Nashkel Carnival: Gambling Tent"},
	{"BG4905", "Nashkel Carnival: Potion Tent"},
	{"BG4906", "Nashkel Carnival: Zordral's Tent"},
	{"BG4907", "Nashkel Carnival: Magic Items Tent"},
	{"BG4908", "Nashkel Carnival: Melee Weapons Tent"},
	{"BG4909", "Nashkel Carnival: Ranged Weapons Tent"},
	{"BG5000", "Valley of the Tombs"},
	{"BG5001", "Valley of the Tombs: Tomb"},
	{"BG5100", "Gnoll Stronghold"},
	{"BG5101", "Gnoll Stronghold: Xvart Cave"},
	{"BG5102", "Gnoll Stronghold: Tome Cave"},
	{"BG5103", "Gnoll Stronghold: Crawler Cave"},
	{"BG5200", "Dryad Falls"},
	{"BG5201", "Firewine Ruins"},
	{"BG5202", "Dryad Falls: Cave"},
	{"BG5300", "Fire Leaf Forest"},
	{"BG5400", "Nashkel Mines"},
	{"BG5401", "Nashkel Mines: L1"},
	{"BG5402", "Nashkel Mines: L2"},
	{"BG5403", "Nashkel Mines: L3"},
	{"BG5404", "Nashkel Mines: L4"},
	{"BG5405", "Nashkel Mines: Mulahey's Cave"},
	{"BG5406", "Nashkel Mines: Cabin"},
	{"BG5500", "Gibberling Mountains"},
	{"BG5506", "Candlekeep Caves"},
	{"BG5600", "Random Encounter: Plains1"},
	{"BG5601", "Random Encounter: Plains2"},
	{"BG5700", "Random Encounter: Canyon1"},
	{"BG5701", "Random Encounter: Canyon2"},
	{"BG5800", "Random Encounter: Cliff1"},
	{"BG5801", "Random Encounter: Cliff2"},
	{"BG5900", "Random Encounter: Grassland1"},
	{"BG5901", "Random Encounter: Grassland2"},
	{"BG6000", "Random Encounter: Forest1"},
	{"BG6001", "Random Encounter: Forest2"},
	{"BG6100", "Random Encounter: Road"},
	--BG2:SoA Areas
	{"AR0020", "Athkatla: City Gates"},
	{"AR0021", "Crooked Crane 1st Floor"},
	{"AR0022", "Crooked Crane 2nd Floor"},
	{"AR0041", "Random Encounter: City"},
	{"AR0042", "Random Encounter: Outdoor"},
	{"AR0043", "Random Encounter: Outdoor (With rocks)"},
	{"AR0044", "Random Encounter: Outdoor (sand)"},
	{"AR0045", "Random Encounter: City"},
	{"AR0046", "Random Encounter: City (bridge)"},
	{"AR0082", "Lich Grave in Bridge District Inn"},
	{"AR0085", "Tree of Life"},
	{"AR0201", "Leading to ghoul village and beholder lair (Temple District)"},
	{"AR0202", "The Unseeing Eye Cult Hideout"},
	{"AR0203", "Temple of the Forgotten God - Amaunator (Unseeing Eye Plot)"},
	{"AR0204", "The Forgotten Believers - Temple City (Unseeing Eye Plot)"},
	{"AR0205", "The Beholder Hideout (Unseeing Eye Plot)"},
	{"AR0206", "Ghoul village (Temple District)"},
	{"AR0300", "Athkatla: Docks"},
	{"AR0301", "Mae'Var's Hide Out"},
	{"AR0302", "Mae'Var's Inn 1st Floor - Entrance"},
	{"AR0303", "Mae'Var's Inn 2nd Floor - Training Area"},
	{"AR0304", "Mae'Var's Inn 3rd Floor - Edwin"},
	{"AR0305", "Shadow Thieves Guild Entrance"},
	{"AR0306", "Renal Bloodscalp's Hideout"},
	{"AR0307", "Aran Linvail's Hideout"},
	{"AR0308", "Harper's 1st Floor"},
	{"AR0309", "Harper's 2nd Floor"},
	{"AR0310", "Pirates' Hideout in Sea Bouny Tavern"},
	{"AR0311", "Gaelan Bayle's House- First Floor"},
	{"AR0312", "Gaelan Bayle's House- Second Floor"},
	{"AR0313", "Sea Bounty Tavern 1st Floor"},
	{"AR0314", "Sea Bounty Tavern 2nd Floor"},
	{"AR0315", "Rayic Gethras' Home – 1st Floor"},
	{"AR0316", "Rayic Gethras' Home – 2nd Floor"},
	{"AR0317", "Rayic Gethras' Home – 3rd Floor"},
	{"AR0318", "Storage Room (near Shadow Thief Guildhall)"},
	{"AR0319", "Temple of Oghma"},
	{"AR0321", "Thief Stronghold (Hideout)"},
	{"AR0322", "Thief Stronghold (1st Floor)"},
	{"AR0323", "Thief Stronghold (2nd Floor)"},
	{"AR0324", "Thief Stronghold (3rd Floor)"},
	{"AR0325", "Corthala House 1st Floor"},
	{"AR0326", "Corthala House 2nd Floor"},
	{"AR0330", "House 1st Floor (with Kangaxx)"},
	{"AR0331", "House Cellar (with Kangaxx)"},
	{"AR0332", "Barracks in the Docks 1st area"},
	{"AR0333", "Barracks in the Docks 2nd area"},
	{"AR0334", "Cromwell's Shop"},
	{"AR0335", "Home on top of Cromwell's Shop"},
	{"AR0400", "Athkatla: Slums"},
	{"AR0401", "Jansen Cellar"},
	{"AR0402", "Jansen 1st Floor"},
	{"AR0403", "Jansen 2nd Floor"},
	{"AR0404", "Sewers beneath Coronet - Lilarcor"},
	{"AR0405", "Slaver's Ship Building"},
	{"AR0406", "Copper Coronet"},
	{"AR0407", "Prebek's House"},
	{"AR0408", "Temple of Ilmater"},
	{"AR0409", "Ployer's home"},
	{"AR0410", "Planar Sphere: Navigator's room"},
	{"AR0411", "Planar Sphere: Entrance floor"},
	{"AR0412", "Planar Sphere: Ice and Fire Room"},
	{"AR0413", "Planar Sphere: Enginge Room"},
	{"AR0414", "Planar Sphere: Demon Plane"},
	{"AR0415", "Paladin Stronghold Quest Safehouse – 1st floor"},
	{"AR0416", "Paladin Stronghold Quest Safehouse – 2nd floor"},
	{"AR0417", "Borinall's House"},
	{"AR0418", "Myconid Area (Copper Coronet Sewers)"},
	{"AR0419", "Myconid Area (Planar Sphere)"},
	{"AR0420", "Planar Sphere: Lizardmen Hideout"},
	{"AR0500", "Athkatla: Bridge District"},
	{"AR0501", "Tanner's House 1st Floor"},
	{"AR0502", "Tanner's House Cellar"},
	{"AR0503", "Tanner's House Dock"},
	{"AR0504", "Saerk's House 1st Floor"},
	{"AR0505", "Saerk's House 2nd Floor"},
	{"AR0506", "Balthis Estate"},
	{"AR0507", "Am-Si's House 1st Floor (Buried Alive Quest)"},
	{"AR0508", "Am-Si's House 1st Floor (Buried Alive Quest)"},
	{"AR0509", "Five Flagons Inn 1st Floor"},
	{"AR0510", "Five Flagons Inn Theater"},
	{"AR0511", "Five Flagons Inn 2nd Floor"},
	{"AR0512", "Temple of Helm (Bridge District)"},
	{"AR0513", "Delosar's Inn 1st Floor"},
	{"AR0514", "Delosar's Inn 2nd Floor"},
	{"AR0515", "Delosar's Inn 3rd Floor"},
	{"AR0516", "Planar Prison"},
	{"AR0517", "Planar Prison Cell 1 (closest to wyvern)"},
	{"AR0518", "Planar Prison Cell 2 (east of warden)"},
	{"AR0519", "Planar Prison Cell 3 (next to cell 2)"},
	{"AR0520", "Planar Prison Cell 4 (south of warden)"},
	{"AR0521", "Planar Prison Cell 5 (next to cell 4)"},
	{"AR0525", "House South of Delosar's Inn 1st Floor (with Kangaxx)"},
	{"AR0526", "House South of Delosar's Inn Cellar (with Kangaxx)"},
	{"AR0527", "House Next To Balthis Estate 1st Floor"},
	{"AR0528", "House Next To Balthis Estate 2nd Floor"},
	{"AR0529", "Neb's Hideout"},
	{"AR0530", "Storehouse (with hostile Clerics)"},
	{"AR0531", "House next to Storehouse with Clerics"},
	{"AR0600", "Circus Tent Entryway (Kalah's)"},
	{"AR0601", "Air Plane in Irenicus's Dungeon"},
	{"AR0602", "Irenicus's Dungeon 1st Floor"},
	{"AR0603", "Irenicus's Dungeon 2nd Floor"},
	{"AR0604", "Circus Tent 1st Floor (Kalah's)"},
	{"AR0605", "Circus Tent 2nd Floor (Kalah's)"},
	{"AR0606", "Circus Tent 3rd Floor (Kalah's)"},
	{"AR0607", "Circus Tent"},
	{"AR0700", "Athkatla: Waukeen's Promenade"},
	{"AR0701", "The Sewers"},
	{"AR0702", "Adventurer's Mart"},
	{"AR0703", "Temple of Ilmater"},
	{"AR0704", "Mithrest Inn"},
	{"AR0705", "Mekrath's Hideout"},
	{"AR0706", "Armourer/Fletcher"},
	{"AR0707", "Enge's Shop"},
	{"AR0708", "Cernd's former home"},
	{"AR0709", "Den of the Seven Vales 1st Floor"},
	{"AR0710", "Fennecia's Home"},
	{"AR0711", "Illithid Hideout in the Sewers"},
	{"AR0712", "Den of the Seven Vales 2nd Floor"},
	{"AR0713", "Emprty Store next to Enge's Shop"},
	{"AR0800", "Athkatla: Graveyard"},
	{"AR0802", "Southern Dungeons (South of Pai'Na's Hideout)"},
	{"AR0804", "Pai'Na's Hideout"},
	{"AR0805", "Crypt (Sethle near entrance)"},
	{"AR0806", "Crypt King's Tomb"},
	{"AR0807", "Crypt (next to De'Arnise Funeral)"},
	{"AR0808", "Bodhi's Hideout (Abduction Plot, getting Imoen's soul)"},
	{"AR0809", "Bodhi's Lair (Abduction Plot, getting Imoen's soul)"},
	{"AR0810", "Crypt (South of Wellyn's Grave)"},
	{"AR0811", "Crypt (near Arenthis & Risa)"},
	{"AR0812", "Crypt (Northwest of Tirdir's Grave)"},
	{"AR0813", "Crypt (near Stein)"},
	{"AR0900", "Athkatla: Temple District"},
	{"AR0901", "Temple of Helm"},
	{"AR0902", "Temple of Lathander"},
	{"AR0903", "Order of the Radiant Heart"},
	{"AR0904", "Temple of Talos"},
	{"AR0905", "Pimlico's Estate"},
	{"AR0906", "Guarded Compound 1st Floor"},
	{"AR0907", "Guarded Compound 2nd Floor"},
	{"AR1000", "Athkatla: Government District"},
	{"AR1001", "Delryn's Estate"},
	{"AR1002", "Council of Six Building"},
	{"AR1003", "Firecam's Estate"},
	{"AR1004", "Deril Estate"},
	{"AR1005", "Prison (Government District)"},
	{"AR1006", "Jysstev Estate"},
	{"AR1007", "Vulova Estate"},
	{"AR1008", "Twisted Rune"},
	{"AR1009", "Roenall Estate"},
	{"AR1010", "Temple of Waukeen"},
	{"AR1100", "Umar Hills"},
	{"AR1101", "Valygar's Cabin"},
	{"AR1102", "Merella's Cabin"},
	{"AR1103", "Jermien's House"},
	{"AR1104", "Mayor Lloyd's House"},
	{"AR1105", "Imnesvale Inn"},
	{"AR1106", "Umar Hills Cave"},
	{"AR1200", "Windspear Hills"},
	{"AR1201", "Firkraarg's Entrance"},
	{"AR1202", "Firkaarg's Maze"},
	{"AR1203", "Firkraag's Hideout"},
	{"AR1204", "Garren Windspear's Cabin"},
	{"AR1300", "de'Arnise Keep"},
	{"AR1301", "de'Arnise Cellar"},
	{"AR1302", "de'Arnise Keep 1st Floor"},
	{"AR1303", "de'Arnise Keep 2nd Floor"},
	{"AR1401", "Temple Ruins"},
	{"AR1402", "Shadow Dragon Hideout"},
	{"AR1403", "Wolfwere Hideout"},
	{"AR1404", "Shadow Temple Area"},
	{"AR1500", "The Asylum (Spellhold)"},
	{"AR1501", "Cave (Spellhold Test)"},
	{"AR1502", "Fire Room (Spellhold Test)"},
	{"AR1503", "Spore Room (Spellhold Test)"},
	{"AR1504", "Crushing Trap Room (Spellhold Test)"},
	{"AR1505", "Lever Room (Spellhold Test)"},
	{"AR1506", "Troll Cave (Spellhold Test)"},
	{"AR1507", "Kobold Room (Spellhold Test)"},
	{"AR1508", "Final Judgement Spellhold Test"},
	{"AR1509", "Myconid Room (Spellhold Test)"},
	{"AR1510", "Sahuagin Room (Spellhold Test)"},
	{"AR1511", "Elven Table while Spellhold Test"},
	{"AR1512", "Bodhi's Hunt Level 1"},
	{"AR1513", "Bodhi's Hunt Level 2"},
	{"AR1514", "Bodhi's Hunt Level 3"},
	{"AR1515", "Spellhold Main Floor"},
	{"AR1516", "Spellhold Basement"},
	{"AR1600", "Brynnlaw"},
	{"AR1601", "Desharik's Estate"},
	{"AR1602", "Vulgar Monkey Inn"},
	{"AR1603", "Brynnlaw's Shop"},
	{"AR1604", "Temple of Umberlee (Brynnlaw)"},
	{"AR1605", "Perth the Adept's House"},
	{"AR1606", "Cayia's House"},
	{"AR1608", "Festhall Hallway"},
	{"AR1609", "Courtesan Room (Galvena's Festhall)"},
	{"AR1610", "Festhall Prison"},
	{"AR1611", "Festhall Entrance"},
	{"AR1612", "Festhall Kitchen"},
	{"AR1613", "Festhall Room (with Prison Key)"},
	{"AR1700", "Small Teeth Pass"},
	{"AR1800", "North Forest"},
	{"AR1900", "Druid Grove Area"},
	{"AR1901", "Druid Grove"},
	{"AR1902", "Storehouse (Ihtafeer's Storehouse)"},
	{"AR1904", "Troll Mound"},
	{"AR1905", "Ogre's Tower (limited wish quest)"},
	{"AR2000", "Trademeet"},
	{"AR2001", "The Smithy (Trademeet)"},
	{"AR2002", "Fentan's House"},
	{"AR2006", "Tiris' Home"},
	{"AR2007", "Mayors House (Trademeet)"},
	{"AR2008", "Temple of Waukeen"},
	{"AR2009", "Mayor's House Prison (Trademeet)"},
	{"AR2010", "Vyatari's Pub"},
	{"AR2011", "Alibakkar's Estate"},
	{"AR2012", "Lurraxol's Estate"},
	{"AR2013", "Trademeet Crypt"},
	{"AR2014", "Dao Djinni Tent"},
	{"AR2015", "Caravan Merchant's Tent"},
	{"AR2016", "Raffee's Tent"},
	{"AR2017", "Courtesan Tent"},
	{"AR2100", "Underdark"},
	{"AR2101", "Beholder Cave in Underdark"},
	{"AR2102", "Adalon's Cave"},
	{"AR2200", "Ust Natha"},
	{"AR2201", "Temple in Ust Natha"},
	{"AR2202", "Inn in Ust Natha 1st Floor"},
	{"AR2203", "Inn in Ust Natha; 2nd Floor"},
	{"AR2204", "Female Fighter's Society in Ust Natha"},
	{"AR2205", "Male Fighter's Society in Ust Natha"},
	{"AR2206", "Qilue's House in Ust Natha"},
	{"AR2207", "Deirex's Tower in Ust Natha"},
	{"AR2208", "Jarlaxle's House"},
	{"AR2209", "Jae'llat's House"},
	{"AR2210", "Deirex's Cave"},
	{"AR2300", "Underwater City (Sahuagin City)"},
	{"AR2400", "Mind Flayers in Underdark"},
	{"AR2401", "Cave Between Underdark and Exit from Underdark"},
	{"AR2402", "Kuo Toa in Underdark"},
	{"AR2500", "Elven Temple at Underdark Exit"},
	{"AR2600", "Forest of Tethyr"},
	{"AR2601", "Encounter - Drizzt Do'Urden"},
	{"AR2602", "Cave in Woods to Suldanesselar"},
	{"AR2603", "Cabin in Forest of Tethyr"},
	{"AR2800", "Suldanesselar"},
	{"AR2801", "House of High Priestess of Suldanesselar"},
	{"AR2802", "House in Suldanesselar"},
	{"AR2803", "Rillifane Temple"},
	{"AR2806", "Tree of Life"},
	{"AR2807", "Black Dragon's Hideout"},
	{"AR2808", "House in Suldanesselar"},
	{"AR2810", "House in Suldanesselar"},
	{"AR2811", "House in Suldanesselar"},
	{"AR2812", "Temple of Rillifane (after the Battle)"},
	{"AR2900", "The Abyss"},
	{"AR2901", "Fear Test (Abyss)"},
	{"AR2902", "Pride Test (Abyss)"},
	{"AR2903", "Greed (Abyss)"},
	{"AR2904", "Selfish (Abyss)"},
	{"AR2905", "Sarevok (Abyss)"},
	--BG2:ToB Areas
	{"AR3000", "Watcher's Keep"},
	{"AR3001", "Watcher's Keep: Altar level"},
	{"AR3003", "Watcher's Keep: Compass level"},
	{"AR3004", "Watcher's Keep"},
	{"AR3005", "Watcher's Keep: tieflings"},
	{"AR3006", "Watcher's Keep: Succubus"},
	{"AR3007", "Watcher's Keep: Cambion"},
	{"AR3008", "Watcher's Keep: Baalor (Paladin Sword)"},
	{"AR3009", "Watcher's Keep: Wild Magic ?"},
	{"AR3010", "Watcher's Keep: Tanar'ri - Obelisk"},
	{"AR3011", "Watcher's Keep: Bard's Gloves, Manman's Journal"},
	{"AR3012", "Watcher's Keep: Tahazzar"},
	{"AR3013", "Watcher's Keep: Ka'rashur"},
	{"AR3014", "Watcher's Keep: White Dragon Scales"},
	{"AR3015", "Watcher's Keep: Aesgareth"},
	{"AR3016", "Watcher's Keep: Chromatic Demon; Elementalist Level"},
	{"AR3017", "Watcher's Keep: Carston and the machine"},
	{"AR3018", "Watcher's Keep: Saladrex (Dragon)"},
	{"AR3019", "Watcher's Keep: Helmite Level - Last Seals"},
	{"AR3020", "Watcher's Keep: Imprisoned One"},
	{"AR3021", "Watcher's Keep: Ilithids"},
	{"AR3022", "Watcher's Keep: Anti-Paladins"},
	{"AR3023", "Watcher's Keep: Adventure Level"},
	{"AR3024", "Watcher's Keep: Dragon - Fear Challenge"},
	{"AR3025", "Watcher's Keep: Orcs - Ixil's Nail"},
	{"AR3026", "Watcher's Keep: Imp - Game"},
	{"AR3027", "Watcher's Keep: Crypt - Demi-Lich"},
	{"AR4000", "Sacred Grove (Spirit Heads)"},
	{"AR4500", "Pocket Plane"},
	{"AR5000", "Saradush"},
	{"AR5001", "Gromnir 1st Level"},
	{"AR5002", "Gromnir's Hideout"},
	{"AR5003", "Saradush Tavern (Tankard Tree)"},
	{"AR5004", "Temple of Waukeen"},
	{"AR5005", "Gromnir's Barracks"},
	{"AR5006", "Saradush Prison"},
	{"AR5007", "Basement Entrance to Gromnir's Hideout"},
	{"AR5008", "Kiser's Home"},
	{"AR5010", "Countess Santele's Home"},
	{"AR5011", "Arcana Archives"},
	{"AR5012", "House"},
	{"AR5013", "Sewers beneath Saradush"},
	{"AR5014", "Kiser's Home - Cellar"},
	{"AR5015", "Saradush Militia Entrance"},
	{"AR5016", "Saradush Castle Jail (Kiser Plot)"},
	{"AR5200", "Marching Mountains"},
	{"AR5201", "Fire Giant Keep"},
	{"AR5202", "Forest of Mir - The Temple"},
	{"AR5203", "Siege Camp"},
	{"AR5204", "Yaga-Shura's Keep"},
	{"AR5500", "Amkethran"},
	{"AR5501", "Amkethran Inn"},
	{"AR5502", "Kerrick the Smith's House"},
	{"AR5503", "Chyil's House"},
	{"AR5504", "Smuggler Cave"},
	{"AR5505", "Balthazar's Home"},
	{"AR5506", "Commoner's Home"},
	{"AR5507", "Empty Home"},
	{"AR5508", "Faheed's Home"},
	{"AR5509", "Amkethran Crypt"},
	{"AR6000", "Abazigal's Lair"},
	{"AR6001", "Abazigal's Lair: Entrance Hall"},
	{"AR6002", "Abazigal's Lair: Cells"},
	{"AR6003", "Abazigal's Lair: Iycanth the Mad, Bondari"},
	{"AR6004", "Abazigal's Lair: Fll'Yissetat"},
	{"AR6005", "Abazigal's Lair: Hideout"},
	{"AR6008", "Frost Salamander Cave"},
	{"AR6011", "Orbs and Gauth"},
	{"AR6012", "Pool Home - breath under water"},
	{"AR6100", "Sendai's Enclave"},
	{"AR6101", "Main Entrance Sendai's Lair"},
	{"AR6102", "Sendai's Enclave: Slavemaster"},
	{"AR6103", "Sendai's Enclave: Spiders"},
	{"AR6104", "Sendai's Enclave: Crossway"},
	{"AR6105", "Sendai's Enclave: Diaytha, Abishai"},
	{"AR6106", "Sendai's Enclave: Captain Egeissag"},
	{"AR6107", "Sendai's Enclave: Mithykyl"},
	{"AR6108", "Sendai's Enclave: Sendai"},
	{"AR6109", "Sendai's Enclave: Drow"},
	{"AR6110", "Sendai's Enclave: Odamaron"},
	{"AR6111", "Sendai's Enclave: Woodcutter's House"},
	{"AR6300", "The Oasis"},
	{"AR6400", "Forest Valley"},
	--BGEE Areas
	{"OH2000", "Adoy's Enclave"},
	{"OH3000", "Cloud Peaks"},
	--BG2EE:SoA Areas
	{"OH4000", "Abandoned Amphitheater"},
	{"OH4100", "Heretic Temple"},
	{"OH5100", "Resurrection Gorge"},
	{"OH5300", "Helmite Camp"},
	{"OH6000", "Wild Forest"},
	{"OH6100", "Hidden Refuge"},
	{"OH6200", "Hidden Refuge"},
	--BG2EE:ToB Areas
	{"OH4200", "Deepstone Clanhold"},
	{"OH6400", "Clearing"},
	--SOD Areas
~
	REPLACE_TEXTUALLY ~filenames_stringrefs[ %TAB%]*\[[ %TAB%]*'MALE\([0-9]+\)'~ ~filenames_stringrefs['BGMALE\1'~
	REPLACE_TEXTUALLY ~filenames_stringrefs[ %TAB%]*\[[ %TAB%]*'FEMALE\([0-9]+\)'~ ~filenames_stringrefs['BGFEML\1'~
	REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*Infinity_AddDLC[ %TAB%]*([ %TAB%]*'\(.*\)'[ %TAB%]*,[ %TAB%]*'\(.*\)'[ %TAB%]*,[ %TAB%]*\([-0-9]+\)[ %TAB%]*,[ %TAB%]*\([-0-9]+\)[ %TAB%]*,[ %TAB%]*'\(.*\)'[ %TAB%]*,[ %TAB%]*'\(.*\)'[ %TAB%]*)~ BEGIN
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*Infinity_AddDLC[ %TAB%]*([ %TAB%]*'%MATCH1%'~)) BEGIN
			SPRINT match ~%MATCH2%~ SPRINT res ~bmp~ LPM EET_PCU_outer_res SPRINT MATCH2 ~%match%~
			PATCH_IF (MATCH3 > 0) BEGIN SET MATCH3 = RESOLVE_STR_REF ((AT MATCH3)) END
			PATCH_IF (MATCH4 > 0) BEGIN SET MATCH4 = RESOLVE_STR_REF ((AT MATCH4)) END
			SPRINT addDLC_var ~%addDLC_var%Infinity_AddDLC( '%MATCH1%', '%MATCH2%', %MATCH3%, %MATCH4%, '%MATCH5%', '%MATCH6%' )%LNL%~
			//PATCH_PRINT ~Infinity_AddDLC: %MATCH1% ; %MATCH2% ; %MATCH3% ; %MATCH4% ; %MATCH5% ; %MATCH6%~
		END
	END ~~
	REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*Infinity_AddDLCContent[ %TAB%]*([ %TAB%]*'\(.*\)'[ %TAB%]*,[ %TAB%]*\([0-9]+\)[ %TAB%]*)~ BEGIN
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*Infinity_AddDLCContent[ %TAB%]*([ %TAB%]*'%MATCH1%'~)) BEGIN
			SPRINT addDLCContent_var ~%addDLCContent_var%Infinity_AddDLCContent('%MATCH1%', %MATCH2% )%LNL%~
			//PATCH_PRINT ~Infinity_AddDLCContent: %MATCH1% ; %MATCH2%~
		END
	END ~~
	SET male_cnt = 0
	SET female_cnt = 0
	FOR (cnt = 4000000; cnt <= 4000010; cnt = cnt + 1) BEGIN
		SET strref = RESOLVE_STR_REF ((AT cnt))
		PATCH_IF (cnt <= 4000004) BEGIN //BG2 male
			SET male_cnt = male_cnt + 1
			SPRINT filenames_stringrefs_var ~%filenames_stringrefs_var%filenames_stringrefs['MALE%male_cnt%'] = {%strref%, 2}%LNL%~
		END ELSE PATCH_IF (cnt <= 4000008) BEGIN //BG2 female
			SET female_cnt = female_cnt + 1
			SPRINT filenames_stringrefs_var ~%filenames_stringrefs_var%filenames_stringrefs['FEMALE%female_cnt%'] = {%strref%, 2}%LNL%~
		END ELSE PATCH_IF (cnt = 4000009) BEGIN //BG1 main male
			SPRINT filenames_stringrefs_var ~%filenames_stringrefs_var%filenames_stringrefs['BGMAINM'] = {%strref%, 2}%LNL%~
		END ELSE PATCH_IF (cnt = 4000010) BEGIN //BG1 main female
			SPRINT filenames_stringrefs_var ~%filenames_stringrefs_var%filenames_stringrefs['BGMAINF'] = {%strref%, 2}%LNL%~
		END
	END
	REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*filenames_stringrefs[ %TAB%]*\[[ %TAB%]*'\(.*\)'[ %TAB%]*\][ %TAB%]*=[ %TAB%]*{\([-0-9]+\)[ %TAB%]*,[ %TAB%]*\([0-9]+\)[ %TAB%]*}~ BEGIN
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*filenames_stringrefs[ %TAB%]*\[[ %TAB%]*'%MATCH1%'~)) BEGIN
			PATCH_IF (MATCH2 > 0) BEGIN SET MATCH2 = RESOLVE_STR_REF ((AT MATCH2)) END
			INNER_ACTION BEGIN
				ACTION_GET_STRREF MATCH2 "strref"
				OUTER_SPRINT strref ~BG1 %strref%~
				STRING_SET MATCH2 ~%strref%~
			END
			SPRINT filenames_stringrefs_var ~%filenames_stringrefs_var%filenames_stringrefs['%MATCH1%'] = {%MATCH2%, %MATCH3%}%LNL%~
			//PATCH_PRINT ~filenames_stringrefs: %MATCH1% ; %MATCH2% ; %MATCH3%~
		END
	END ~~
	REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*chapterBackgrounds[ %TAB%]*\[\([0-9]+\)\][ %TAB%]*=[ %TAB%]*"\(.*\)"~ BEGIN
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*chapterBackgrounds[ %TAB%]*\[%MATCH1%\]~)) BEGIN
			SPRINT chapterBackgrounds_var ~%chapterBackgrounds_var%chapterBackgrounds[%MATCH1%] = "%MATCH2%"%LNL%~
			//PATCH_PRINT ~chapterBackgrounds: %MATCH1% ; %MATCH2%~
		END
	END ~~
	REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*createQuest[ %TAB%]*([ %TAB%]*\([-0-9]+\)[ %TAB%]*)~ BEGIN
		PATCH_IF (MATCH1 > 0) BEGIN SET MATCH1 = RESOLVE_STR_REF ((AT MATCH1)) END
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*createQuest[ %TAB%]*([ %TAB%]*%MATCH1%[ %TAB%]*)~)) BEGIN
			SPRINT createQuest_var ~%createQuest_var%%TAB%createQuest    ( %MATCH1% )%LNL%~
			//DEFINE_ASSOCIATIVE_ARRAY createQuest BEGIN ~%TAB%createQuest    ( %MATCH1% )~ => ~dummy~ END
			//PATCH_PRINT ~createQuest: %MATCH1%~
		END
	END ~~
	REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*createEntry[ %TAB%]*([ %TAB%]*\([-0-9]+\)[ %TAB%]*,[ %TAB%]*\([-0-9]+\)[ %TAB%]*,[ %TAB%]*\([-0-9]+\)[ %TAB%]*,[ %TAB%]*{[ %TAB%]*\(.*\)[ %TAB%]*}[ %TAB%]*,[ %TAB%]*\(.*\)[ %TAB%]*)~ BEGIN
		PATCH_IF (MATCH1 > 0) BEGIN SET MATCH1 = RESOLVE_STR_REF ((AT MATCH1)) END
		PATCH_IF (MATCH3 > 0) BEGIN SET MATCH3 = RESOLVE_STR_REF ((AT MATCH3)) END
		SPRINT createEntry_var ~%createEntry_var%%TAB%createEntry    ( %MATCH1%, %MATCH2%, %MATCH3%, {%MATCH4%}, %MATCH5%)%LNL%~
		//DEFINE_ASSOCIATIVE_ARRAY createQuest BEGIN ~%TAB%createEntry    ( %MATCH1%, %MATCH2%, %MATCH3%, {%MATCH4%}, %MATCH5% )~ => ~dummy~ END
		//PATCH_PRINT ~createEntry: : %MATCH1% ; %MATCH2% ; %MATCH3% ; %MATCH4% ; %MATCH5%~
	END ~~
	REPLACE_TEXTUALLY ~--.*~ ~~
	REPLACE_TEXTUALLY ~{~ ~~
	REPLACE_TEXTUALLY ~}~ ~~
	REPLACE_TEXTUALLY ~,~ ~ ~
	REPLACE_TEXTUALLY ~'~ ~ ~
	COUNT_2DA_ROWS 2 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 2 "col1"
		PATCH_IF ((~%col1%~ STRING_CONTAINS_REGEXP ~portraits=?~)=0) BEGIN
			SET portraits_row = cnt + 1
		END ELSE PATCH_IF ((~%col1%~ STRING_CONTAINS_REGEXP ~movies=?~)=0) BEGIN
			SET portraits_row_end = cnt
			SET movies_row = cnt + 1
		END ELSE PATCH_IF ((~%col1%~ STRING_CONTAINS_REGEXP ~credits=?~)=0) BEGIN
			SET movies_row_end = cnt
		END ELSE PATCH_IF ((~%col1%~ STRING_CONTAINS_REGEXP ~cheatAreas=?~)=0) BEGIN
			SET cheat_row = cnt + 1
		END ELSE PATCH_IF ((~%col1%~ STRING_CONTAINS_REGEXP ~fontcolors=?~)=0) BEGIN
			SET cheat_row_end = cnt
			SET cnt = cntrow
		END
	END
	FOR (cnt = portraits_row; cnt < portraits_row_end; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 2 "col1"
		SPRINT match ~%col1%~ SPRINT res ~bmp~ LPM EET_PCU_outer_res SPRINT col1 ~%match%~
		READ_2DA_ENTRY cnt 1 2 "col2"
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*{[ %TAB%]*'%col1%'[ %TAB%]*,[ %TAB%]*%col2%[ %TAB%]*}~)) BEGIN
			SPRINT portraits_var ~%portraits_var%%TAB%{'%col1%', %col2%},%LNL%~
			//PATCH_PRINT ~portraits: %col1% ; %col2%~
		END
	END
	FOR (cnt = movies_row; cnt < movies_row_end; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 2 "col1"
		SPRINT match ~%col1%~ SPRINT res ~mve~ LPM EET_PCU_outer_res SPRINT col1 ~%match%~
		TO_LOWER col1 //otherwise it doesn't work
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*{[ %TAB%]*'%col1%'~)) BEGIN
			READ_2DA_ENTRY cnt 1 2 "col2"
			READ_2DA_ENTRY cnt 2 2 "col3"
			READ_2DA_ENTRY cnt 3 2 "col4"
			SET col4 = RESOLVE_STR_REF ((AT col4))
			READ_2DA_ENTRY cnt 4 2 "col5"
			READ_2DA_ENTRY cnt 5 2 "col6"
			SPRINT movies_var ~%movies_var%%TAB%{'%col1%', %col2%, %col3%, %col4%, %col5%, %col6%},%LNL%~
			//PATCH_PRINT ~movies: %col1% ; %col2% ; %col3% ; %col4% ; %col5% ; %col6%~
		END
	END
	FOR (cnt = cheat_row; cnt < cheat_row_end; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 2 "col1"
		//READ_2DA_ENTRY cnt 1 2 "col2" //contains spaces
		SPRINT match ~%col1%~ SPRINT res ~are~ LPM EET_PCU_outer_res SPRINT col1 ~%match%~
		PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~bgee.lua~ ~^[ %TAB%]*{[ %TAB%]*%col1%~)) BEGIN
			REPLACE_EVALUATE CASE_INSENSITIVE ~^[ %TAB%]*%col1%[ %TAB%]*"\(.*\)"~ BEGIN
				SPRINT cheat_var ~%cheat_var%%TAB%{%col1%, "%MATCH1%"},%LNL%~
				//PATCH_PRINT ~cheatAreas: %col1% ; %MATCH1%~
			END ~%col1%  "%MATCH1%"~
		END
	END

<<<<<<<< .../bgee.lua

%addDLC_var%
%addDLCContent_var%
%filenames_stringrefs_var%
%chapterBackgrounds_var%
>>>>>>>>
COPY_EXISTING ~bgee.lua~ ~override~
	REPLACE_TEXTUALLY ~}[%newline%]*\(}[%newline%]*movies =\)~ ~},%LNL%%portraits_var%\1~ //notice added missing , at the last line
	REPLACE_TEXTUALLY ~\(movies[ %TAB%]*=[%newline%]*{\)~ ~\1%LNL%%movies_var%~
	REPLACE_TEXTUALLY ~\(cheatAreas[ %TAB%]*=[%newline%]*{\)~ ~\1%LNL%%cheat_var%~
	REPLACE_TEXTUALLY ~\(createEntry.+)\)[%newline%]*end~ ~\1%LNL%%createQuest_var%%createEntry_var%end~
	APPEND_FILE ~.../bgee.lua~ EVALUATE_BUFFER
BUT_ONLY

COPY ~engine.lua~ ~engine.lua~
	REPLACE_TEXTUALLY ~engine_name[ %TAB%]*=[ %TAB%]*".*"~ ~engine_name = "Baldur's Gate - Enhanced Edition Trilogy"~

//WeiDU currently doesn't support custom USER_DIRECTORY constant variable
OUTER_PATCH_SAVE USER_DIRECTORY_EET ~%USER_DIRECTORY%~ BEGIN
	REPLACE_TEXTUALLY ~Baldur's Gate II - Enhanced Edition~ ~Baldur's Gate - Enhanced Edition Trilogy~
END
ACTION_IF (NOT DIRECTORY_EXISTS ~%USER_DIRECTORY_EET%/save~) BEGIN
	MKDIR ~%USER_DIRECTORY_EET%/save~
END
ACTION_IF (NOT FILE_EXISTS ~%USER_DIRECTORY_EET%/Baldur.lua~) AND (FILE_EXISTS ~%USER_DIRECTORY%/Baldur.lua~) BEGIN
	COPY + ~%USER_DIRECTORY%/Baldur.lua~ ~%USER_DIRECTORY_EET%~
END

ACTION_IF (FILE_EXISTS ~%USER_DIRECTORY_EET%/Baldur.lua~) BEGIN
<<<<<<<< .../Baldur.lua
SetPrivateProfileString('%row%','%option%','%option_1%')
>>>>>>>>
	COPY + ~%USER_DIRECTORY_EET%/Baldur.lua~ ~%USER_DIRECTORY_EET%~
		REPLACE_TEXTUALLY ~^.+'Last Save .+[%newline%]*~ ~~
		DEFINE_ASSOCIATIVE_ARRAY table_EET_ini BEGIN
			"Text" , "%LANGUAGE_BG2%" => "Language"
			"EET Installation State" , 1 => "Program Options"
			"Active Campaign" , 1 => "Program Options"
			//"Never Show Nuisance SOD" , 0 => "Program Options"
			"Debug Mode" , 1 => "Program Options"
			"Logging On" , 1 => "Program Options"
			"Cheats" , 1 => "Game Options"
			"Show Black Space" , 0 => "Graphics"
			"BG4LOGO" , 1 => "MOVIES"
			"BILOGO" , 1 => "MOVIES"
			"BLACKPI_" , 1 => "MOVIES"
			"BWDRAGON" , 1 => "MOVIES"
			"CREDITS" , 1 => "MOVIES"
			"INFELOGO" , 1 => "MOVIES"
			"INTRO_" , 1 => "MOVIES"
			"INTRO15F" , 1 => "MOVIES"
			"INTRO" , 1 => "MOVIES"
			"TSRLOGO" , 1 => "MOVIES"
			"WOTC" , 1 => "MOVIES"
		END
		PHP_EACH table_EET_ini AS option => row BEGIN
			COUNT_REGEXP_INSTANCES ~'%option%'~ num_matches
			PATCH_IF (num_matches = 0) BEGIN
				APPEND_FILE ~.../Baldur.lua~ EVALUATE_BUFFER
			END ELSE BEGIN
				REPLACE_TEXTUALLY ~^.+'%option%'.+$~ ~SetPrivateProfileString('%row%','%option%','%option_1%')~
			END
		END
	BUT_ONLY
END ELSE BEGIN
<<<<<<<< .../Baldur.lua
SetPrivateProfileString('Language','Text','%LANGUAGE_BG2%')
SetPrivateProfileString('Program Options','EET Installation State','1')
SetPrivateProfileString('Program Options','Active Campaign','1')
SetPrivateProfileString('Program Options','Debug Mode','1')
SetPrivateProfileString('Program Options','Logging On','1')
SetPrivateProfileString('Game Options','Cheats','1')
SetPrivateProfileString('Graphics','Show Black Space','0')
SetPrivateProfileString('MOVIES','BG4LOGO','1')
SetPrivateProfileString('MOVIES','BILOGO','1')
SetPrivateProfileString('MOVIES','BLACKPI_','1')
SetPrivateProfileString('MOVIES','BWDRAGON','1')
SetPrivateProfileString('MOVIES','CREDITS','1')
SetPrivateProfileString('MOVIES','INFELOGO','1')
SetPrivateProfileString('MOVIES','INTRO_','1')
SetPrivateProfileString('MOVIES','INTRO15F','1')
SetPrivateProfileString('MOVIES','INTRO','1')
SetPrivateProfileString('MOVIES','TSRLOGO','1')
SetPrivateProfileString('MOVIES','WOTC','1')
>>>>>>>>
	COPY + ~.../Baldur.lua~ ~%USER_DIRECTORY_EET%/Baldur.lua~ EVALUATE_BUFFER
END

/////                                                  \\\\\
///// Minor BG2:EE patching                            \\\\\
/////                                                  \\\\\

//changes Bandit Scalp itemtype to Tattoos (39)
COPY_EXISTING ~MISC86.ITM~ ~override~
	WRITE_SHORT 0x1c 39
BUT_ONLY

//Viconia cutscene people spawned via AR1000.BCS instead of ARE
COPY_EXISTING ~AR1000.ARE~ ~override~
	READ_LONG  0x54 "actor_off"
	READ_SHORT 0x58 "actor_num"
	FOR (index = 0 ; index < actor_num ; index = index + 1) BEGIN
		READ_ASCII ("%actor_off%" + 0x80 + (0x110 * "%index%")) "resref" // cre file
		PATCH_IF ("%resref%" STRING_EQUAL_CASE "VICG1")
		OR ("%resref%" STRING_EQUAL_CASE "VICG2")
		OR ("%resref%" STRING_EQUAL_CASE "VICTOWN1")
		OR ("%resref%" STRING_EQUAL_CASE "VICTOWN2")
		OR ("%resref%" STRING_EQUAL_CASE "VICTOWN3")
		OR ("%resref%" STRING_EQUAL_CASE "VICTOWN4")
		OR ("%resref%" STRING_EQUAL_CASE "VICG3") BEGIN
			PATCH_PRINT "Patching %SOURCE_FILE%: removing actor %resref%"
			LPF fj_are_structure
				INT_VAR fj_delete_mode = "%index%"
				STR_VAR fj_structure_type = actor
			END
			SET index = index - 1
			READ_SHORT 0x58 "actor_num"
		END
	END
BUT_ONLY

//The Army Scythe +1 uses BG1 icon (darker one kept for Drow Crossbow of Speed)
COPY_EXISTING ~XBOW06.ITM~ ~override~
	READ_ASCII 0x3a "resref"
	PATCH_IF (~%resref%~ STRING_EQUAL_CASE ~IXBOW06~) BEGIN
		WRITE_ASCII 0x3a "IXBOW06_" #8
	END
	READ_LONG 0x64 "abil_off"
	READ_SHORT 0x68 "abil_cnt"
	FOR (cnt=0; cnt<"%abil_cnt%"; cnt=cnt+1) BEGIN
		READ_ASCII ("%abil_off%"+0x38*cnt+0x4) "resref"
		PATCH_IF (~%resref%~ STRING_EQUAL_CASE ~IXBOW06~) BEGIN
			WRITE_ASCII 0x3a "IXBOW06_" #8
		END
	END
BUT_ONLY

//overwrite GUI chapter MOS reference
COPY_EXISTING ~GUICHAP.CHU~ ~override~
	WRITE_ASCII 0x28 ~GUICHP1B~ #8
BUT_ONLY

/////                                                  \\\\\
///// Compatibility patches                            \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~EET/temp/WeiDU.log~ BEGIN
	COPY - ~EET/tbl/compatibility.tbl~ ~EET/tbl~
		COUNT_2DA_ROWS 2 "cntrow"
		FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
			READ_2DA_ENTRY cnt 0 2 "col1"
			READ_2DA_ENTRY cnt 1 2 "col2"
			PATCH_IF ("%col2%" = 1) BEGIN
				INNER_ACTION BEGIN
					ACTION_IF (FILE_CONTAINS ~EET/temp/WeiDU.log~ EVAL ~^%col1%~) BEGIN
						PRINT ~Including %col1% compatibility patches~
						INCLUDE ~EET/compat/%col1%/include.tph~
					END
				END
			END
		END
END

/////                                                  \\\\\
///// Decompile BCS / DLG files                        \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH file IN RACE CLASS SPECIFIC BEGIN
	COPY_EXISTING + ~%file%.IDS~ ~EET/temp~
	COPY ~EET/temp/array/%file%.IDS~ ~override~
END

ACTION_BASH_FOR ~%patch_dir%/bcs~ ~.*~ BEGIN
	MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.BAF~
	COPY + ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.BAF~ ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.BAF~
		DECOMPILE_BCS_TO_BAF
END

ACTION_BASH_FOR ~%patch_dir%/dlg~ ~.*~ BEGIN
	MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.D~
	COPY + ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.D~ ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.D~
		DECOMPILE_DLG_TO_D
		REPLACE_TEXTUALLY ~EET/TEMP/PATCH/DLG/~ ~~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// PCU conversion                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~PCU conversion...~

//print and append variables generated during installation
PRINT ~Additional variables created during installation, not listed in lib/pcu_dict.tph:~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_sum BEGIN%LNL%%log_remapped_sum%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_kit BEGIN%LNL%%log_remapped_kit%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_race BEGIN%LNL%%log_remapped_race%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_class BEGIN%LNL%%log_remapped_class%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_spec BEGIN%LNL%%log_remapped_spec%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_ipro BEGIN%LNL%%log_remapped_ipro%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_mus BEGIN%LNL%%log_remapped_mus%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_bam BEGIN%LNL%%log_remapped_bam%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_eff BEGIN%LNL%%log_remapped_eff%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_itm BEGIN%LNL%%log_remapped_itm%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_spl BEGIN%LNL%%log_remapped_spl%END~
PRINT ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_splName BEGIN%LNL%%log_remapped_splName%END~

COPY + ~.../blank.txt~ ~EET/temp/new_vars.tph~
APPEND_OUTER + ~EET/temp/new_vars.tph~ ~ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_sum BEGIN%LNL%%log_remapped_sum%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_kit BEGIN%LNL%%log_remapped_kit%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_race BEGIN%LNL%%log_remapped_race%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_class BEGIN%LNL%%log_remapped_class%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_spec BEGIN%LNL%%log_remapped_spec%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_ipro BEGIN%LNL%%log_remapped_ipro%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_mus BEGIN%LNL%%log_remapped_mus%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_bam BEGIN%LNL%%log_remapped_bam%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_eff BEGIN%LNL%%log_remapped_eff%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_itm BEGIN%LNL%%log_remapped_itm%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_spl BEGIN%LNL%%log_remapped_spl%END%LNL%%LNL%ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_splName BEGIN%LNL%%log_remapped_splName%END%LNL%~

COPY ~EET/lib/pcu_dict.tph~ ~EET/lib~
	APPEND_FILE ~EET/temp/new_vars.tph~

//FOR variables
OUTER_SET count = 0
OUTER_SET cntrow = 0
OUTER_SET cnt = 0
OUTER_SET cntare = 0
OUTER_SET cntcre = 0
OUTER_SET cntwav = 0

//reporting variables
OUTER_SET are_check = 0
OUTER_SET baf_check = 0
OUTER_SET bcs_check = 0
OUTER_SET cre_check = 0
OUTER_SET d_check = 0
OUTER_SET dlg_check = 0
OUTER_SET eff_check = 0
OUTER_SET gam_check = 0
OUTER_SET itm_check = 0
OUTER_SET pro_check = 0
OUTER_SET spl_check = 0
OUTER_SET sto_check = 0
OUTER_SET tp2_check = 0
OUTER_SET tra_check = 0
OUTER_SET vef_check = 0
OUTER_SET vvc_check = 0
OUTER_SET wed_check = 0
OUTER_SET wmp_check = 0
OUTER_SET ignore_check = 0
OUTER_SET ren_check = 0

//create file list for conversion
COPY + ~.../blank.txt~ ~EET/temp/Filelist.txt~
ACTION_FOR_EACH var IN are bcs cre dlg eff ini itm pro spl sto vef vvc wed "" BEGIN
	ACTION_BASH_FOR ~%patch_dir%/%var%~ ~^.+$~ BEGIN
		APPEND_OUTER + ~EET/temp/Filelist.txt~ ~%var%/%BASH_FOR_FILE%~
	END
END

//conversion
PRINT ~Please be patient during the conversion process. This may take some time...~

COPY + ~EET/temp/Filelist.txt~ ~EET/temp~
	REPLACE_TEXTUALLY ~ ~ ~?~
	COUNT_2DA_ROWS 1 "cntrow"
	PATCH_PRINT ~%cntrow% entries parsed, commencing patching...~
	FOR (count = 0; count < "%cntrow%"; count = count + 1) BEGIN
		SET cntcurrent = "%count%" + 1
		READ_2DA_ENTRY "%count%" 0 1 "patch_name"
		INNER_PATCH_SAVE ~patch_name~ ~%patch_name%~ BEGIN
			REPLACE_TEXTUALLY ~\?~ ~ ~
		END
		INNER_ACTION BEGIN
			COPY + ~%patch_dir%/%patch_name%~ ~%patch_dir%/%patch_name%~
				SET checked = 0
				READ_ASCII 0x0 "filetype" ELSE ~Ignoring 0 byte file~
				LAUNCH_PATCH_MACRO ~EET_PCU_core~
			//Unrecognised files
			ACTION_IF ("%checked%" = 0) BEGIN
				OUTER_SET ignore_check = "%ignore_check%" + 1
				PRINT ~%cntcurrent%/%cntrow% Ignoring %patch_name%...~
			END
			//Complete INNER_PATCH and FOR commands during reading of Filelist.txt
		END
	END

PRINT ~Conversion complete. Statistics:
ARE %are_check%
BAF %baf_check%
BCS %bcs_check%
CRE %cre_check%
D   %d_check%
DLG %dlg_check%
EFF %eff_check%
GAM %gam_check%
ITM %itm_check%
PRO %pro_check%
SPL %spl_check%
STO %sto_check%
TP* %tp2_check%
TRA %tra_check%
VEF %vvc_check%
VVC %vvc_check%
WED %wed_check%
WMP %wmp_check%

%ignore_check% files ignored.
%ren_check% files renamed.~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BG:EE files patching and installation            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~BG:EE files patching and installation...~

/////                                                  \\\\\
///// Dummy mod files                                  \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH var IN b g3 000 UB xxx BEGIN
	ACTION_BASH_FOR ~%bgee_dir%/override~ ~^.+\.%var%$~ BEGIN
		COPY ~%BASH_FOR_FILESPEC%~ ~override~
	END
END

/////                                                  \\\\\
///// 2DA                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing 2DA files...~

COPY + ~EET/temp/2da/DPALACE.2DA~ ~EET/temp/2da~
	SET_2DA_ENTRY 8 1 2 "135"
	REPLACE ~70019~ @2000007

<<<<<<<< .../chapter.2da
SONG					%song%
>>>>>>>>
ACTION_DEFINE_ASSOCIATIVE_ARRAY table_EET_song BEGIN
	CHPTXT0 => 107
	DRMTXT2 => 111
	DRMTXT3 => 111
	DRMTXT4 => 111
	DRMTXT5 => 111
	DRMTXT6 => 111
	DRMTXT7 => 111
	//ARRIVEA => 
	//BPENDA => 
	//LEAVEA => 
	//TOSCENDA => 
	//ULGOTHST => 
END

ACTION_PHP_EACH table_EET_song AS file => song BEGIN
	COPY + ~EET/temp/2da/%file%.2DA~ ~EET/temp/2da~
		APPEND_FILE ~.../chapter.2da~ EVALUATE_BUFFER
		PRETTY_PRINT_2DA
END

OUTER_SET strref = RESOLVE_STR_REF (@2000000)
<<<<<<<< .../CHPTXT1.2da
2DA     V1.0
NONE
        0       1        2        3
SWITCH  DEFAULT DEFAULT  DEFAULT  DEFAULT
DEFAULT 16203   %strref% 11898    11899
SONG    135
>>>>>>>>
COPY ~.../CHPTXT1.2da~ ~override~ EVALUATE_BUFFER
	PRETTY_PRINT_2DA

<<<<<<<< .../CHPTXT.2da
2DA V1.0
NONE
        0           1
SWITCH  DEFAULT     DEFAULT
DEFAULT %strref_1%  %strref_2%
SONG    135
>>>>>>>>
ACTION_DEFINE_ASSOCIATIVE_ARRAY table_EET_CHPTXT BEGIN
	//1 , ~###16203~ , ~2000000~ => ~CHPTXT1~
	2 , ~###16204~ , ~2000001~ => ~CHPTXT2~
	3 , ~###16205~ , ~2000002~ => ~CHPTXT3~
	4 , ~###16206~ , ~2000003~ => ~CHPTXT4~
	5 , ~###16207~ , ~2000004~ => ~CHPTXT5~
	6 , ~###16208~ , ~2000005~ => ~CHPTXT6~
	7 , ~###16209~ , ~2000006~ => ~CHPTXT7~
	8 , ~2000059~ , ~56974~ => ~CHPTXT8~
	9 , ~2000060~ , ~56368~ => ~CHPTXT9~
	10 , ~2000061~ , ~56371~ => ~CHPTXT10~
	11 , ~2000062~ , ~56373~ => ~CHPTXT11~
	12 , ~2000063~ , ~56375~ => ~CHPTXT12~
	13 , ~2000065~ , ~56378~ => ~CHPTXT13~
END
ACTION_PHP_EACH table_EET_CHPTXT AS strref => file BEGIN
	ACTION_IF IS_AN_INT strref_1 BEGIN
		OUTER_SET strref_1 = RESOLVE_STR_REF ((AT strref_1))
	END
	ACTION_IF IS_AN_INT strref_2 BEGIN
		OUTER_SET strref_2 = RESOLVE_STR_REF ((AT strref_2))
	END
	COPY ~.../CHPTXT.2da~ ~override/%file%.2DA~ EVALUATE_BUFFER
		REPLACE_TEXTUALLY ~###~ ~~
		PRETTY_PRINT_2DA
END

COPY ~EET/base/2da~ ~override~
	~EET/temp/2da~ ~override~

COPY_EXISTING ~CAMPAIGN.2DA~ ~override~
	PRETTY_PRINT_2DA
	FOR (cnt = 4000100; cnt <= 4000113; cnt = cnt + 1) BEGIN
		REPLACE ~%cnt%~ ( AT "cnt" )
	END
	PRETTY_PRINT_2DA
BUT_ONLY

/////                                                  \\\\\
///// ACM / MUS                                        \\\\\
/////                                                  \\\\\

PRINT ~Installing ACM/MUS files...~

GET_DIRECTORY_ARRAY dir ~%bgee_dir%/music~ ~~
ACTION_PHP_EACH dir AS from => to BEGIN
	OUTER_PATCH_SAVE dir_name ~%to%~ BEGIN
		REPLACE_TEXTUALLY ~^%bgee_dir%/music/~ ~~
	END
	ACTION_TO_UPPER dir_name
	ACTION_IF (VARIABLE_IS_SET $table_2DA_songlist(~%dir_name%~)) BEGIN
		OUTER_TEXT_SPRINT dest_dir $table_2DA_songlist(~%dir_name%~)
	END ELSE BEGIN
		OUTER_SPRINT dest_dir ~%dir_name%~
	END
	ACTION_IF (NOT DIRECTORY_EXISTS ~music/%dest_dir%~) BEGIN
		MKDIR ~music/%dest_dir%~
		ACTION_BASH_FOR ~%to%~ ~^.+$~ BEGIN
			OUTER_PATCH_SAVE dest_file ~%BASH_FOR_FILE%~ BEGIN
				REPLACE_TEXTUALLY ~^%dir_name%~ ~%dest_dir%~
			END
			COPY_LARGE ~%BASH_FOR_FILESPEC%~ ~music/%dest_dir%/%dest_file%~
		END
	END
END

ACTION_BASH_FOR ~%bgee_dir%/music~ ~^.+\.mus$~ BEGIN
	ACTION_TO_UPPER BASH_FOR_RES
	ACTION_IF (VARIABLE_IS_SET $table_2DA_songlist(~%BASH_FOR_RES%~)) BEGIN
		OUTER_TEXT_SPRINT dest_res $table_2DA_songlist(~%BASH_FOR_RES%~)
	END ELSE BEGIN
		OUTER_SPRINT dest_res ~%BASH_FOR_RES%~
	END
	ACTION_IF (NOT FILE_EXISTS ~music/%dest_res%.mus~) BEGIN
		COPY ~%BASH_FOR_FILESPEC%~ ~music/%dest_res%.mus~
			REPLACE_TEXTUALLY ~%BASH_FOR_RES%~ ~%dest_res%~
	END
END

/////                                                  \\\\\
///// ARE                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing ARE files...~

//typo in BG:EE, assigned AE1213.BCS instead of AR1213.BCS
COPY + ~%patch_dir%/are/BG1213.ARE~ ~%patch_dir%/are~
	WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
BUT_ONLY

//Imoen ARE actor spawn has Override Script Name flag set, disable it to use Imoen2 DV
COPY + ~%patch_dir%/are/BD0103.ARE~ ~%patch_dir%/are~
	READ_LONG  0x54 "actor_off"
	READ_SHORT 0x58 "actor_num"
	FOR (index = 0 ; index < actor_num ; index = index + 1) BEGIN
		READ_ASCII ("%actor_off%" + (0x110 * "%index%")) "name"
		PATCH_IF ("%name%" STRING_EQUAL_CASE "Imoen") BEGIN
			//WRITE_ASCII ("%actor_off%" + (0x110 * "%index%")) "Imoen2"
			READ_LONG ("%actor_off%" + 0x28 + (0x110 * "%index%")) "flags"
			PATCH_IF (flags BAND BIT3) BEGIN
				SET flags = flags - BIT3
				WRITE_LONG ("%actor_off%" + 0x28 + (0x110 * "%index%")) "flags"
			END
		END
	END
BUT_ONLY

// Area script assigner
COPY + ~%patch_dir%/are~ ~%patch_dir%/are~
	PATCH_IF (SOURCE_SIZE > 0x11b) BEGIN
		READ_ASCII 0x94 "script"
		PATCH_IF (FILE_EXISTS ~%patch_dir%/bcs/%script%.BCS~) BEGIN
		END ELSE PATCH_IF (FILE_EXISTS ~%patch_dir%/bcs/%SOURCE_RES%.BCS~) BEGIN
			PATCH_PRINT ~%SOURCE_FILE% - Area script (%SOURCE_RES%.BCS) assigned~
			WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
		END ELSE PATCH_IF ((~%script%~ STRING_EQUAL_CASE ~~)=0) AND ((~%script%~ STRING_EQUAL_CASE ~NONE~)=0) BEGIN
			PATCH_PRINT ~%SOURCE_FILE% - Missing area script (%script%.BCS) created~
			INNER_ACTION BEGIN
				COPY + ~EET/base/blank.bcs~ ~%patch_dir%/bcs/%script%.BCS~
			END
		END ELSE BEGIN
			PATCH_PRINT ~%SOURCE_FILE% - Missing area script (%SOURCE_RES%.BCS) created and assigned~
			WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
			INNER_ACTION BEGIN
				COPY + ~EET/base/blank.bcs~ ~%patch_dir%/bcs/%SOURCE_RES%.BCS~
			END
		END
	END
BUT_ONLY

OUTER_SPRINT logged ~~
COPY ~%patch_dir%/are~ ~override~
	WRITE_SHORT 0x48 THIS | (1 << BG1AREA)
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff RET log END
	SPRINT logged ~%logged%%LNL%%log%~
COPY + ~.../blank.txt~ ~EET/temp/are.tph~
	PATCH_LOG ~%logged%~
APPEND_OUTER + ~EET/temp/are.tph~ ~%logged%~

COPY_EXISTING ~BD6100.ARE~ ~override~
	~AR0602.ARE~ ~override~
	LPF fj_are_structure
		INT_VAR
		fj_type        = 8 //nonvisible
		fj_loc_x       = 88
		fj_loc_y       = 76
		fj_box_left    = 72
		fj_box_top     = 26
		fj_box_right   = 120
		fj_box_bottom  = 58
		fj_trap_loc_x  = 80
		fj_trap_loc_y  = 70
		fj_vertex_0    = 111 + (58 << 16)
		fj_vertex_1    = 72 + (45 << 16)
		fj_vertex_2    = 82 + (26 << 16)
		fj_vertex_3    = 120 + (39 << 16)
		STR_VAR
		fj_structure_type = container
		fj_name           = ~K#ImportContainer~
	END

/*COPY_EXISTING ~TU0015.are~ ~override~
	~TU0016.are~ ~override~
	~TU0017.are~ ~override~
	~TU0018.are~ ~override~
	READ_BYTE 0x14 "flags"
	PATCH_IF (~%flags%~ BAND BIT0) BEGIN
	END ELSE BEGIN
		SET flags = flags + BIT0
		WRITE_BYTE 0x14 "flags"
	END
BUT_ONLY*/

/////                                                  \\\\\
///// BAM                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing BAM files...~

COPY_LARGE + ~EET/base/bam~ ~%biff_dir%~

/////                                                  \\\\\
///// BCS                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing BCS files...~

//restore IDS files
COPY ~EET/temp/RACE.IDS~ ~override~
	~EET/temp/CLASS.IDS~ ~override~
	~EET/temp/SPECIFIC.IDS~ ~override~

//compile scripts
COMPILE ~%patch_dir%/bcs~
	~EET/compile/baf~

//patching BG:EE scripts
INCLUDE ~EET/lib/bg1_BCS.tph~

/////                                                  \\\\\
///// BMP                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing BMP files...~

//nothing to do

/////                                                  \\\\\
///// BS                                               \\\\\
/////                                                  \\\\\

PRINT ~Installing BS files...~

MKDIR ~scripts~

COPY ~%bgee_dir%/scripts/ohfight1.bs~ ~scripts~

COPY ~EET/base/test/EET_test.baf~ ~scripts/EET_test.bs~
	COMPILE_BAF_TO_BCS

/////                                                  \\\\\
///// CHR                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing CHR files...~

MKDIR ~characters~

ACTION_BASH_FOR ~%patch_dir%/chr~ ~^.+\.chr$~ BEGIN
	COPY ~%BASH_FOR_FILESPEC%~ ~characters~
		LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff RET log END
		PATCH_LOG ~%log%~
END

/////                                                  \\\\\
///// CRE                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing CRE files...~

OUTER_SPRINT logged ~~
COPY ~%patch_dir%/cre~ ~override~
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff STR_VAR array = remapped_strref RET log END
	SPRINT logged ~%logged%%LNL%%log%~
COPY + ~.../blank.txt~ ~EET/temp/cre.tph~
	PATCH_LOG ~%logged%~
APPEND_OUTER + ~EET/temp/cre.tph~ ~%logged%~

COPY ~EET/base/cre~ ~override~

// patching BG:EE cre files
INCLUDE ~EET/lib/bg1_cre.tph~

// Find Familiar externalization
ACTION_FOR_EACH file IN FAMCAT_ FAMDUST_ FAMFAIR_ FAMFER_ FAMIMP_ FAMPSD_ FAMQUAS_ FAMRAB_ FAMCAT FAMDUST FAMFAIR FAMFER FAMIMP FAMPSD FAMQUAS FAMRAB FAMCAT25 FAMDUS25 FAMFAI25 FAMFER25 FAMIMP25 FAMPSD25 FAMQUA25 FAMRAB25 BEGIN
	COPY_EXISTING ~%file%.CRE~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode = 232 /*Cast Spell on Condition*/ target = 1 /*Self*/ parameter2 = 16 /*Target dies*/ timing = 9 /*Instant/Permanent*/ STR_VAR resource = "K#FAMKIL" END
	BUT_ONLY
END

/////                                                  \\\\\
///// DLG                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing DLG files...~

COPY + ~%patch_dir%/dlg/BPNAJIM.D~ ~%patch_dir%/dlg~
	REPLACE_TEXTUALLY ~IncrementChapter("BPEND")[%newline%]GoToStartScreen()~ ~ClearAllActions()
    ReallyForceSpellRES("K#FAMREM",Player1)
    ReallyForceSpellRES("K#FAMREM",Player2)
    ReallyForceSpellRES("K#FAMREM",Player3)
    ReallyForceSpellRES("K#FAMREM",Player4)
    ReallyForceSpellRES("K#FAMREM",Player5)
    ReallyForceSpellRES("K#FAMREM",Player6)
    StartCutScene("K#CUTBP2")~
BUT_ONLY

COMPILE ~%patch_dir%/dlg~
	~EET/compile/d~

/////                                                  \\\\\
///// EFF                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing EFF files...~

OUTER_SPRINT logged ~~
COPY ~%patch_dir%/eff~ ~override~
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff STR_VAR array = remapped_strref RET log END
	SPRINT logged ~%logged%%LNL%%log%~
COPY + ~.../blank.txt~ ~EET/temp/eff.tph~
	PATCH_LOG ~%logged%~
APPEND_OUTER + ~EET/temp/eff.tph~ ~%logged%~

COPY ~EET/base/eff~ ~override~

/////                                                  \\\\\
///// GAM                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing GAM...~

OUTER_SPRINT ~gam~ ~2DA%WNL%dummy%WNL%CRE_NAME ORIENTATION AREA X Y~
COPY + ~%patch_dir%/BALDUR.GAM~ ~%patch_dir%~
	READ_LONG 0x30 npcs_off
	READ_LONG 0x34 npcs_cnt
	FOR (cnt=0; cnt<"%npcs_cnt%"; cnt=cnt+1) BEGIN
		SET offset = ("%npcs_off%"+0x160*cnt)
		READ_ASCII offset + 0xc "cre_name"
		READ_LONG offset + 0x14 "cre_orient"
		READ_ASCII offset + 0x18 "area_name"
		READ_SHORT offset + 0x20 "loc_x"
		READ_SHORT offset + 0x22 "loc_y"
		SPRINT ~gam~ ~%gam%%WNL%%cnt% %cre_name% %cre_orient% %area_name% %loc_x% %loc_y%~
	END
	PATCH_LOG ~%gam%~

COPY + ~.../blank.txt~ ~EET/temp/gam.2da~
APPEND_OUTER + ~EET/temp/gam.2da~ ~%gam%~
COPY + ~EET/temp/gam.2da~ ~EET/temp~
	PRETTY_PRINT_2DA

COPY - ~EET/temp/gam.2da~ ~EET/temp~
	COUNT_2DA_ROWS 6 GAM_count
	READ_2DA_ENTRIES_NOW ~#GAM_patch_table~ 6
BUT_ONLY

COPY ~EET/base/BALDUR.GAM~ ~override~
	READ_LONG 0x20 "PC_offset"
	READ_LONG 0x28 "US_offset"
	READ_LONG 0x30 "npcs_off"
	READ_LONG 0x34 "npcs_cnt"
	READ_LONG 0x38 "glob_offset"
	READ_LONG 0x50 "jrnl_offset"
	READ_LONG 0x68 "famil_offset"
	READ_LONG 0x6c "locat_offset"
	READ_LONG 0x78 "US2_offset"

	FOR (cnt=0; cnt<"%GAM_count%"; cnt=cnt+1) BEGIN
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 1 "cre_name"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 2 "cre_orient"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 3 "area_name"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 4 "loc_x"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 5 "loc_y"

		INSERT_BYTES "%npcs_off%" 0x160
		WRITE_ASCIIE ("%npcs_off%" + 0x0c) "%cre_name%" #8
		WRITE_ASCIIE ("%npcs_off%" + 0x18) "%area_name%" #8
		WRITE_SHORT ("%npcs_off%" + 0x20) "%loc_x%"
		WRITE_SHORT ("%npcs_off%" + 0x22) "%loc_y%"
		WRITE_SHORT ("%npcs_off%" + 0x8c) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0x8e) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0x90) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0x92) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0xb4) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0xb6) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0xb8) 0xffff	  
	END

	PATCH_IF ("%cnt%" > 0) BEGIN	
		SET delta = 0x160 * cnt
		SET npcs_cnt = npcs_cnt + cnt
		WRITE_LONG 0x34 "%npcs_cnt%"

		PATCH_IF ("%PC_offset%">="%npcs_off%") BEGIN
			SET PC_offset = PC_offset + delta
			WRITE_LONG 0x20 "%PC_offset%"
		END
		PATCH_IF ("%US_offset%">="%npcs_off%") BEGIN
			SET US_offset = US_offset + delta
			WRITE_LONG 0x28 "%US_offset%"
		END
		PATCH_IF ("%glob_offset%">="%npcs_off%") BEGIN
			SET glob_offset = glob_offset + delta
			WRITE_LONG 0x38 "%glob_offset%"
		END
		PATCH_IF ("%jrnl_offset%">="%npcs_off%") BEGIN
			SET jrnl_offset = jrnl_offset + delta
			WRITE_LONG 0x50 "%jrnl_offset%"
		END
		PATCH_IF ("%famil_offset%">="%npcs_off%") BEGIN
			SET famil_offset = famil_offset + delta
			WRITE_LONG 0x68 "%famil_offset%"
		END
		PATCH_IF ("%locat_offset%">="%npcs_off%") BEGIN
			SET locat_offset = locat_offset + delta
			WRITE_LONG 0x6c "%locat_offset%"
		END
		PATCH_IF ("%US2_offset%">="%npcs_off%") BEGIN
			SET US2_offset = US2_offset + delta
			WRITE_LONG 0x78 "%US2_offset%"
		END
	END

/////                                                  \\\\\
///// GLSL                                             \\\\\
/////                                                  \\\\\

PRINT ~Installing GLSL files...~

COPY ~EET/base/glsl~ ~override~ //check in future patch if fpYUV.GLSL is fixed in BG2:EE

/////                                                  \\\\\
///// GUI                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing GUI...~

ACTION_BASH_FOR ~EET/base/gui~ ~^.+\.mbc$~ BEGIN
	AT_NOW ~%tileconv% -e -o "EET%os_slash%temp%os_slash%biff%os_slash%%BASH_FOR_RES%.mos" "EET%os_slash%base%os_slash%gui%os_slash%%BASH_FOR_FILE%" %bash_debug%~ EXACT
END
LAM bash_log

ACTION_BASH_FOR ~EET/base/gui/sod~ ~^.+\.mbc$~ BEGIN
	AT_NOW ~%tileconv% -e -o "EET%os_slash%SoD_GUI%os_slash%%BASH_FOR_RES%.mos" "EET%os_slash%base%os_slash%gui%os_slash%sod%os_slash%%BASH_FOR_FILE%" %bash_debug%~ EXACT
END
LAM bash_log

/*ACTION_DEFINE_ASSOCIATIVE_ARRAY table_EET_fileIndex BEGIN
	"WORLDMAP" , "MOS" => "20"
	"BIGLOGO" , "BAM" => "30"
	"TITLE" , "BAM" => "40"
	"CMPGEET" , "BAM" => "50"
	"STARTSOD" , "MOS" => "60"
	"MAPICONS" , "BAM" => "70"
	"STARTBG2" , "MOS" => "80"
END
ACTION_PHP_EACH table_EET_fileIndex AS file => index BEGIN
	COPY + ~EET/base/gui/%file%.%file_1%~ ~%biff_dir%~
		LPF ~UPDATE_PVRZ_INDICES~ RET original_base_index new_base_index END
	ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
		ACTION_BASH_FOR ~EET/base/gui~ ~mos%index%.*\.pvrz~ BEGIN
			LAF ~INSTALL_PVRZ~ INT_VAR original_base_index new_base_index STR_VAR source_file = EVAL ~%BASH_FOR_FILESPEC%~ END
		END
	END ELSE BEGIN
		WARN ~WARNING: Couldn't install PVRZ files~
	END
END*/

//ACTION_CLEAR_ARRAY remapped_pvrz

ACTION_FOR_EACH file IN ~MAPICONS.BAM~ ~WORLDMAP.MOS~ ~BIGLOGO.BAM~ ~TITLE.BAM~ ~CMPGEET.BAM~ ~STARTSOD.MOS~ ~STARTBG2.MOS~ BEGIN
	COPY + ~EET/base/gui/%file%~ ~%biff_dir%~
		READ_LONG 0x10 "blocks_cnt"
		PATCH_IF (~%SOURCE_EXT%~ STRING_EQUAL_CASE ~bam~) BEGIN
			READ_LONG 0x1c "blocks_off"
		END ELSE BEGIN //mos
			READ_LONG 0x14 "blocks_off"
		END
		FOR (cnt=0; cnt<"%blocks_cnt%"; cnt=cnt+1) BEGIN
			READ_LONG ("%blocks_off%"+0x1c*cnt+0x0) "original_base_index"
			//PATCH_PRINT ~cnt = %cnt%, original_base_index = %original_base_index%~
			PATCH_IF (VARIABLE_IS_SET $remapped_pvrz_base(~%original_base_index%~)) BEGIN
				TEXT_SPRINT new_base_index $remapped_pvrz_base(~%original_base_index%~)
				WRITE_LONG ("%blocks_off%"+0x1c*cnt+0x0) "new_base_index"
			END ELSE BEGIN
				SET new_base_index = reserve_index_start + reserve_index_cnt //variables already set via prep_PVRZ.tph
				SET reserve_index_cnt = reserve_index_cnt + 1
				//PATCH_PRINT ~remapped_pvrz_base = %original_base_index% => %new_base_index%~
				DEFINE_ASSOCIATIVE_ARRAY remapped_pvrz_base BEGIN ~%original_base_index%~ => ~%new_base_index%~ END
				WRITE_LONG ("%blocks_off%"+0x1c*cnt+0x0) "new_base_index"
				PATCH_FOR_EACH index IN original_base_index new_base_index BEGIN
					SET value = EVAL ~%index%~
					PATCH_IF (value < 10) BEGIN
						SPRINT EVAL ~%index%~ ~000%value%~
					END ELSE PATCH_IF (value < 100) BEGIN
						SPRINT EVAL ~%index%~ ~00%value%~
					END ELSE PATCH_IF (value < 1000) BEGIN
						SPRINT EVAL ~%index%~ ~0%value%~
					END ELSE BEGIN
						SPRINT EVAL ~%index%~ ~%value%~
					END
				END
				INNER_ACTION BEGIN
					//LAF ~INSTALL_PVRZ~ INT_VAR original_base_index new_base_index STR_VAR source_file = EVAL ~EET/base/gui/mos%pvrz_name%.pvrz~ target_folder = EVAL ~%biff_dir%~ END
					COPY_LARGE + ~EET/base/gui/mos%original_base_index%.pvrz~ ~%biff_dir%/mos%new_base_index%.pvrz~
				END
			END
		END
END

ACTION_BASH_FOR ~%biff_dir%/pvrz~ ~^.+\.pvrz$~ BEGIN
	MOVE + ~%biff_dir%/pvrz~ ~%biff_dir%~
END

/////                                                  \\\\\
///// INI                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing INI files...~

COPY ~%patch_dir%/ini~ ~override~

/////                                                  \\\\\
///// ITM                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing ITM files...~

COPY ~EET/base/itm/SCRL1V_.ITM~ ~override~

OUTER_SPRINT logged ~~
COPY ~%patch_dir%/itm~ ~override~
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff STR_VAR array = remapped_strref RET log END
	SPRINT logged ~%logged%%LNL%%log%~
COPY + ~.../blank.txt~ ~EET/temp/itm.tph~
	PATCH_LOG ~%logged%~
APPEND_OUTER + ~EET/temp/itm.tph~ ~%logged%~

COPY_EXISTING ~BPRNG1.ITM~ ~override~
	LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = "208" END //Minimum HP
BUT_ONLY

//Modify permanent sleep spell effect (SoD Final)
COPY_EXISTING ~BDDAGG06.ITM~ ~override~
	~BDDAGG07.ITM~ ~override~
	LPF ALTER_ITEM_EFFECT INT_VAR check_headers = 1 match_opcode = 39 /*Sleep*/ timing = 0 /*Instant/Limited*/ duration = 7200 /*ONE_DAY*/ END
BUT_ONLY

/////                                                  \\\\\
///// PRO                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing PRO files...~

COPY ~%patch_dir%/pro~ ~override~

/////                                                  \\\\\
///// SPL                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing SPL files...~

//Find Familiar related spells
COPY ~EET/base/spl/SPWI123.SPL~ ~override~
	~EET/base/spl/SPCL342.SPL~ ~override~
	~EET/base/spl/K#FAMKIL.SPL~ ~override~
	~EET/base/spl/K#FAMREM.SPL~ ~override~
	~EET/base/spl/K#REST.SPL~ ~override~
	~EET/base/spl/K#UNREST.SPL~ ~override~
ACTION_FOR_EACH var IN 4 6 9 12 24 BEGIN
	COPY ~EET/base/spl/K#CURE.SPL~ ~override/K#CURE%var%.SPL~
		WRITE_LONG 0x9e "%var%"
	//OUTER_SET value = ((var / 2) * 3)
	//COPY ~EET/base/spl/K#DMG.SPL~ ~override/K#DMG%value%.SPL~
	//	WRITE_LONG 0x9e "%value%"
END

//remove Bhaal Powers
COPY ~EET/base/spl/K#REMBHA.SPL~ ~override~

//assign BG2 portraits on transition
ACTION_DEFINE_ASSOCIATIVE_ARRAY table_EET_portraitID BEGIN
	"NEDWIN" => "EDW"
	"NIMOEN" => "IMO"
	"NJAHEIR" => "JAH"
	"NMINSC" => "MIN"
	"NVICON" => "VIC"
END
ACTION_PHP_EACH table_EET_portraitID AS portrait => ID BEGIN
	COPY ~EET/base/spl/K#PORTR.SPL~ ~override/K#POR%ID%.SPL~
		WRITE_ASCIIE ~0xae~ ~%portrait%S~ #8 // Small portrait
		WRITE_ASCIIE ~0xde~ ~%portrait%M~ #8 // Medium portrait
END

//assign NPC colors after resurrection from chunk death
ACTION_DEFINE_ASSOCIATIVE_ARRAY table_EET_fileID BEGIN
	"IMOEN" => "IMO"
	"EDWIN" => "EDW"
END
ACTION_PHP_EACH table_EET_fileID AS file => ID BEGIN
	COPY_EXISTING ~%file%.cre~ ~override~
		READ_BYTE 0x2c "metal" //Metal Colour Index	9e
		READ_BYTE 0x2d "minor" //Minor Colour Index
		READ_BYTE 0x2e "major" //Major Colour Index
		READ_BYTE 0x2f "skin" //Skin Colour Index
		READ_BYTE 0x30 "leather" //Leather Colour Index
		READ_BYTE 0x31 "armor" //Armor Colour Index
		READ_BYTE 0x32 "hair" //Hair Colour Index
	BUT_ONLY
	COPY ~EET/base/spl/K#COLOR.SPL~ ~override/K#COL%ID%.SPL~
		WRITE_LONG 0x9e "metal"
		WRITE_LONG 0xce "minor"
		WRITE_LONG 0xfe "major"
		WRITE_LONG 0x12e "skin"
		WRITE_LONG 0x15e "leather"
		WRITE_LONG 0x18e "armor"
		WRITE_LONG 0xbe "hair"
END

OUTER_SPRINT logged ~~
COPY ~%patch_dir%/spl~ ~override~
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff STR_VAR array = remapped_strref RET log END
	SPRINT logged ~%logged%%LNL%%log%~
COPY + ~.../blank.txt~ ~EET/temp/spl.tph~
	PATCH_LOG ~%logged%~
APPEND_OUTER + ~EET/temp/spl.tph~ ~%logged%~

/////                                                  \\\\\
///// STO                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing STO files...~

OUTER_SPRINT logged ~~
COPY ~%patch_dir%/sto~ ~override~
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff RET log END
	SPRINT logged ~%logged%%LNL%%log%~
COPY + ~.../blank.txt~ ~EET/temp/sto.tph~
	PATCH_LOG ~%logged%~
APPEND_OUTER + ~EET/temp/sto.tph~ ~%logged%~

/////                                                  \\\\\
///// VEF                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing VEF files...~

COPY ~%patch_dir%/vef~ ~override~

/////                                                  \\\\\
///// VVC                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing VVC files...~

COPY ~%patch_dir%/vvc~ ~override~

/////                                                  \\\\\
///// WAV                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing WAV files...~

ACTION_FOR_EACH dir IN ~%bgee_dir%/lang/%LANGUAGE%/sounds~ ~%bgee_dir%/lang/en_US/sounds~ ~%bgee_dir%/sounds~ BEGIN
	ACTION_BASH_FOR ~%dir%~ ~^.+\.wav$~ BEGIN
		ACTION_TO_UPPER BASH_FOR_RES
		ACTION_IF (VARIABLE_IS_SET $remapped_wav(~%BASH_FOR_RES%~)) BEGIN
			OUTER_TEXT_SPRINT dest_file $remapped_wav(~%BASH_FOR_RES%~)
		END ELSE BEGIN
			OUTER_SPRINT dest_file ~%BASH_FOR_RES%~
		END
		ACTION_IF (NOT FILE_EXISTS ~lang/%LANGUAGE_BG2%/sounds/%dest_file%.wav~) BEGIN
			COPY_LARGE ~%BASH_FOR_FILESPEC%~ ~lang/%LANGUAGE_BG2%/sounds/%dest_file%.wav~
		END
	END
END

COPY_LARGE + ~EET/base/wav~ ~EET/temp/wav~

/////                                                  \\\\\
///// WBM                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing WBM files...~

MKDIR ~lang/%LANGUAGE_BG2%/movies~

//BG1 movies with voice over
ACTION_FOR_EACH vidname IN BEREGOST INTRO_ NASHKELL WYVERN BEGIN
	ACTION_IF (FILE_EXISTS ~%tra_path%/%LANGUAGE%/movies/%vidname%.ogg~) BEGIN
		AT_NOW ~%ffmpeg% -i EET%os_slash%base%os_slash%lmovies%os_slash%%vidname%.wbm -i EET%os_slash%lang%os_slash%%LANGUAGE%%os_slash%movies%os_slash%%vidname%.ogg -acodec copy -vcodec copy EET%os_slash%temp%os_slash%%vidname%.webm~
	END ELSE BEGIN
		AT_NOW ~%ffmpeg% -i EET%os_slash%base%os_slash%lmovies%os_slash%%vidname%.wbm -i EET%os_slash%lang%os_slash%en_US%os_slash%movies%os_slash%%vidname%.ogg -acodec copy -vcodec copy EET%os_slash%temp%os_slash%%vidname%.webm~
	END
	COPY_LARGE ~EET/temp/%vidname%.webm~ ~lang/%LANGUAGE_BG2%/movies/%vidname%.wbm~
END

//other movies
COPY_LARGE ~EET/base/movies~ ~lang/%LANGUAGE_BG2%/movies~

ACTION_FOR_EACH dir IN ~%bgee_dir%/lang/%LANGUAGE%/movies~ ~%bgee_dir%/lang/en_US/movies~ ~%bgee_dir%/movies~ BEGIN
	ACTION_BASH_FOR ~%dir%~ ~^.+\.wbm$~ BEGIN
		ACTION_TO_UPPER BASH_FOR_RES
		ACTION_IF (VARIABLE_IS_SET $remapped_mve(~%BASH_FOR_RES%~)) BEGIN
			OUTER_TEXT_SPRINT dest_file $remapped_mve(~%BASH_FOR_RES%~)
		END ELSE BEGIN
			OUTER_SPRINT dest_file ~%BASH_FOR_RES%~
		END
		ACTION_IF (NOT FILE_EXISTS ~lang/%LANGUAGE_BG2%/movies/%dest_file%.wbm~) BEGIN
			COPY_LARGE ~%BASH_FOR_FILESPEC%~ ~lang/%LANGUAGE_BG2%/movies/%dest_file%.wbm~
		END
	END
END

/////                                                  \\\\\
///// WED                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing WED files...~

// patching BG3300 (Beregost)
ACTION_DEFINE_ARRAY tilesToFix BEGIN 3799 3800 3879 3880 END
LAF FIX_WATER INT_VAR numTilesToFix = 4 STR_VAR wed = ~%patch_dir%/wed/bg3300.wed~ tis = ~%biff_dir%/eetBG33/bg3300.tis~ END

// patching BG3000
ACTION_DEFINE_ARRAY tilesToFix BEGIN 2588 END
LAF FIX_WATER INT_VAR numTilesToFix = 1 STR_VAR wed = ~%patch_dir%/wed/bg3000.wed~ tis = ~%biff_dir%/eetBG30/bg3000.tis~ END

// patching BG1802
ACTION_DEFINE_ARRAY tilesToFix BEGIN 1292 1293 END
LAF FIX_WATER INT_VAR numTilesToFix = 2 STR_VAR wed = ~%patch_dir%/wed/bg1802.wed~ tis = ~%biff_dir%/eetBG18/bg1802.tis~ END

COPY ~%patch_dir%/wed~ ~override~

/////                                                  \\\\\
///// WFX                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing WFX files...~

COPY ~EET/temp/wfx~ ~override~

/////                                                  \\\\\
///// WMP                                              \\\\\
/////                                                  \\\\\

PRINT ~Installing WMP files...~

MKDIR ~Worldmap~

COPY_EXISTING ~xnewarea.2da~ ~override~
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~^.*ar3000.*$~ ~1~ //without 1 engine behave in weird way (sets XP CAP to 2950000)
IF ~ar3000~
BUT_ONLY

//icons_array
ACTION_DEFINE_ASSOCIATIVE_ARRAY wmp_bg2_icons_array BEGIN
	OH4000 , 29 => 106
	OH4100 , 32 => 108
	OH5100 , 34 => 110
	OH6000 , 33 => 109
	OH6100 , 31 => 107
	OH6200 , 31 => 107
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY wmp_tob_icons_array BEGIN
	AR4000 , 6 => 91
	AR3000 , 5 => 90
	AR5000 , 8 => 93
	AR5500 , 14 => 99
	AR5203 , 7 => 92
	AR5202 , 9 => 94
	AR5200 , 10 => 95
	AR6300 , 15 => 100
	AR6400 , 13 => 98
	AR6000 , 16 => 101
	AR6100 , 12 => 97
	OH4200 , 18 => 111
	OH6400 , 11 => 96
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY wmp_bg2_2da_links_array BEGIN
	AR3000 => XL3000
END

//dump wmp
COPY + ~%patch_dir%/worldmap.wmp~ ~%patch_dir%~
	LPF ~dump_wmp~
	INT_VAR
		tra_read = 1
		skip_areas = 1
	STR_VAR
		output_links = "EET/temp/map_bgee_links.tbl"
		output_strings = "EET/temp/map_bgee_trans.tra"
	END

COPY + ~%patch_dir%/sodmap.wmp~ ~%patch_dir%~
	LPF ~dump_wmp~
	INT_VAR
		tra_read = 1
		skip_areas = 1
	STR_VAR
		output_links = "EET/temp/map_sod_links.tbl"
		output_strings = "EET/temp/map_sod_trans.tra"
	END

COPY_EXISTING ~worldmap.wmp~ ~override~
	LPF ~dump_wmp~
	INT_VAR
		move_y = 2283
		compare_bams = 1
	STR_VAR
		2da_links_array = "wmp_bg2_2da_links_array"
		icons_array = "wmp_bg2_icons_array"
		output_areas = "EET/temp/map_bg2_areas.tbl"
		output_links = "EET/temp/map_bg2_links.tbl"
		output_strings = "EET/temp/map_bg2_trans.tra"
	END
BUT_ONLY

COPY_EXISTING ~worldm25.wmp~ ~override~
	LPF ~dump_wmp~
	INT_VAR
		move_x = 893
		move_y = 3011
		compare_bams = 1
	STR_VAR
		icons_array = "wmp_tob_icons_array"
		output_areas = "EET/temp/map_tob_areas.tbl"
		output_links = "EET/temp/map_tob_links.tbl"
		output_strings = "EET/temp/map_tob_trans.tra"
	END
BUT_ONLY

//manual edits
COPY + ~EET/tbl/map_mods_areas.tbl~ ~EET/temp/map_bgee_areas.tbl~ //only used by BP-BGT Worldmap

COPY + ~EET/temp/map_sod_links.tbl~ ~EET/temp~
	REPLACE_TEXTUALLY ~\(BD2000[ %TAB%]+W[ %TAB%]+BD3000[ %TAB%]+EXITN[ %TAB%]+\)84~ ~\142~

COPY + ~EET/temp/map_bgee_links.tbl~ ~EET/temp~
	APPEND_FILE ~EET/temp/map_sod_links.tbl~
	APPEND_FILE ~EET/tbl/map_mods_links.tbl~

COPY + ~EET/temp/map_bgee_trans.tra~ ~EET/temp~
	REPLACE_EVALUATE CASE_INSENSITIVE ~^\(@[A-Z0-9]+\)\(.*[%newline%]*\)~ BEGIN
		PATCH_IF (FILE_CONTAINS_EVALUATED (~EET/lang/%LANGUAGE%/map_mods_trans.tra~ ~^%MATCH1%[ %TAB%]~)) BEGIN
			SPRINT MATCH1 ~~
			SPRINT MATCH2 ~~
		END
	END ~%MATCH1%%MATCH2%~
	APPEND_FILE ~EET/lang/%LANGUAGE%/map_mods_trans.tra~
	APPEND_FILE ~EET/temp/map_sod_trans.tra~

ACTION_DEFINE_ASSOCIATIVE_ARRAY wmp_soa_xy_array BEGIN //little fixes for misplaced Athkatla icons (useful also for vanilla BG2 worldmap)
	1 , "-1" , "-4" => AR0800
	2 , "3" , "5" => AR0400
	3 , "0" , "4" => AR0700
	4 , "3" , "-3" => AR0300
	5 , "-1" , "1" => AR0020
	6 , "-6" , "1" => AR0500
	7 , "0" , "3" => AR1000
	8 , "4" , "2" => AR0900
END

COPY + ~EET/temp/map_bg2_areas.tbl~ ~EET/temp~
	REPLACE_TEXTUALLY ~\(AR0700[ ]+\)15 ~ ~\13  ~
	PHP_EACH wmp_soa_xy_array AS xy => area BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(%area%[ ]+[0-9]+[ ]+[0-9]+[ ]+\)\([0-9]+\)\([ ]+\)\([0-9]+\)~ BEGIN
			SET MATCH2 = MATCH2 + xy_1
			SET MATCH4 = MATCH4 + xy_2
		END ~%MATCH1%%MATCH2%%MATCH3%%MATCH4%~
	END

ACTION_DEFINE_ASSOCIATIVE_ARRAY wmp_tob_xy_array BEGIN //move icons to lore friendly coordinates
	1 , "-182" , "-157" => AR3000
	2 , "-420" , "-60" => AR4000
	3 , "15" , "-33" => OH6400
END

COPY + ~EET/temp/map_tob_areas.tbl~ ~EET/temp~
	REPLACE_TEXTUALLY ~\(AR3000[ ]+\)7 ~ ~\13 ~
	REPLACE_TEXTUALLY ~\(AR4000[ ]+\)7 ~ ~\13 ~
	PHP_EACH wmp_tob_xy_array AS xy => area BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(%area%[ ]+[0-9]+[ ]+[0-9]+[ ]+\)\([0-9]+\)\([ ]+\)\([0-9]+\)~ BEGIN
			SET MATCH2 = MATCH2 + xy_1
			SET MATCH4 = MATCH4 + xy_2
		END ~%MATCH1%%MATCH2%%MATCH3%%MATCH4%~
	END

COPY + ~EET/temp/map_tob_trans.tra~ ~EET/temp~
	REPLACE_TEXTUALLY ~^@AR4000.*[%newline%]*~ ~~
	REPLACE_TEXTUALLY ~^@AR6400.*[%newline%]*~ ~~

//BP-BGT Worldmap
ACTION_FOR_EACH file IN areas.tbl links.tbl trans.tra BEGIN
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/Worldmap/map_mods_%file%~) BEGIN
		COPY + ~%bgee_dir%/Worldmap/map_mods_%file%~ ~EET/temp~
			PHP_EACH remapped_are AS source => dest BEGIN
				REPLACE_TEXTUALLY ~%source%~ ~%dest%~
			END
	END
	ACTION_IF (FILE_EXISTS ~Worldmap/map_mods_%file%~) BEGIN
		COPY ~Worldmap/map_mods_%file%~ ~Worldmap~
			APPEND_FILE ~EET/temp/map_bgee_%file%~
			PATCH_IF (FILE_EXISTS ~EET/temp/map_mods_%file%~) BEGIN
				APPEND_FILE ~EET/temp/map_mods_%file%~
			END
	END ELSE BEGIN
		COPY ~EET/temp/map_bgee_%file%~ ~Worldmap/map_mods_%file%~
			PATCH_IF (FILE_EXISTS ~EET/temp/map_mods_%file%~) BEGIN
				APPEND_FILE ~EET/temp/map_mods_%file%~
			END
	END
END

//EET worldmap

COPY + ~EET/tbl/map_eet_areas.tbl~ ~EET/temp~
	APPEND_FILE ~EET/temp/map_bg2_areas.tbl~
	APPEND_FILE ~EET/temp/map_tob_areas.tbl~
	LPF fix_table END
	LPM read_area_array_from_2da

COPY + ~Worldmap/map_mods_links.tbl~ ~EET/temp/map_eet_links.tbl~
	APPEND_FILE ~EET/temp/map_bg2_links.tbl~
	APPEND_FILE ~EET/tbl/map_ar3000_links.tbl~ //for some reason BG2:EE doesn't have links to Watcher's Keep (XL3000.2DA only has links from it)
	APPEND_FILE ~EET/temp/map_tob_links.tbl~
	LPF fix_table END
	LPM read_link_array_from_2da

COPY + ~Worldmap/map_mods_trans.tra~ ~EET/temp/map_eet_trans.tra~
	APPEND_FILE ~EET/temp/map_bg2_trans.tra~
	APPEND_FILE ~EET/temp/map_tob_trans.tra~
	LPF fix_table END
	LPM read_trans_array_from_2da

LAM merge_in_translations

COPY_EXISTING ~worldmap.wmp~ ~override~
	READ_LONG 0xc mo
	READ_LONG mo + 0x20 na
	READ_LONG mo + 0x24 ao
	READ_LONG mo + 0x28 lo
	READ_LONG mo + 0x2c nl
	FOR (i = 0; i < nl; ++i) BEGIN
		DELETE_BYTES lo 0xd8
	END
	FOR (i = 0; i < na; ++i) BEGIN
		DELETE_BYTES ao 0xf0
		SET lo = lo - 0xf0
	END
	WRITE_LONG mo + 0x20 0
	WRITE_LONG mo + 0x24 ao
	WRITE_LONG mo + 0x28 lo
	WRITE_LONG mo + 0x2c 0
BUT_ONLY

INCLUDE ~EET/lib/build_worldmap.tpa~

//SOD BG Worldmap

COPY ~%patch_dir%/BGMAP.wmp~ ~override~
	LPF ~EET_expand_tlk~ INT_VAR max = StrRef_cutoff RET log END

/////                                                  \\\\\
///// Iron Crisis (item shattering)                    \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ARRAY eet_ironCrisis_array BEGIN 
AX1H01.ITM
AX1H04.ITM
BLUN02.ITM
BLUN04.ITM
BLUN06.ITM
BLUN08.ITM
DAGG01.ITM
DAGG06.ITM
DAGG07.ITM
HALB01.ITM
HAMM01.ITM
SPER01.ITM
SW1H01.ITM
SW1H04.ITM
SW1H07.ITM
SW1H12.ITM
SW1H17.ITM
SW1H20.ITM
SW1H43.ITM
SW1H46.ITM
SW1H48.ITM
SW2H01.ITM
/*SHLD01.ITM
SHLD03.ITM
SHLD05.ITM
SHLD08.ITM
SHLD09.ITM
SHLD10.ITM
SHLD11.ITM
SHLD12.ITM
SHLD13.ITM
SHLD14.ITM
SHLD15.ITM
SHLD16.ITM
SHLD01A.ITM
SHLD03A.ITM
SHLD05A.ITM
SHLD08A.ITM
SHLD09A.ITM
SHLD10A.ITM
SHLD11A.ITM
SHLD12A.ITM
SHLD13A.ITM
SHLD14A.ITM
SHLD15A.ITM
SHLD16A.ITM
CHAN01.ITM
CHAN04.ITM
PLAT01.ITM
PLAT04.ITM
HELM01.ITM
HELM08.ITM
HELM09.ITM
HELM10.ITM
HELM11.ITM
HELM12.ITM
HELM13.ITM
HELM15.ITM*/
END
LAF ~EET_IRON_CRISIS~
	STR_VAR
	shattering_array = EVAL ~eet_ironCrisis_array~ //array with files to patch (supports: weapons, armors, shields, helmets)
END

/////                                                  \\\\\
///// EET transition                                   \\\\\
/////                                                  \\\\\

INCLUDE ~EET/lib/transition.tph~

/////                                                  \\\\\
///// Fixpack                                          \\\\\
/////                                                  \\\\\

INCLUDE ~EET/lib/bgee_fixpack.tph~

/////                                                  \\\\\
///// Exe patching                                     \\\\\
/////                                                  \\\\\

COPY ~Baldur%exe%~ ~Baldur%exe%~
	REPLACE_TEXTUALLY ~NITEDAY~ ~~ (7)
	REPLACE_TEXTUALLY ~DAYNITE~ ~~ (7)
	REPLACE_TEXTUALLY ~SPIN649~ ~~ (7)
	REPLACE_TEXTUALLY ~SPIN822~ ~~ (7)
	//change default campaign
	REPLACE_TEXTUALLY ~SOD~ ~BG1~
	REPLACE_TEXTUALLY ~Never Show Nuisance BG1~ ~Never Show Nuisance SOD~
	REPLACE_TEXTUALLY ~Edition/BG1save~ ~Edition/sodsave~
	REPLACE_TEXTUALLY ~usingBG1StartMenu~ ~usingSODStartMenu~
BUT_ONLY

/////                                                  \\\\\
///// Biffing                                          \\\\\
/////                                                  \\\\\

PRINT ~Biffing graphic resources and wav files~

MAKE_BIFF ~eetTU00~ BEGIN ~%biff_dir%/eetTU00~ ~^.*$~ END
MAKE_BIFF ~eetOH2~ BEGIN ~%biff_dir%/eetOH2~ ~^.*$~ END
MAKE_BIFF ~eetOH3~ BEGIN ~%biff_dir%/eetOH3~ ~^.*$~ END
MAKE_BIFF ~eetOH9~ BEGIN ~%biff_dir%/eetOH9~ ~^.*$~ END
MAKE_BIFF ~eetBD0~ BEGIN ~%biff_dir%/eetBD0~ ~^.*$~ END
MAKE_BIFF ~eetBD1~ BEGIN ~%biff_dir%/eetBD1~ ~^.*$~ END
MAKE_BIFF ~eetBD2~ BEGIN ~%biff_dir%/eetBD2~ ~^.*$~ END
MAKE_BIFF ~eetBD3~ BEGIN ~%biff_dir%/eetBD3~ ~^.*$~ END
MAKE_BIFF ~eetBD4~ BEGIN ~%biff_dir%/eetBD4~ ~^.*$~ END
MAKE_BIFF ~eetBD5~ BEGIN ~%biff_dir%/eetBD5~ ~^.*$~ END
MAKE_BIFF ~eetBD6~ BEGIN ~%biff_dir%/eetBD6~ ~^.*$~ END
MAKE_BIFF ~eetBD7~ BEGIN ~%biff_dir%/eetBD7~ ~^.*$~ END
MAKE_BIFF ~eetBG00~ BEGIN ~%biff_dir%/eetBG00~ ~^.*$~ END
MAKE_BIFF ~eetBG01~ BEGIN ~%biff_dir%/eetBG01~ ~^.*$~ END
MAKE_BIFF ~eetBG02~ BEGIN ~%biff_dir%/eetBG02~ ~^.*$~ END
MAKE_BIFF ~eetBG03~ BEGIN ~%biff_dir%/eetBG03~ ~^.*$~ END
MAKE_BIFF ~eetBG04~ BEGIN ~%biff_dir%/eetBG04~ ~^.*$~ END
MAKE_BIFF ~eetBG05~ BEGIN ~%biff_dir%/eetBG05~ ~^.*$~ END
MAKE_BIFF ~eetBG06~ BEGIN ~%biff_dir%/eetBG06~ ~^.*$~ END
MAKE_BIFF ~eetBG07~ BEGIN ~%biff_dir%/eetBG07~ ~^.*$~ END
MAKE_BIFF ~eetBG08~ BEGIN ~%biff_dir%/eetBG08~ ~^.*$~ END
MAKE_BIFF ~eetBG09~ BEGIN ~%biff_dir%/eetBG09~ ~^.*$~ END
MAKE_BIFF ~eetBG10~ BEGIN ~%biff_dir%/eetBG10~ ~^.*$~ END
MAKE_BIFF ~eetBG11~ BEGIN ~%biff_dir%/eetBG11~ ~^.*$~ END
MAKE_BIFF ~eetBG12~ BEGIN ~%biff_dir%/eetBG12~ ~^.*$~ END
MAKE_BIFF ~eetBG13~ BEGIN ~%biff_dir%/eetBG13~ ~^.*$~ END
MAKE_BIFF ~eetBG14~ BEGIN ~%biff_dir%/eetBG14~ ~^.*$~ END
MAKE_BIFF ~eetBG15~ BEGIN ~%biff_dir%/eetBG15~ ~^.*$~ END
MAKE_BIFF ~eetBG16~ BEGIN ~%biff_dir%/eetBG16~ ~^.*$~ END
MAKE_BIFF ~eetBG17~ BEGIN ~%biff_dir%/eetBG17~ ~^.*$~ END
MAKE_BIFF ~eetBG18~ BEGIN ~%biff_dir%/eetBG18~ ~^.*$~ END
MAKE_BIFF ~eetBG19~ BEGIN ~%biff_dir%/eetBG19~ ~^.*$~ END
MAKE_BIFF ~eetBG20~ BEGIN ~%biff_dir%/eetBG20~ ~^.*$~ END
MAKE_BIFF ~eetBG21~ BEGIN ~%biff_dir%/eetBG21~ ~^.*$~ END
MAKE_BIFF ~eetBG22~ BEGIN ~%biff_dir%/eetBG22~ ~^.*$~ END
MAKE_BIFF ~eetBG23~ BEGIN ~%biff_dir%/eetBG23~ ~^.*$~ END
MAKE_BIFF ~eetBG24~ BEGIN ~%biff_dir%/eetBG24~ ~^.*$~ END
MAKE_BIFF ~eetBG26~ BEGIN ~%biff_dir%/eetBG26~ ~^.*$~ END
MAKE_BIFF ~eetBG27~ BEGIN ~%biff_dir%/eetBG27~ ~^.*$~ END
MAKE_BIFF ~eetBG28~ BEGIN ~%biff_dir%/eetBG28~ ~^.*$~ END
MAKE_BIFF ~eetBG29~ BEGIN ~%biff_dir%/eetBG29~ ~^.*$~ END
MAKE_BIFF ~eetBG30~ BEGIN ~%biff_dir%/eetBG30~ ~^.*$~ END
MAKE_BIFF ~eetBG31~ BEGIN ~%biff_dir%/eetBG31~ ~^.*$~ END
MAKE_BIFF ~eetBG32~ BEGIN ~%biff_dir%/eetBG32~ ~^.*$~ END
MAKE_BIFF ~eetBG33~ BEGIN ~%biff_dir%/eetBG33~ ~^.*$~ END
MAKE_BIFF ~eetBG34~ BEGIN ~%biff_dir%/eetBG34~ ~^.*$~ END
MAKE_BIFF ~eetBG35~ BEGIN ~%biff_dir%/eetBG35~ ~^.*$~ END
MAKE_BIFF ~eetBG36~ BEGIN ~%biff_dir%/eetBG36~ ~^.*$~ END
MAKE_BIFF ~eetBG37~ BEGIN ~%biff_dir%/eetBG37~ ~^.*$~ END
MAKE_BIFF ~eetBG38~ BEGIN ~%biff_dir%/eetBG38~ ~^.*$~ END
MAKE_BIFF ~eetBG39~ BEGIN ~%biff_dir%/eetBG39~ ~^.*$~ END
MAKE_BIFF ~eetBG40~ BEGIN ~%biff_dir%/eetBG40~ ~^.*$~ END
MAKE_BIFF ~eetBG41~ BEGIN ~%biff_dir%/eetBG41~ ~^.*$~ END
MAKE_BIFF ~eetBG42~ BEGIN ~%biff_dir%/eetBG42~ ~^.*$~ END
MAKE_BIFF ~eetBG43~ BEGIN ~%biff_dir%/eetBG43~ ~^.*$~ END
MAKE_BIFF ~eetBG44~ BEGIN ~%biff_dir%/eetBG44~ ~^.*$~ END
MAKE_BIFF ~eetBG45~ BEGIN ~%biff_dir%/eetBG45~ ~^.*$~ END
MAKE_BIFF ~eetBG46~ BEGIN ~%biff_dir%/eetBG46~ ~^.*$~ END
MAKE_BIFF ~eetBG47~ BEGIN ~%biff_dir%/eetBG47~ ~^.*$~ END
MAKE_BIFF ~eetBG48~ BEGIN ~%biff_dir%/eetBG48~ ~^.*$~ END
MAKE_BIFF ~eetBG49~ BEGIN ~%biff_dir%/eetBG49~ ~^.*$~ END
MAKE_BIFF ~eetBG50~ BEGIN ~%biff_dir%/eetBG50~ ~^.*$~ END
MAKE_BIFF ~eetBG51~ BEGIN ~%biff_dir%/eetBG51~ ~^.*$~ END
MAKE_BIFF ~eetBG52~ BEGIN ~%biff_dir%/eetBG52~ ~^.*$~ END
MAKE_BIFF ~eetBG53~ BEGIN ~%biff_dir%/eetBG53~ ~^.*$~ END
MAKE_BIFF ~eetBG54~ BEGIN ~%biff_dir%/eetBG54~ ~^.*$~ END
MAKE_BIFF ~eetBG55~ BEGIN ~%biff_dir%/eetBG55~ ~^.*$~ END
MAKE_BIFF ~eetBG56~ BEGIN ~%biff_dir%/eetBG56~ ~^.*$~ END
MAKE_BIFF ~eetBG57~ BEGIN ~%biff_dir%/eetBG57~ ~^.*$~ END
MAKE_BIFF ~eetBG58~ BEGIN ~%biff_dir%/eetBG58~ ~^.*$~ END
MAKE_BIFF ~eetBG59~ BEGIN ~%biff_dir%/eetBG59~ ~^.*$~ END
MAKE_BIFF ~eetBG60~ BEGIN ~%biff_dir%/eetBG60~ ~^.*$~ END
MAKE_BIFF ~eetBG61~ BEGIN ~%biff_dir%/eetBG61~ ~^.*$~ END
MAKE_BIFF ~eetBGGUI~ BEGIN ~%biff_dir%/eetBGGUI~ ~^.*$~ END
MAKE_BIFF ~eetBGWAV~ BEGIN ~EET/temp/wav~ ~^.*$~ END
MAKE_BIFF ~eetBGWBM~ BEGIN ~EET/temp/wbm~ ~^.*$~ END

OUTER_SET currentTotal = 0
OUTER_SET currentFile = 0
MKDIR ~%biff_dir%/eetBGM0~
OUTER_SET biff_temp = currentFile
OUTER_WHILE (biff_temp > 0) BEGIN
	OUTER_SET biff_temp = biff_temp - 1
	MAKE_BIFF ~eetBGM%biff_temp%~ BEGIN ~%biff_dir%/eetBGM%biff_temp%~ ~^.*$~ END
END
ACTION_BASH_FOR ~%biff_dir%~ ~^.*$~ BEGIN
	ACTION_IF ( BASH_FOR_SIZE + currentTotal > 30000000 /* 30M */ && currentTotal > 0 ) BEGIN
		MAKE_BIFF ~eetBGM%currentFile%~ BEGIN ~%biff_dir%/eetBGM%currentFile%~ ~^.*$~ END
		OUTER_SET currentFile = currentFile + 1
		OUTER_SET currentTotal = 0
		MKDIR ~%biff_dir%/eetBGM%currentFile%~
	END
	MOVE + ~%BASH_FOR_FILESPEC%~ ~%biff_dir%/eetBGM%currentFile%~
	OUTER_SET currentTotal = currentTotal + BASH_FOR_SIZE
END
ACTION_IF ( currentTotal > 0 ) BEGIN
	MAKE_BIFF ~eetBGM%currentFile%~ BEGIN ~%biff_dir%/eetBGM%currentFile%~ ~^.*$~ END
END

/////                                                  \\\\\
///// Finalise installation                            \\\\\
/////                                                  \\\\\

COPY ~.../blank.txt~ ~override/EET.flag~
APPEND ~EET.flag~ ~EET_JOURNAL~
PRINT ~'EET.flag' file is used by weidu to recognize the installation as EET~

COPY ~%WEIDU_EXECUTABLE%~ ~setup-EET_end%exe%~
	~EET/lib/EET_end.tp2~ ~setup-EET_end.tp2~

/*ACTION_IF (FILE_EXISTS ~EET/lang/%LANGUAGE%/readme.html~) BEGIN
	AT_INTERACTIVE_EXIT ~VIEW EET%os_slash%lang%os_slash%%LANGUAGE%%os_slash%readme.html~
END ELSE BEGIN
	AT_INTERACTIVE_EXIT ~VIEW EET%os_slash%docs%os_slash%[en_US]readme.html~
END*/
